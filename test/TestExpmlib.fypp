#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module TestExpmlib
    ! Fortran Standard Library.
    use iso_fortran_env
    use stdlib_math, only: is_close, all_close
    use stdlib_linalg, only: eye, diag
    use stdlib_io_npy, only: save_npy
    ! Testdrive
    use testdrive, only: new_unittest, unittest_type, error_type, check
    ! LightKrylov
    use LightKrylov
    use LightKrylov_Constants
    use LightKrylov_Logger
    use LightKrylov_Utils, only : eig, sqrtm
    ! Test Utilities
    use LightKrylov_TestUtils

    implicit none

    private

    character(len=*), parameter, private :: this_module      = 'LK_TExpmLib'
    character(len=*), parameter, private :: this_module_long = 'LightKrylov_TestExpmLib'

    #:for kind, type in RC_KINDS_TYPES
    public :: collect_expm_${type[0]}$${kind}$_testsuite
    #:endfor

    #:for kind, type in RC_KINDS_TYPES
    public :: collect_sqrtm_${type[0]}$${kind}$_testsuite
    #:endfor

contains

    !--------------------------------------------------------------
    !-----     UNIT TESTS FOR DENSE MATRIX EXPONENTIATION     -----
    !--------------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_expm_${type[0]}$${kind}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
                        new_unittest("Dense expm.", test_dense_expm_${type[0]}$${kind}$), &
                        new_unittest("Krylov expm.", test_kexptA_${type[0]}$${kind}$) &
                    ]

        return
    end subroutine collect_expm_${type[0]}$${kind}$_testsuite

    subroutine test_dense_expm_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Problem dimension.
        integer, parameter :: n = 5, m = 6
        ! Test matrix.
        ${type}$ :: A(n, n), E(n, n), Eref(n, n)
        integer :: i, j
        real(${kind}$) :: err
        character(len=256) :: msg

        ! Initialize matrix.
        A = 0.0_${kind}$
        do i = 1, n-1
            A(i, i+1) = m*1.0_${kind}$
        enddo

        ! Reference with analytical exponential.
        Eref = eye(n, mold=1.0_${kind}$)
        do i = 1, n-1
            do j = 1, n-i
                Eref(i, i+j) = Eref(i, i+j-1)*m/j
            enddo
        enddo

        ! Compute matrix exponential.
        E = expm(A)

        ! Check result.
        err = maxval(abs(E-Eref))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_dense_expm_${type[0]}$${kind}$', eq='maxval(abs(E-Eref))', context=msg)

        return
    end subroutine test_dense_expm_${type[0]}$${kind}$

    subroutine test_kexptA_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test matrix.
        class(linop_${type[0]}$${kind}$), allocatable :: A
        ! Basis vectors.
        class(vector_${type[0]}$${kind}$), allocatable :: Q, Xref, Xkryl
        ! Krylov subspace dimension.
        integer, parameter :: kdim = test_size
    
        ! ----- Internal variables -----
        ${type}$, allocatable :: E(:, :)
        real(${kind}$), parameter :: tau = 0.1_${kind}$
        integer, parameter :: nkmax = 64
        real(${kind}$) :: err
        integer :: info
        character(len=256) :: msg
        
        ! Initialize data.
        A = linop_${type[0]}$${kind}$() ; call init_rand(A)
        allocate(Q) ; call init_rand(Q)
        allocate(Xref) ; call Xref%zero()
        allocate(XKryl) ; call Xkryl%zero()

        ! Dense computation.
        allocate(E(kdim, kdim)) ; E = expm(tau*A%data)
        Xref%data = matmul(E, Q%data)

        ! Krylov exponential.
        call kexpm(Xkryl, A, Q, tau, rtol_${kind}$, info, kdim=nkmax)
        call check_info(info, 'kexpm', module=this_module_long, procedure='test_kexptA_${type[0]}$${kind}$')

        ! Check result.
        call Xkryl%sub(Xref) ; err = Xkryl%norm()
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_kexptA_${type[0]}$${kind}$', &
                                 & eq='Comparison with dense matrix exponential', context=msg)

        return
    end subroutine test_kexptA_${type[0]}$${kind}$

    subroutine test_block_kexptA_${type[0]}$${kind}$(error)
        !! This function tests the Krylov based approximation of the action of the exponential
        !! propagator against the dense computation for a random operator, a random RHS and a 
        !! typical value of tau.

        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        class(linop_${type[0]}$${kind}$), allocatable :: A
        ! Basis vectors.
        class(vector_${type[0]}$${kind}$), allocatable :: B(:)
        class(vector_${type[0]}$${kind}$), allocatable :: Cref(:)
        class(vector_${type[0]}$${kind}$), allocatable :: C(:), Cblk(:)
        ! Krylov subspace dimension.
        integer, parameter :: kdim = test_size
        ! Test matrix.
        ${type}$, allocatable :: Adata(:, :), Edata(:, :)
        ! Information flag.
        integer :: info
        ! Test parameters
        integer       , parameter :: nkmax = 15
        integer       , parameter :: p = 3
        real(${kind}$), parameter :: tau = 0.1_${kind}$
        real(${kind}$), parameter :: tol = rtol_${kind}$
        ! Misc.
        integer  :: i, j
        ${type}$, allocatable :: Xdata(:, :), Qdata(:, :)
        real(${kind}$) :: errv
        ${type}$ :: err(p, p)
        character(len=256) :: msg

        allocate(Adata(kdim, kdim)) ; Adata = 0.0_${kind}$
        allocate(Edata(kdim, kdim)) ; Edata = 0.0_${kind}$
        allocate(Xdata(test_size, p)) ; Xdata = 0.0_${kind}$

        allocate(Cref(p)) ; call initialize_krylov_subspace(Cref)
        allocate(C(p))    ; call initialize_krylov_subspace(C)
        allocate(Cblk(p)) ; call initialize_krylov_subspace(Cblk)

        ! --> Initialize operator.
        A = linop_${type[0]}$${kind}$() ; call init_rand(A) ; call get_data(Adata, A)
       
        ! --> Initialize rhs.
        allocate(B(1:p)) ; call init_rand(B)
        allocate(Qdata(test_size, p)) ; call get_data(Qdata, B)

        ! Comparison is dense computation (10th order Pade approximation)
        Edata = expm(tau*Adata) ; Xdata = matmul(Edata,Qdata) ; call put_data(Cref, Xdata)

        ! Compute Krylov matrix exponential using sequential arnoldi method for each input column
        do i = 1,p
            call kexpm(C(i), A, B(i), tau, tol, info, kdim=nkmax)
            call check_info(info, 'kexpm', module=this_module_long, procedure='test_block_kexptA_${type[0]}$${kind}$, 1')
        end do
        
        ! Compute Krylov matrix exponential using block-arnoldi method
        call kexpm(Cblk, A, B, tau, tol, info, kdim=nkmax)
        call check_info(info, 'kexpm', module=this_module_long, procedure='test_block_kexptA_${type[0]}$${kind}$, 2')
    
        do i = 1, p
            write(output_unit, *) C(i)%norm(), Cblk(i)%norm()
            call C(i)%sub(Cref(i)) ; call Cblk(i)%sub(Cref(i))
        end do

        ! Compute 2-norm of the error
        
        do i = 1, size(C)
            do j = 1, size(C)
                err(i, j) = C(i)%dot(C(j))
            enddo
        enddo
        errv = norm2(abs(err))
        !call get_err_str(msg, "max err: ", errv)
 
        do i = 1, size(Cblk)
            do j = 1, size(Cblk)
                err(i, j) = Cblk(i)%dot(Cblk(j))
            enddo
        enddo
        errv = norm2(abs(err))
        call get_err_str(msg, "max err: ", errv)

        call check(error, errv < rtol_${kind}$)
        call check_test(error, 'test_block_kexptA_${type[0]}$${kind}$', &
                        & eq='Comparison with dense matrix exponential', context=msg)

        return
    end subroutine test_block_kexptA_${type[0]}$${kind}$

    #:endfor
    !-----------------------------------------------------------
    !-----     UNIT TESTS FOR DENSE MATRIX SQUARE ROOT     -----
    !-----------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_sqrtm_${type[0]}$${kind}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
                        new_unittest("Dense sqrtm for positive definite matrices.", test_dense_sqrtm_pos_def_${type[0]}$${kind}$), &
                        new_unittest("Dense sqrtm for positive semi-definite matrices.", test_dense_sqrtm_pos_semi_def_${type[0]}$${kind}$) &
                    ]

        return
    end subroutine collect_sqrtm_${type[0]}$${kind}$_testsuite

    subroutine test_dense_sqrtm_pos_def_${type[0]}$${kind}$(error)
       !> This function tests the matrix version of the sqrt function for the case of
       ! a symmetric/hermitian positive definite matrix
    
       !> Error type to be returned.
       type(error_type), allocatable, intent(out) :: error
       !> Problem dimension.
       integer, parameter :: n = 5
       !> Test matrix.
       ${type}$ :: A(n, n)
       ${type}$ :: sqrtmA(n, n)
       complex(${kind}$) :: lambda(n)
       real(${kind}$) :: err
       integer :: i, info
       character(len=256) :: msg
    
       ! --> Initialize matrix.
       #:if type[0] == "r"
       call random_number(A)
       #:else
       call random_number(A%re)
       call random_number(A%im)
       #:endif
       ! make symmetric/hermitian positive definite
       #:if type[0] == "r"
       A = 0.5_${kind}$*(A + transpose(A))
       #:else
       A = 0.5_${kind}$*(A + conjg(transpose(A)))
       #:endif
       call eig(A, sqrtmA, lambda)
       do i = 1,n
          lambda(i) = abs(lambda(i)) + 0.1_${kind}$
       end do
       ! reconstruct matrix
       #:if type[0] == "r"
       A = matmul(sqrtmA, matmul(diag(abs(lambda)), transpose(sqrtmA)))
       #:else
       A = matmul(sqrtmA, matmul(diag(abs(lambda)), conjg(transpose(sqrtmA))))
       #:endif
     
       ! compute matrix square root
       call sqrtm(A, sqrtmA, info)
       call check_info(info, 'sqrtm', module=this_module_long, procedure='test_dense_sqrtm_pos_def_${type[0]}$${kind}$')
    
       #:if type[0] == "r"
       err = maxval(matmul(sqrtmA, sqrtmA) - A)
       call get_err_str(msg, "max err: ", err)
       call check(error, err < rtol_${kind}$)
       #:else
       err =  maxval(abs(matmul(sqrtmA, sqrtmA) - A))
       call get_err_str(msg, "max err: ", err)
       call check(error, err < rtol_${kind}$)
       #:endif
       call check_test(error, 'test_dense_sqrtm_pos_def_${type[0]}$${kind}$', eq='sqrt(A)**2 = A', context=msg)
    
       return
    end subroutine test_dense_sqrtm_pos_def_${type[0]}$${kind}$
    
    subroutine test_dense_sqrtm_pos_semi_def_${type[0]}$${kind}$(error)
       !> This function tests the matrix version of the sqrt function for the case 
       ! of a symmetric semi-definite matrix
    
       !> Error type to be returned.
       type(error_type), allocatable, intent(out) :: error
       !> Problem dimension.
       integer, parameter :: n = 25
       !> Test matrix.
       ${type}$ :: A(n, n)
       ${type}$ :: sqrtmA(n, n)
       complex(${kind}$) :: lambda(n)
       real(${kind}$) :: err
       integer :: i, info
       character(len=256) :: msg
    
       ! --> Initialize matrix.
       #:if type[0] == "r"
       call random_number(A)
       #:else
       call random_number(A%re)
       call random_number(A%im)
       #:endif
       ! make symmetric/hermitian positive semi-definite
       #:if type[0] == "r"
       A = 0.5_${kind}$*(A + transpose(A))
       #:else
       A = 0.5_${kind}$*(A + conjg(transpose(A)))
       #:endif
       call eig(A, sqrtmA, lambda)
       do i = 1,n-1
          lambda(i) = abs(lambda(i)) + 0.1_${kind}$
       end do
       lambda(n) = zero_r${kind}$
       ! reconstruct matrix
       #:if type[0] == "r"
       A = matmul(sqrtmA, matmul(diag(abs(lambda)), transpose(sqrtmA)))
       #:else
       A = matmul(sqrtmA, matmul(diag(abs(lambda)), conjg(transpose(sqrtmA))))
       #:endif
    
       ! compute matrix square root
       call sqrtm(A, sqrtmA, info)
       call check_info(info, 'sqrtm', module=this_module_long, procedure='test_dense_sqrtm_pos_semi_def_${type[0]}$${kind}$')
    
       #:if type[0] == "r"
       err = maxval(matmul(sqrtmA, sqrtmA) - A)
       call get_err_str(msg, "max err: ", err)
       call check(error, err < rtol_${kind}$)
       #:else
       err = maxval(abs(matmul(sqrtmA, sqrtmA) - A))
       call get_err_str(msg, "max err: ", err)
       call check(error, err < rtol_${kind}$)
       #:endif
       call check_test(error, 'test_dense_sqrtm_pos_semi_def_${type[0]}$${kind}$', eq='sqrt(A)**2 = A', context=trim(msg))
    
       return
    end subroutine test_dense_sqrtm_pos_semi_def_${type[0]}$${kind}$

    #:endfor

end module

