#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module TestLinops
    ! Fortran Standard Library
    use stdlib_math, only: is_close, all_close

    ! LightKrylov
    use LightKrylov
    use LightKrylov_Constants
    
    ! Testdrive
    use testdrive, only: new_unittest, unittest_type, error_type, check
    use TestVectors

    implicit none
    
    private

    character*128, parameter, private :: this_module = 'LightKrylov_TestLinops'

    #:for kind, type in RC_KINDS_TYPES
    public :: collect_linop_${type[0]}$${kind}$_testsuite
    #:endfor

    #:for kind, type in RC_KINDS_TYPES
    type, extends(abstract_linop_${type[0]}$${kind}$), public :: linop_${type[0]}$${kind}$
        ${type}$, dimension(test_size, test_size) :: data = zero_${type[0]}$${kind}$
    contains
        private
        procedure, pass(self), public :: matvec  => matvec_${type[0]}$${kind}$
        procedure, pass(self), public :: rmatvec => rmatvec_${type[0]}$${kind}$
    end type

    #:if type[0] == "r"
    type, extends(abstract_sym_linop_${type[0]}$${kind}$), public :: spd_linop_${type[0]}$${kind}$
        ${type}$, dimension(test_size, test_size) :: data = zero_${type[0]}$${kind}$
    contains
        private
        procedure, pass(self), public :: matvec => sdp_matvec_${type[0]}$${kind}$
        procedure, pass(self), public :: rmatvec => sdp_matvec_${type[0]}$${kind}$
    end type
    #:else
    type, extends(abstract_hermitian_linop_${type[0]}$${kind}$), public :: hermitian_linop_${type[0]}$${kind}$
        ${type}$, dimension(test_size, test_size) :: data = zero_${type[0]}$${kind}$
    contains
        private
        procedure, pass(self), public :: matvec => hermitian_matvec_${type[0]}$${kind}$
        procedure, pass(self), public :: rmatvec => hermitian_matvec_${type[0]}$${kind}$
    end type
    #:endif

    #:endfor
contains

    !--------------------------------------------------------------------
    !-----     DEFINITIONS OF THE VARIOUS TYPE-BOUND PROCEDURES     -----
    !--------------------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine matvec_${type[0]}$${kind}$(self, vec_in, vec_out)
        class(linop_${type[0]}$${kind}$), intent(in)  :: self
        class(abstract_vector_${type[0]}$${kind}$)       , intent(in)  :: vec_in
        class(abstract_vector_${type[0]}$${kind}$)       , intent(out) :: vec_out

        select type(vec_in)
        type is(vector_${type[0]}$${kind}$)
            select type(vec_out)
            type is(vector_${type[0]}$${kind}$)

            vec_out%data = matmul(self%data, vec_in%data)

            end select
        end select

        return
    end subroutine matvec_${type[0]}$${kind}$

    subroutine rmatvec_${type[0]}$${kind}$(self, vec_in, vec_out)
        class(linop_${type[0]}$${kind}$), intent(in)  :: self
        class(abstract_vector_${type[0]}$${kind}$)       , intent(in)  :: vec_in
        class(abstract_vector_${type[0]}$${kind}$)       , intent(out) :: vec_out

        select type(vec_in)
        type is(vector_${type[0]}$${kind}$)
            select type(vec_out)
            type is(vector_${type[0]}$${kind}$)

            #:if type[0] == "c"
            vec_out%data = matmul(transpose(conjg(self%data)), vec_in%data)
            #:else
            vec_out%data = matmul(transpose(self%data), vec_in%data)
            #:endif

            end select
        end select

        return
    end subroutine rmatvec_${type[0]}$${kind}$

    #:if type[0] == "r"
    subroutine sdp_matvec_${type[0]}$${kind}$(self, vec_in, vec_out)
        class(spd_linop_${type[0]}$${kind}$), intent(in)  :: self
        class(abstract_vector_${type[0]}$${kind}$)       , intent(in)  :: vec_in
        class(abstract_vector_${type[0]}$${kind}$)       , intent(out) :: vec_out

        select type(vec_in)
        type is(vector_${type[0]}$${kind}$)
            select type(vec_out)
            type is(vector_${type[0]}$${kind}$)

            vec_out%data = matmul(self%data, vec_in%data)

            end select
        end select

        return
    end subroutine sdp_matvec_${type[0]}$${kind}$
    #:else
    subroutine hermitian_matvec_${type[0]}$${kind}$(self, vec_in, vec_out)
        class(hermitian_linop_${type[0]}$${kind}$), intent(in)  :: self
        class(abstract_vector_${type[0]}$${kind}$)       , intent(in)  :: vec_in
        class(abstract_vector_${type[0]}$${kind}$)       , intent(out) :: vec_out

        select type(vec_in)
        type is(vector_${type[0]}$${kind}$)
            select type(vec_out)
            type is(vector_${type[0]}$${kind}$)

            vec_out%data = matmul(self%data, vec_in%data)

            end select
        end select

        return
    end subroutine hermitian_matvec_${type[0]}$${kind}$
   #:endif

    #:endfor

    !---------------------------------------------------------
    !-----     DEFINITIONS OF THE VARIOUS UNIT TESTS     -----
    !---------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_linop_${type[0]}$${kind}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
                    new_unittest("Matrix-vector product", test_matvec_${type[0]}$${kind}$), &
                    new_unittest("Tranpose Matrix-vector product", test_rmatvec_${type[0]}$${kind}$),  &
                    new_unittest("Adjoint Matrix-vector product", test_adjoint_matvec_${type[0]}$${kind}$),  &
                    new_unittest("Tranpose Adjoint Matrix-vector product", test_adjoint_rmatvec_${type[0]}$${kind}$) &
                    ]
        return
    end subroutine collect_linop_${type[0]}$${kind}$_testsuite

    subroutine test_matvec_${type[0]}$${kind}$(error)
        ! Error to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test vectors.
        type(vector_${type[0]}$${kind}$), allocatable :: x, y
        ! Test LinOp.
        type(linop_${type[0]}$${kind}$), allocatable :: A
        #:if type[0] == "c"
        real(${kind}$), dimension(test_size, test_size, 2) :: Adata
        #:endif

        ! Initialize vectors.
        x = vector_${type[0]}$${kind}$() ; call x%rand()
        y = vector_${type[0]}$${kind}$() ; call y%rand()

        ! Initialize matrix.
        A = linop_${type[0]}$${kind}$()
        #:if type[0] == "c"
        call random_number(Adata) ; A%data%re = Adata(:, :, 1) ; A%data%im = Adata(:, :, 2)
        #:else
        call random_number(A%data)
        #:endif

        ! Compute matrix-vector product.
        call A%matvec(x, y)

        ! Check result.
        #:if type[0] == "c"
        call check(error, norm2(abs(y%data - matmul(A%data, x%data))) < rtol_${kind}$)
        #:else
        call check(error, norm2(y%data - matmul(A%data, x%data)) < rtol_${kind}$)
        #:endif

        return
    end subroutine test_matvec_${type[0]}$${kind}$

    subroutine test_rmatvec_${type[0]}$${kind}$(error)
        ! Error to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test vectors.
        type(vector_${type[0]}$${kind}$), allocatable :: x, y
        ! Test LinOp.
        type(linop_${type[0]}$${kind}$), allocatable :: A
        #:if type[0] == "c"
        real(${kind}$), dimension(test_size, test_size, 2) :: Adata
        #:endif

        ! Initialize vectors.
        x = vector_${type[0]}$${kind}$() ; call x%rand()
        y = vector_${type[0]}$${kind}$() ; call y%rand()

        ! Initialize matrix.
        A = linop_${type[0]}$${kind}$()
        #:if type[0] == "c"
        call random_number(Adata) ; A%data%re = Adata(:, :, 1) ; A%data%im = Adata(:, :, 2)
        #:else
        call random_number(A%data)
        #:endif

        ! Compute matrix-vector product.
        call A%rmatvec(x, y)

        ! Check result.
        #:if type[0] == "c"
        call check(error, norm2(abs(y%data - matmul(transpose(conjg(A%data)), x%data))) < rtol_${kind}$)
        #:else
        call check(error, norm2(y%data - matmul(transpose(A%data), x%data)) < rtol_${kind}$)
        #:endif
        
        return
    end subroutine test_rmatvec_${type[0]}$${kind}$

    subroutine test_adjoint_matvec_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test vectors.
        type(vector_${type[0]}$${kind}$), allocatable :: x, y
        ! Test LinOp.
        type(linop_${type[0]}$${kind}$), allocatable :: A
        type(adjoint_linop_${type[0]}$${kind}$), allocatable :: B
        #:if type[0] == "c"
        real(${kind}$), dimension(test_size, test_size, 2) :: Adata
        #:endif

        ! Internal variable.
        real(${kind}$) :: alpha

        ! Initialize matrix.
        A = linop_${type[0]}$${kind}$()
        #:if type[0] == "c"
        call random_number(Adata) ; A%data%re = Adata(:, :, 1) ; A%data%im = Adata(:, :, 2)
        #:else
        call random_number(A%data)
        #:endif

        ! Adjoint operator.
        allocate(B) ; B%A = A

        ! Initialize vectors.
        x = vector_${type[0]}$${kind}$() ; call x%rand()
        y = vector_${type[0]}$${kind}$() ; call y%zero()

        ! Compute adjoint matrix-vector product
        call B%matvec(x, y)

        ! Check result.
        #:if type[0] == "c"
        alpha = norm2(abs(y%data - matmul(transpose(conjg(A%data)), x%data)))
        #:else
        alpha = norm2(y%data - matmul(transpose(A%data), x%data))
        #:endif

        call check(error, alpha < rtol_${kind}$)

       return
    end subroutine test_adjoint_matvec_${type[0]}$${kind}$

    subroutine test_adjoint_rmatvec_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test vectors.
        type(vector_${type[0]}$${kind}$), allocatable :: x, y
        ! Test LinOp.
        type(linop_${type[0]}$${kind}$), allocatable :: A
        type(adjoint_linop_${type[0]}$${kind}$), allocatable :: B
        #:if type[0] == "c"
        real(${kind}$), dimension(test_size, test_size, 2) :: Adata
        #:endif

        ! Internal variable.
        real(${kind}$) :: alpha

        ! Initialize matrix.
        A = linop_${type[0]}$${kind}$()
        #:if type[0] == "c"
        call random_number(Adata) ; A%data%re = Adata(:, :, 1) ; A%data%im = Adata(:, :, 2)
        #:else
        call random_number(A%data)
        #:endif

        ! Adjoint operator.
        allocate(B) ; B%A = A

        ! Initialize vectors.
        x = vector_${type[0]}$${kind}$() ; call x%rand()
        y = vector_${type[0]}$${kind}$() ; call y%zero()

        ! Compute adjoint matrix-vector product
        call B%rmatvec(x, y)

        ! Check result.
        #:if type[0] == "c"
        alpha = norm2(abs(y%data - matmul(A%data, x%data)))
        #:else
        alpha = norm2(y%data - matmul(A%data, x%data))
        #:endif

        call check(error, alpha < rtol_${kind}$)

       return
    end subroutine test_adjoint_rmatvec_${type[0]}$${kind}$


    #:endfor
end module TestLinops

