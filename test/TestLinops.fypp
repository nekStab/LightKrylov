#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module TestLinops
    ! Fortran Standard Library
    use stdlib_math, only: is_close, all_close
    use stdlib_linalg, only: norm, hermitian
    ! Testdrive
    use testdrive, only: new_unittest, unittest_type, error_type, check
    ! LightKrylov
    use LightKrylov
    use LightKrylov_Constants
    use LightKrylov_Logger
    ! Test Utilities
    use LightKrylov_TestUtils

    implicit none
    
    private

    character(len=*), parameter, private :: this_module      = 'LK_TLinops'
    character(len=*), parameter, private :: this_module_long = 'LightKrylov_TestLinops'
    integer, parameter :: n = 128

    #:for kind, type in RC_KINDS_TYPES
    public :: collect_linop_${type[0]}$${kind}$_testsuite
    #:endfor

contains

    !---------------------------------------------------------
    !-----     DEFINITIONS OF THE VARIOUS UNIT TESTS     -----
    !---------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_linop_${type[0]}$${kind}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
                    new_unittest("Matrix-vector product", test_matvec_${type[0]}$${kind}$), &
                    new_unittest("Tranpose Matrix-vector product", test_rmatvec_${type[0]}$${kind}$),  &
                    new_unittest("Adjoint Matrix-vector product", test_adjoint_matvec_${type[0]}$${kind}$),  &
                    new_unittest("Tranpose Adjoint Matrix-vector product", test_adjoint_rmatvec_${type[0]}$${kind}$) &
                    ]
        return
    end subroutine collect_linop_${type[0]}$${kind}$_testsuite

    subroutine test_matvec_${type[0]}$${kind}$(error)
        ! Error to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test vectors.
        type(dense_vector_${type[0]}$${kind}$) :: x, y
        ${type}$ :: x_(n), y_(n)
        ! Test LinOp.
        type(dense_linop_${type[0]}$${kind}$) :: A
        #:if type[0] == "c"
        real(${kind}$) :: Adata(n, n, 2)
        #:endif

        ! Initialize vectors.
        x = dense_vector(x_) ; call x%rand()
        y = dense_vector(y_) ; call y%zero()

        ! Initialize matrix.
        A = dense_linop_${type[0]}$${kind}$() ; allocate(A%data(n, n))
        #:if type[0] == "c"
        call random_number(Adata) ; A%data%re = Adata(:, :, 1) ; A%data%im = Adata(:, :, 2)
        #:else
        call random_number(A%data)
        #:endif

        ! Compute matrix-vector product.
        call A%apply_matvec(x, y)

        ! Check result.
        call check(error, norm(y%data - matmul(A%data, x%data), 2) < rtol_${kind}$)
        call check_test(error, 'test_matvec_${type[0]}$${kind}$', eq='norm(y - A @ x)')
        
        return
    end subroutine test_matvec_${type[0]}$${kind}$

    subroutine test_rmatvec_${type[0]}$${kind}$(error)
        ! Error to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test vectors.
        type(dense_vector_${type[0]}$${kind}$) :: x, y
        ${type}$ :: x_(n), y_(n)
        ! Test LinOp.
        type(dense_linop_${type[0]}$${kind}$) :: A
        #:if type[0] == "c"
        real(${kind}$) :: Adata(n, n, 2)
        #:endif

        ! Initialize vectors.
        x = dense_vector(x_) ; call x%rand()
        y = dense_vector(y_) ; call y%zero()

        ! Initialize matrix.
        allocate(A%data(n, n))
        #:if type[0] == "c"
        call random_number(Adata) ; A%data%re = Adata(:, :, 1) ; A%data%im = Adata(:, :, 2)
        #:else
        call random_number(A%data)
        #:endif

        ! Compute matrix-vector product.
        call A%apply_rmatvec(x, y)

        ! Check result.
        call check(error, norm(y%data - matmul(hermitian(A%data), x%data), 2) < rtol_${kind}$)
        call check_test(error, 'test_rmatvec_${type[0]}$${kind}$', eq='norm(y - A.H @ x)')
       
        return
    end subroutine test_rmatvec_${type[0]}$${kind}$

    subroutine test_adjoint_matvec_${type[0]}$${kind}$(error)
        ! Error to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test vectors.
        type(dense_vector_${type[0]}$${kind}$) :: x, y
        ${type}$ :: x_(n), y_(n)
        ! Test LinOp.
        type(dense_linop_${type[0]}$${kind}$) :: A
        type(adjoint_linop_${type[0]}$${kind}$), allocatable :: B
        #:if type[0] == "c"
        real(${kind}$) :: Adata(n, n, 2)
        #:endif

        ! Initialize vectors.
        x = dense_vector(x_) ; call x%rand()
        y = dense_vector(y_) ; call y%zero()

        ! Initialize matrix.
        allocate(A%data(n, n))
        #:if type[0] == "c"
        call random_number(Adata) ; A%data%re = Adata(:, :, 1) ; A%data%im = Adata(:, :, 2)
        #:else
        call random_number(A%data)
        #:endif

        ! Compute matrix-vector product.
        B = adjoint(A) ; call B%apply_matvec(x, y)

        ! Check result.
        call check(error, norm(y%data - matmul(hermitian(A%data), x%data), 2) < rtol_${kind}$)
        call check_test(error, 'test_adjoint_matvec_${type[0]}$${kind}$', eq='norm(y - adjoint(A) @ x)')

       return
    end subroutine test_adjoint_matvec_${type[0]}$${kind}$

    subroutine test_adjoint_rmatvec_${type[0]}$${kind}$(error)
        ! Error to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test vectors.
        type(dense_vector_${type[0]}$${kind}$) :: x, y
        ${type}$ :: x_(n), y_(n)
        ! Test LinOp.
        type(dense_linop_${type[0]}$${kind}$) :: A
        type(adjoint_linop_${type[0]}$${kind}$), allocatable :: B
        #:if type[0] == "c"
        real(${kind}$) :: Adata(n, n, 2)
        #:endif

        ! Initialize vectors.
        x = dense_vector(x_) ; call x%rand()
        y = dense_vector(y_) ; call y%zero()

        ! Initialize matrix.
        allocate(A%data(n, n))
        #:if type[0] == "c"
        call random_number(Adata) ; A%data%re = Adata(:, :, 1) ; A%data%im = Adata(:, :, 2)
        #:else
        call random_number(A%data)
        #:endif

        ! Compute matrix-vector product.
        B = adjoint(A) ; call B%apply_rmatvec(x, y)

        ! Check result.
        call check(error, norm(y%data - matmul(A%data, x%data), 2) < rtol_${kind}$)
        call check_test(error, 'test_adjoint_rmatvec_${type[0]}$${kind}$', eq='norm(y - A @ x)')

       return
    end subroutine test_adjoint_rmatvec_${type[0]}$${kind}$

    #:endfor
end module TestLinops

