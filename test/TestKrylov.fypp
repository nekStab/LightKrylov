#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module TestKrylov
    ! Fortran Standard Library.
    use iso_fortran_env
    use stdlib_math, only: is_close, all_close
    use stdlib_linalg, only: eye
    use stdlib_stats, only: median
    ! Testdrive
    use testdrive, only: new_unittest, unittest_type, error_type, check
    ! LightKrylov
    use LightKrylov
    use LightKrylov_Constants
    use LightKrylov_Logger
    use LightKrylov_AbstractVectors
    ! Test Utilities
    use LightKrylov_TestUtils

    implicit none
    
    private

    character(len=*), parameter, private :: this_module      = 'LK_TBKrylov'
    character(len=*), parameter, private :: this_module_long = 'LightKrylov_TestKrylov'

    #:for kind, type in RC_KINDS_TYPES
    public :: collect_qr_${type[0]}$${kind}$_testsuite
    public :: collect_arnoldi_${type[0]}$${kind}$_testsuite
    public :: collect_lanczos_bidiag_${type[0]}$${kind}$_testsuite
    public :: collect_lanczos_tridiag_${type[0]}$${kind}$_testsuite

    #:endfor

contains

    !----------------------------------------------------------------
    !-----     DEFINITIONS OF THE VARIOUS UNIT TESTS FOR QR     -----
    !----------------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_qr_${type[0]}$${kind}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
                        new_unittest("QR factorization", test_qr_factorization_${type[0]}$${kind}$), &
                        new_unittest("Pivoting QR for a rank deficient matrix", test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$) &
                    ]
        return
    end subroutine collect_qr_${type[0]}$${kind}$_testsuite

    subroutine test_qr_factorization_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test Vectors.
        integer, parameter :: kdim = test_size
        type(vector_${type[0]}$${kind}$), allocatable :: A(:)
        ! Upper triangular matrix.
        ${type}$ :: R(kdim, kdim) = zero_${type[0]}$${kind}$
        ! Information flag.
        integer :: info
        ! Miscellaneous.
        ${type}$, allocatable :: Adata(:, :), Qdata(:, :)
        ${type}$, allocatable :: G(:, :)
        real(${kind}$) :: err
        character(len=256) :: msg

        ! Initialiaze matrix.
        allocate(A(kdim)) ; call init_rand(A)

        ! Get data.
        allocate(Adata(test_size, kdim)) ; call get_data(Adata, A)

        ! In-place QR factorization.
        call qr(A, R, info, tol=atol_${kind}$)
        call check_info(info, 'qr', module=this_module_long, procedure='test_qr_factorization_${type[0]}$${kind}$')

        ! Get data.
        allocate(Qdata(test_size, kdim)) ; call get_data(Qdata, A)

        ! Check correctness.
        err = maxval(abs(Adata - matmul(Qdata, R)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_qr_factorization_${type[0]}$${kind}$', &
                              & info='Factorization', eq='A = Q @ R', context=msg)

        ! Compute Gram matrix associated to the Krylov basis.
        G = Gram(A(:kdim))

        ! Check orthonormality of the computed basis.
        err = norm2(abs(G - eye(kdim, mold=1.0_${kind}$)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_qr_factorization_${type[0]}$${kind}$', &
                              & info='Basis orthonormality', eq='Q.H @ Q = I', context=msg)

        return
    end subroutine test_qr_factorization_${type[0]}$${kind}$

    subroutine test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test basis.
        type(vector_${type[0]}$${kind}$), allocatable :: A(:)
        ! Krylov subspace dimension.
        integer, parameter :: kdim = 20
        ! Number of zero columns.
        integer, parameter :: nzero = 5
        ! Upper triangular matrix.
        ${type}$ :: R(kdim, kdim)
        ! Permutation vector.
        integer :: perm(kdim)
        ! Information flag.
        integer :: info
       
        ! Miscellaneous.
        integer :: k, idx, rk
        real(${kind}$) :: alpha
        logical :: mask(kdim)
        ${type}$, allocatable :: Adata(:, :), Qdata(:, :)
        ${type}$, allocatable :: G(:, :)
        real(${kind}$) :: err
        character(len=256) :: msg

        ! Effective rank.
        rk = kdim - nzero

        ! Initialize matrix.
        allocate(A(kdim)) ; call init_rand(A)

        ! Add zero vectors at random places.
        mask = .true. ; k = nzero
        do while (k > 0)
            call random_number(alpha)
            idx = 1 + floor(kdim*alpha)
            if (mask(idx)) then
                A(idx)%data = zero_${type[0]}$${kind}$
                mask(idx) = .false.
                k = k-1
            endif
        enddo

        ! Copy data.
        allocate(Adata(test_size, kdim)) ; call get_data(Adata, A)

        ! In-place QR factorization.
        call qr(A, R, perm, info, tol=atol_${kind}$)
        call check_info(info, 'qr_pivot', module=this_module_long, procedure='test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$')

        ! Extract data
        allocate(Qdata(test_size, kdim)) ; call get_data(Qdata, A)
        Adata = Adata(:, perm)

        ! Check correctness.
        err = maxval(abs(Adata - matmul(Qdata, R)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$', &
                              & info='Factorization', eq='A = Q @ R', context=msg)

        ! Compute Gram matrix associated to the Krylov basis.
        ! allocate(G(kdim, kdim)) ; G = zero_${type[0]}$${kind}$
        G = Gram(A(:kdim))

        ! Check orthonormality of the computed basis.
        err = norm2(abs(G - eye(kdim, mold=1.0_${kind}$)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$', &
                              & info='Basis orthonormality', eq='Q.H @ Q = I', context=msg)

        return
    end subroutine test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$

    #:endfor
    
    !--------------------------------------------------------------
    !-----     DEFINITIONS OF THE UNIT-TESTS FOR ARNOLDI      -----
    !--------------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_arnoldi_${type[0]}$${kind}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
            new_unittest("Arnoldi factorization", test_arnoldi_factorization_${type[0]}$${kind}$), &
            new_unittest("Block Arnoldi factorization", test_block_arnoldi_factorization_${type[0]}$${kind}$), &
            new_unittest("Krylov-Schur factorization", test_krylov_schur_${type[0]}$${kind}$) &
                    ]
        return
    end subroutine collect_arnoldi_${type[0]}$${kind}$_testsuite

    subroutine test_arnoldi_factorization_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test linear operator.
        type(linop_${type[0]}$${kind}$), allocatable :: A
        ! Krylov subspace.
        type(vector_${type[0]}$${kind}$), allocatable :: X(:)
        integer, parameter :: kdim = test_size
        ! Hessenberg matrix.
        ${type}$, allocatable :: H(:, :)
        ! Information flag.
        integer :: info
        ! Miscellaneous.
        ${type}$, allocatable :: Xdata(:, :)
        ${type}$, allocatable :: G(:, :)
        real(${kind}$) :: err
        character(len=256) :: msg
           
        ! Initialize linear operator.
        A = linop_${type[0]}$${kind}$() ; call init_rand(A)
        ! Initialize Krylov subspace.
        allocate(X(kdim+1)); call zero_basis(X); call X(1)%rand(ifnorm = .true.)
        allocate(H(kdim+1, kdim)) ; H = zero_${type[0]}$${kind}$
        ! Arnoldi factorization.
        call arnoldi(A, X, H, info, tol=atol_${kind}$)
        call check_info(info, 'arnoldi', module=this_module_long, procedure='test_arnoldi_factorization_${type[0]}$${kind}$')

        ! Check correctness of full factorization.
        allocate(Xdata(test_size, kdim+1)) ; call get_data(Xdata, X)
        err = maxval(abs(matmul(A%data, Xdata(:, :kdim)) - matmul(Xdata, H)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_arnoldi_factorization_${type[0]}$${kind}$', &
                              & info='Factorization', eq='A @ X = X_ @ H_', context=msg)


        ! Compute Gram matrix associated to the Krylov basis.
        ! allocate(G(kdim, kdim)) ; G = zero_${type[0]}$${kind}$
        G = Gram(X(:kdim))

        ! Check orthonormality of the computed basis.
        err = maxval(abs(G - eye(kdim, mold=1.0_${kind}$)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_arnoldi_factorization_${type[0]}$${kind}$', &
                              & info='Orthonomality', eq='X.H @ X = I', context=msg)

        return
    end subroutine test_arnoldi_factorization_${type[0]}$${kind}$

    subroutine test_block_arnoldi_factorization_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test Linear operator.
        type(linop_${type[0]}$${kind}$), allocatable :: A
        ! Krylov subspace.
        type(vector_${type[0]}$${kind}$), allocatable :: X(:)
        integer, parameter :: p = 2
        integer, parameter :: kdim = test_size/2
        ! Hessenberg matrix.
        ${type}$, allocatable :: H(:, :)
        ! Information flag.
        integer :: info
        ! Miscellaneous.
        type(vector_${type[0]}$${kind}$), allocatable :: X0(:)
        ${type}$, allocatable :: Xdata(:, :)
        ${type}$, allocatable :: G(:, :)
        real(${kind}$) :: err
        character(len=256) :: msg

        ! Initialize linear operator.
        A = linop_${type[0]}$${kind}$() ; call init_rand(A)

        ! Initialize Krylov subspace.
        allocate(X(p*(kdim+1))) ; allocate(X0(p))
        call init_rand(X0) ; call initialize_krylov_subspace(X, X0)
        allocate(H(p*(kdim+1), p*kdim)) ; H = zero_${type[0]}$${kind}$

        ! Arnoldi factorization.
        call arnoldi(A, X, H, info, blksize=p, tol=atol_${kind}$)
        call check_info(info, 'arnoldi', module=this_module_long, procedure='test_block_arnoldi_factorization_${type[0]}$${kind}$')

        ! Check correctness of full factorization.
        allocate(Xdata(test_size, p*(kdim+1))) ; call get_data(Xdata, X)
        err = maxval(abs(matmul(A%data, Xdata(:, :p*kdim)) - matmul(Xdata, H)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_block_arnoldi_factorization_${type[0]}$${kind}$', &
                              & info='Factorization', eq='A @ X = X_ @ H_', context=msg)

        ! Compute Gram matrix associated to the Krylov basis.
        ! allocate(G(p*kdim, p*kdim)) ; G = zero_${type[0]}$${kind}$
        G = Gram(X(:p*kdim))

        ! Check orthonormality of the computed basis.
        err = maxval(abs(G - eye(p*kdim, mold=1.0_${kind}$)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_block_arnoldi_factorization_${type[0]}$${kind}$', &
                              & info='Basis orthonormality', eq='X.H @ X = I', context=msg)

        return
    end subroutine test_block_arnoldi_factorization_${type[0]}$${kind}$

    subroutine test_krylov_schur_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test operator.
        type(linop_${type[0]}$${kind}$), allocatable :: A
        ! Krylov subspace.
        type(vector_${type[0]}$${kind}$), allocatable :: X(:)
        ! Krylov subspace dimension.
        integer, parameter :: kdim = 100
        ! Hessenberg matrix.
        ${type}$, allocatable :: H(:, :)
        ! Information flag.
        integer :: info
        ! Miscellaneous.
        integer :: n
        ${type}$, allocatable :: Xdata(:, :)
        real(${kind}$) :: err
        character(len=256) :: msg

        ! Initialize matrix.
        A = linop_${type[0]}$${kind}$() ; call init_rand(A)
        A%data = A%data / norm2(abs(A%data))

        ! Initialize Krylov subspace.
        allocate(X(kdim+1)); call zero_basis(X); call X(1)%rand(ifnorm = .true.)
        allocate(H(kdim+1, kdim)) ; H = zero_${type[0]}$${kind}$

        ! Arnoldi factorization.
        call arnoldi(A, X, H, info, tol=atol_${kind}$)
        call check_info(info, 'arnoldi', module=this_module_long, procedure='test_krylov_schur_${type[0]}$${kind}$')

        ! Krylov-Schur condensation.
        call krylov_schur(n, X, H, select_eigs)

        ! Check correctness.
        allocate(Xdata(test_size, kdim+1)) ; call get_data(Xdata, X)
        err = maxval(abs(matmul(A%data, Xdata(:, :n)) - matmul(Xdata(:, :n+1), H(:n+1, :n))))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_krylov_schur_${type[0]}$${kind}$', &
                              & info='Factorization', eq='A @ X = X_ @ H_', context=msg)

        return
    contains
        function select_eigs(eigvals) result(selected)
            complex(${kind}$), intent(in) :: eigvals(:)
            logical, allocatable :: selected(:)
            selected = abs(eigvals) > median(abs(eigvals))
        end function select_eigs
    end subroutine test_krylov_schur_${type[0]}$${kind}$

    #:endfor

    !------------------------------------------------------------------------------
    !-----     DEFINITION OF THE UNIT-TESTS FOR LANCZOS BIDIAGONALIZATION     -----
    !------------------------------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_lanczos_bidiag_${type[0]}$${kind}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
            new_unittest("Lanczos Bidiagonalization", test_lanczos_bidiag_factorization_${type[0]}$${kind}$) &
                ]
        return
    end subroutine collect_lanczos_bidiag_${type[0]}$${kind}$_testsuite

    subroutine test_lanczos_bidiag_factorization_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test Linear Operator
        type(linop_${type[0]}$${kind}$), allocatable :: A
        ! Left and right Krylov bases.
        type(vector_${type[0]}$${kind}$), allocatable :: U(:), V(:)
        ! Krylov subspace dimension.
        integer, parameter :: kdim = test_size
        ! Bidiagonal matrix.
        ${type}$, allocatable :: B(:, :)
        ! Information flag.
        integer :: info
        ! Miscellaneous.
        ${type}$, allocatable :: Udata(:, :), Vdata(:, :)
        ${type}$, allocatable :: G(:, :)
        real(${kind}$) :: err
        character(len=256) :: msg

        ! Initialize linear operator.
        A = linop_${type[0]}$${kind}$() ; call init_rand(A)

        ! Initialize Krylov subspaces.
        allocate(U(kdim+1), V(kdim+1), B(kdim+1,kdim))
        call zero_basis(U); call U(1)%rand(ifnorm = .true.)
        call zero_basis(V)
        B = zero_${type[0]}$${kind}$

        ! Lanczos bidiagonalization.
        call bidiagonalization(A, U, V, B, info, tol=atol_${kind}$)
        call check_info(info, 'bidiagonalization', module=this_module_long, &
                        & procedure='test_lanczos_bidiag_factorization_${type[0]}$${kind}$')

        ! Check correctness.
        allocate(Udata(test_size, kdim+1)) ; call get_data(Udata, U)
        allocate(Vdata(test_size, kdim+1)) ; call get_data(Vdata, V)

        err = maxval(abs(matmul(A%data, Vdata(:, :kdim)) - matmul(Udata, B)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_lanczos_bidiag_factorization_${type[0]}$${kind}$', &
                              & info='Factorization', eq='A @ V = U_ @ B_', context=msg)

        ! Compute Gram matrix associated to the left Krylov basis.
        G = Gram(U(:kdim))

        ! Check orthonormality of the left basis.
        err = maxval(abs(G - eye(kdim, mold=1.0_${kind}$)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_lanczos_bidiag_factorization_${type[0]}$${kind}$', &
                              & info='Basis orthonormality (left)', eq='U.H @ U = I', context=msg)

        ! Compute Gram matrix associated to the right Krylov basis.
        G = Gram(V(:kdim))

        ! Check orthonormality of the right basis.
        err = maxval(abs(G - eye(kdim, mold=1.0_${kind}$)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_lanczos_bidiag_factorization_${type[0]}$${kind}$', &
                              & info='Basis orthonormality (right)', eq='V.H @ V = I', context=msg)

        return
    end subroutine test_lanczos_bidiag_factorization_${type[0]}$${kind}$

   #:endfor

    !-------------------------------------------------------------------------------
    !-----     DEFINITION OF THE UNIT TESTS FOR LANCZOS TRIDIAGONALIZATION     -----
    !-------------------------------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_lanczos_tridiag_${type[0]}$${kind}$_testsuite(testsuite)
        ! Collection of unit tests.
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
             new_unittest("Lanczos Tridiagonalization", test_lanczos_tridiag_factorization_${type[0]}$${kind}$) &
            ]

        return
    end subroutine collect_lanczos_tridiag_${type[0]}$${kind}$_testsuite

    subroutine test_lanczos_tridiag_factorization_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test matrix.
        #:if type[0] == "r"
        type(spd_linop_${type[0]}$${kind}$), allocatable :: A
        #:else
        type(hermitian_linop_${type[0]}$${kind}$), allocatable :: A
        #:endif
        ! Krylov subspace.
        type(vector_${type[0]}$${kind}$), allocatable :: X(:)
        ! Krylov subspace dimension.
        integer, parameter :: kdim = test_size
        ! Tridiagonal matrix.
        ${type}$, allocatable :: T(:, :)
        ! Information flag.
        integer :: info

        ! Internal variables.
        ${type}$, allocatable :: Xdata(:, :)
        ${type}$, allocatable :: G(:, :)
        real(${kind}$) :: err
        character(len=256) :: msg

        ! Initialize tridiagonal matrix.
        allocate(T(kdim+1, kdim)) ; T = zero_${type[0]}$${kind}$

        ! Initialize operator.
        #:if type[0] == "r"
        A = spd_linop_${type[0]}$${kind}$()
        #:else
        A = hermitian_linop_${type[0]}$${kind}$()
        #:endif
        call init_rand(A)

        ! Initialize Krylov subspace.
        allocate(X(kdim+1)); call zero_basis(X); call X(1)%rand(ifnorm = .true.)

        ! Lanczos factorization.
        call lanczos(A, X, T, info, tol=atol_${kind}$)
        call check_info(info, 'lanczos', module=this_module_long, & 
                        & procedure='test_lanczos_tridiag_factorization_${type[0]}$${kind}$')

        ! Check correctness.
        allocate(Xdata(test_size, kdim+1)) ; call get_data(Xdata, X)

        ! Infinity-norm check.
        err = maxval(abs(matmul(A%data, Xdata(:, :kdim)) - matmul(Xdata, T)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_lanczos_tridiag_factorization_${type[0]}$${kind}$', &
                                 & info='Factorization', eq='A @ X = X_ @ T_', context=msg)

        ! Compute Gram matrix associated to the right Krylov basis.
        ! allocate(G(kdim, kdim)) ; G = zero_${type[0]}$${kind}$
        G = Gram(X(:kdim))

        ! Check orthonormality of the Krylov basis.
        err = maxval(abs(G - eye(kdim, mold=1.0_${kind}$)))
        call get_err_str(msg, "max err: ", err)
        call check(error, err < rtol_${kind}$)
        call check_test(error, 'test_lanczos_tridiag_factorization_${type[0]}$${kind}$', &
                                 & info='Orthonomality', eq='X.H @ X = I', context=msg)

        return
    end subroutine test_lanczos_tridiag_factorization_${type[0]}$${kind}$

    #:endfor
end module TestKrylov
