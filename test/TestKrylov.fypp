#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module TestKrylov
    ! Fortran Standard Library.
    use iso_fortran_env
    use stdlib_math, only: is_close, all_close
    use stdlib_linalg, only: eye
    use stdlib_stats, only: median

    ! LightKrylov
    use LightKrylov
    use LightKrylov_Constants
    use LightKrylov_Logger
    use LightKrylov_AbstractVectors

    ! Testdrive
    use testdrive, only: new_unittest, unittest_type, error_type, check
    use TestVectors
    use TestLinops
    use TestUtils

    implicit none
    
    private

    character*128, parameter, private :: this_module = 'LightKrylov_TestKrylov'

    #:for kind, type in RC_KINDS_TYPES
    public :: collect_qr_${type[0]}$${kind}$_testsuite
    public :: collect_arnoldi_${type[0]}$${kind}$_testsuite
    public :: collect_lanczos_bidiag_${type[0]}$${kind}$_testsuite
    public :: collect_lanczos_tridiag_${type[0]}$${kind}$_testsuite

    #:endfor

contains

    !----------------------------------------------------------------
    !-----     DEFINITIONS OF THE VARIOUS UNIT TESTS FOR QR     -----
    !----------------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_qr_${type[0]}$${kind}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
                        new_unittest("QR factorization (correctness & orthonormality)", test_qr_factorization_${type[0]}$${kind}$), &
                        new_unittest("Pivoting QR for a rank deficient matrix (correctness & orthonormality))", test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$) &
                    ]
        return
    end subroutine collect_qr_${type[0]}$${kind}$_testsuite

    subroutine test_qr_factorization_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test Vectors.
        integer, parameter :: kdim = test_size
        type(vector_${type[0]}$${kind}$), allocatable :: A(:)
        ! Upper triangular matrix.
        ${type}$ :: R(kdim, kdim) = zero_${type[0]}$${kind}$
        ! Information flag.
        integer :: info
        ! Miscellaneous.
        ${type}$, dimension(test_size, kdim) :: Adata, Qdata
        ${type}$, dimension(kdim, kdim) :: G, Id

        ! Initiliaze matrix.
        allocate(A(1:kdim)) ; call init_rand(A)

        ! Get data.
        call get_data(Adata, A)

        ! In-place QR factorization.
        call qr(A, R, info, tol=atol_${kind}$)
        call check_info(info, 'qr', module=this_module, procedure='test_qr_factorization_${type[0]}$${kind}$')

        ! Get data.
        call get_data(Qdata, A)

        ! Check correctness.
        call check(error, maxval(abs(Adata - matmul(Qdata, R))) < rtol_${kind}$)
        call check_test(error, 'test_qr_factorization_${type[0]}$${kind}$', &
                        & 'QR factorization not correct: A /= Q @ R')

        ! Compute Gram matrix associated to the Krylov basis.
        G = zero_${type[0]}$${kind}$
        call innerprod(G, A(1:kdim), A(1:kdim))

        ! Check orthonormality of the computed basis.
        Id = eye(kdim)
        call check(error, norm2(abs(G - Id)) < rtol_${kind}$)
        call check_test(error, 'test_qr_factorization_${type[0]}$${kind}$', &
                        & 'Basis Q not orthonormal: Q.H @ Q /= I')

        return
    end subroutine test_qr_factorization_${type[0]}$${kind}$

    subroutine test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test basis.
        type(vector_${type[0]}$${kind}$), allocatable :: A(:)
        ! Krylov subspace dimension.
        integer, parameter :: kdim = 20
        ! Number of zero columns.
        integer, parameter :: nzero = 5
        ! Upper triangular matrix.
        ${type}$ :: R(kdim, kdim)
        ! Permutation vector.
        integer :: perm(kdim)
        ! Information flag.
        integer :: info
       
        ! Miscellaneous.
        integer :: k, idx, rk
        real(${kind}$) :: alpha
        logical :: mask(kdim)
        ${type}$, dimension(test_size, kdim) :: Adata, Qdata
        ${type}$, dimension(kdim, kdim) :: G, Id

        ! Effective rank.
        rk = kdim - nzero

        ! Initialize matrix.
        allocate(A(1:kdim)) ; call init_rand(A)

        ! Add zero vectors at random places.
        mask = .true. ; k = nzero
        do while (k > 0)
            call random_number(alpha)
            idx = 1 + floor(kdim*alpha)
            if (mask(idx)) then
                A(idx)%data = zero_${type[0]}$${kind}$
                mask(idx) = .false.
                k = k-1
            endif
        enddo

        ! Copy data.
        call get_data(Adata, A)

        ! In-place QR factorization.
        call qr(A, R, perm, info, tol=atol_${kind}$)
        call check_info(info, 'qr_pivot', module=this_module, procedure='test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$')

        ! Extract data
        call get_data(Qdata, A)
        Adata = Adata(:, perm)

        ! Check correctness.
        call check(error, maxval(abs(Adata - matmul(Qdata, R))) < rtol_${kind}$)
        call check_test(error, 'test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$', &
                        & 'QR factorization not correct: A /= Q @ R')

        ! Compute Gram matrix associated to the Krylov basis.
        G = zero_${type[0]}$${kind}$
        call innerprod(G, A(1:kdim), A(1:kdim))

        ! Check orthonormality of the computed basis.
        Id = eye(kdim)
        call check(error, norm2(abs(G - Id)) < rtol_${kind}$)
        call check_test(error, 'test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$', &
                        & 'Basis Q not orthonormal: Q.H @ Q /= I')

        return
    end subroutine test_pivoting_qr_exact_rank_deficiency_${type[0]}$${kind}$

    #:endfor
    
    !--------------------------------------------------------------
    !-----     DEFINITIONS OF THE UNIT-TESTS FOR ARNOLDI      -----
    !--------------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_arnoldi_${type[0]}$${kind}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
            new_unittest("Arnoldi factorization (correctness & orthonormality)", test_arnoldi_factorization_${type[0]}$${kind}$), &
            new_unittest("Block Arnoldi factorization (correctness & orthonormality)", test_block_arnoldi_factorization_${type[0]}$${kind}$), &
            new_unittest("Krylov-Schur factorization", test_krylov_schur_${type[0]}$${kind}$) &
                    ]
        return
    end subroutine collect_arnoldi_${type[0]}$${kind}$_testsuite

    subroutine test_arnoldi_factorization_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test linear operator.
        type(linop_${type[0]}$${kind}$), allocatable :: A
        ! Krylov subspace.
        type(vector_${type[0]}$${kind}$), allocatable :: X(:)
        type(vector_${type[0]}$${kind}$), dimension(:), allocatable :: X0
        integer, parameter :: kdim = test_size
        ! Hessenberg matrix.
        ${type}$ :: H(kdim+1, kdim)
        ! Information flag.
        integer :: info
        ! Miscellaneous.
        ${type}$ :: Xdata(test_size, kdim+1)
        ${type}$, dimension(kdim, kdim) :: G, Id
           
        ! Initialize linear operator.
        A = linop_${type[0]}$${kind}$() ; call init_rand(A)
        ! Initialize Krylov subspace.
        allocate(X(1:kdim+1)) ; allocate (X0(1))
        call init_rand(X0) ; call initialize_krylov_subspace(X, X0)
        H = zero_${type[0]}$${kind}$
        ! Arnoldi factorization.
        call arnoldi(A, X, H, info, tol=atol_${kind}$)
        call check_info(info, 'arnoldi', module=this_module, procedure='test_arnoldi_factorization_${type[0]}$${kind}$')

        ! Check correctness of full factorization.
        call get_data(Xdata, X)
        call check(error, maxval(abs(matmul(A%data, Xdata(:, 1:kdim)) - matmul(Xdata, H))) < rtol_${kind}$)
        call check_test(error, 'test_arnoldi_factorization_${type[0]}$${kind}$', &
                        & 'Arnoldi factorization not correct: A @ X[:,1:k] /= X[:,1:k+1] @ H[1:k+1,1:k]')


        ! Compute Gram matrix associated to the Krylov basis.
        G = zero_${type[0]}$${kind}$
        call innerprod(G, X(1:kdim), X(1:kdim))

        ! Check orthonormality of the computed basis.
        Id = eye(kdim)
        call check(error, maxval(abs(G - Id)) < rtol_${kind}$)
        call check_test(error, 'test_arnoldi_factorization_${type[0]}$${kind}$', &
                        & 'Basis X not orthonormal: X[:,1:k].H @ X[:,1:k] /= I')

        return
    end subroutine test_arnoldi_factorization_${type[0]}$${kind}$

    subroutine test_block_arnoldi_factorization_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test Linear operator.
        type(linop_${type[0]}$${kind}$), allocatable :: A
        ! Krylov subspace.
        type(vector_${type[0]}$${kind}$), allocatable :: X(:)
        integer, parameter :: p = 2
        integer, parameter :: kdim = test_size/2
        ! Hessenberg matrix.
        ${type}$ :: H(p*(kdim+1), p*kdim)
        ! Information flag.
        integer :: info
        ! Miscellaneous.
        type(vector_${type[0]}$${kind}$), allocatable :: X0(:)
        ${type}$, dimension(test_size, p*(kdim+1)) :: Xdata
        ${type}$, dimension(p*kdim, p*kdim) :: G
        real(${kind}$), dimension(p*kdim, p*kdim) :: Id

        ! Initialize linear operator.
        A = linop_${type[0]}$${kind}$() ; call init_rand(A)

        ! Initialize Krylov subspace.
        allocate(X(1:p*(kdim+1))) ; allocate(X0(1:p))
        call init_rand(X0) ; call initialize_krylov_subspace(X, X0)
        H = zero_${type[0]}$${kind}$

        ! Arnoldi factorization.
        call arnoldi(A, X, H, info, blksize=p, tol=atol_${kind}$)
        call check_info(info, 'arnoldi', module=this_module, procedure='test_block_arnoldi_factorization_${type[0]}$${kind}$')

        ! Check correctness of full factorization.
        call get_data(Xdata, X)
        call check(error, maxval(abs(matmul(A%data, Xdata(:, 1:p*kdim)) - matmul(Xdata, H))) < rtol_${kind}$)
        call check_test(error, 'test_block_arnoldi_factorization_${type[0]}$${kind}$', &
                        & 'Arnoldi factorization not correct: A @ X[:,1:p*k] /= X[:,1:p*(k+1)] @ H[1:p*(k+1),1:k]')

        ! Compute Gram matrix associated to the Krylov basis.
        G = zero_${type[0]}$${kind}$
        call innerprod(G, X(1:p*kdim), X(1:p*kdim))

        ! Check orthonormality of the computed basis.
        Id = eye(p*kdim)
        call check(error, maxval(abs(G - Id)) < rtol_${kind}$)
        call check_test(error, 'test_block_arnoldi_factorization_${type[0]}$${kind}$', &
                        & 'Basis X not orthonormal: X[:,1:k].H @ X[:,1:k] /= I')

        return
    end subroutine test_block_arnoldi_factorization_${type[0]}$${kind}$

    subroutine test_krylov_schur_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test operator.
        type(linop_${type[0]}$${kind}$), allocatable :: A
        ! Krylov subspace.
        type(vector_${type[0]}$${kind}$), allocatable :: X(:), X0(:)
        ! Krylov subspace dimension.
        integer, parameter :: kdim = 100
        ! Hessenberg matrix.
        ${type}$ :: H(kdim+1, kdim)
        ! Information flag.
        integer :: info

        ! Miscellaneous.
        integer :: n
        ${type}$ :: Xdata(test_size, kdim+1)
        real(${kind}$) :: alpha

        ! Initialize matrix.
        A = linop_${type[0]}$${kind}$() ; call init_rand(A)
        A%data = A%data / norm2(abs(A%data))

        ! Initialize Krylov subspace.
        allocate(X(kdim+1)) ; allocate(X0(1))
        call init_rand(X0) ; call initialize_krylov_subspace(X, X0)
        H = zero_${type[0]}$${kind}$

        ! Arnoldi factorization.
        call arnoldi(A, X, H, info, tol=atol_${kind}$)
        call check_info(info, 'arnoldi', module=this_module, procedure='test_krylov_schur_${type[0]}$${kind}$')

        ! Krylov-Schur condensation.
        call krylov_schur(n, X, H, select_eigs)

        ! Check correctness.
        call get_data(Xdata, X)
        alpha = maxval(abs(matmul(A%data, Xdata(:, :n)) - matmul(Xdata(:, :n+1), H(:n+1, :n))))
        call check(error, alpha < rtol_${kind}$)
        call check_test(error, 'test_krylov_schur_${type[0]}$${kind}$', &
                        & 'Arnoldi factorization not correct: A @ X[:,1:k] /= X[:,1:k+1] @ H[1:k+1,1:k]')

        return
    contains
        function select_eigs(eigvals) result(selected)
            complex(${kind}$), intent(in) :: eigvals(:)
            logical                       :: selected(size(eigvals))
           
            selected = abs(eigvals) > median(abs(eigvals))
            return
        end function select_eigs
    end subroutine test_krylov_schur_${type[0]}$${kind}$

    #:endfor

    !------------------------------------------------------------------------------
    !-----     DEFINITION OF THE UNIT-TESTS FOR LANCZOS BIDIAGONALIZATION     -----
    !------------------------------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_lanczos_bidiag_${type[0]}$${kind}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
            new_unittest("Lanczos Bidiagonalization (correctness and left/right orthonormality)", test_lanczos_bidiag_factorization_${type[0]}$${kind}$) &
                ]
        return
    end subroutine collect_lanczos_bidiag_${type[0]}$${kind}$_testsuite

    subroutine test_lanczos_bidiag_factorization_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test Linear Operator
        type(linop_${type[0]}$${kind}$), allocatable :: A
        ! Left and right Krylov bases.
        type(vector_${type[0]}$${kind}$), allocatable :: U(:), V(:)
        ! Krylov subspace dimension.
        integer, parameter :: kdim = test_size
        ! Bidiagonal matrix.
        ${type}$ :: B(kdim+1, kdim)
        ! Information flag.
        integer :: info
        ! Miscellaneous.
        real(${kind}$) :: alpha
        ${type}$, dimension(test_size, kdim+1) :: Udata, Vdata
        ${type}$, dimension(kdim, kdim) :: G, Id
        type(vector_${type[0]}$${kind}$), allocatable :: X0(:)

        ! Initialize linear operator.
        A = linop_${type[0]}$${kind}$() ; call init_rand(A)

        ! Initialize Krylov subspaces.
        allocate(U(1:kdim+1)) ; allocate(V(1:kdim+1)) ; allocate(X0(1))
        call init_rand(X0) ; call initialize_krylov_subspace(U, X0)
        call initialize_krylov_subspace(V) ; B = zero_${type[0]}$${kind}$

        ! Lanczos bidiagonalization.
        call lanczos_bidiagonalization(A, U, V, B, info, tol=atol_${kind}$)
        call check_info(info, 'lanczos_bidiagonalization', module=this_module, &
                        & procedure='test_lanczos_bidiag_factorization_${type[0]}$${kind}$')

        ! Check correctness.
        call get_data(Udata, U)
        call get_data(Vdata, V)

        alpha = maxval(abs(matmul(A%data, Vdata(:, 1:kdim)) - matmul(Udata, B)))
        call check(error, alpha < rtol_${kind}$)
        call check_test(error, 'test_lanczos_bidiag_factorization_${type[0]}$${kind}$', &
                              & 'Bidiagonal Lanczos factorization not correct: A @ V[:,1:k] /= U[:,1:k+1] @ B[1:k+1,1:k]')

        ! Compute Gram matrix associated to the left Krylov basis.
        G = zero_${type[0]}$${kind}$
        call innerprod(G, U(1:kdim), U(1:kdim))

        ! Check orthonormality of the left basis.
        Id = eye(kdim)
        call check(error, maxval(abs(G - Id)) < rtol_${kind}$)
        call check_test(error, 'test_lanczos_bidiag_factorization_${type[0]}$${kind}$', &
                              & 'Left basis U not orthonormal: U[:,1:k].H @ U[:,1:k] /= I')

        ! Compute Gram matrix associated to the right Krylov basis.
        G = zero_${type[0]}$${kind}$
        call innerprod(G, V(1:kdim), V(1:kdim))

        ! Check orthonormality of the right basis.
        call check(error, maxval(abs(G - Id)) < rtol_${kind}$)
        call check_test(error, 'test_lanczos_bidiag_factorization_${type[0]}$${kind}$', &
                              & 'Right basis V not orthonormal: V[:,1:k].H @ V[:,1:k] /= I')

        return
    end subroutine test_lanczos_bidiag_factorization_${type[0]}$${kind}$

   #:endfor

    !-------------------------------------------------------------------------------
    !-----     DEFINITION OF THE UNIT TESTS FOR LANCZOS TRIDIAGONALIZATION     -----
    !-------------------------------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_lanczos_tridiag_${type[0]}$${kind}$_testsuite(testsuite)
        ! Collection of unit tests.
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
             new_unittest("Lanczos Tridiagonalization (correctness & orthonormality)", test_lanczos_tridiag_factorization_${type[0]}$${kind}$) &
            ]

        return
    end subroutine collect_lanczos_tridiag_${type[0]}$${kind}$_testsuite

    subroutine test_lanczos_tridiag_factorization_${type[0]}$${kind}$(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test matrix.
        #:if type[0] == "r"
        type(spd_linop_${type[0]}$${kind}$), allocatable :: A
        #:else
        type(hermitian_linop_${type[0]}$${kind}$), allocatable :: A
        #:endif
        ! Krylov subspace.
        type(vector_${type[0]}$${kind}$), allocatable :: X(:)
        ! Krylov subspace dimension.
        integer, parameter :: kdim = test_size
        ! Tridiagonal matrix.
        ${type}$ :: T(kdim+1, kdim)
        ! Information flag.
        integer :: info

        ! Internal variables.
        ${type}$ :: Xdata(test_size, kdim+1)
        real(${kind}$) :: alpha
        class(vector_${type[0]}$${kind}$), allocatable :: X0(:)
        ${type}$, dimension(kdim, kdim) :: G, Id

        ! Initialize tridiagonal matrix.
        T = zero_${type[0]}$${kind}$

        ! Initialize operator.
        #:if type[0] == "r"
        A = spd_linop_${type[0]}$${kind}$()
        #:else
        A = hermitian_linop_${type[0]}$${kind}$()
        #:endif
        call init_rand(A)

        ! Initialize Krylov subspace.
        allocate(X(kdim+1)) ; allocate(X0(1))
        call init_rand(X0) ; call initialize_krylov_subspace(X, X0)

        ! Lanczos factorization.
        call lanczos_tridiagonalization(A, X, T, info, tol=atol_${kind}$)
        call check_info(info, 'lanczos_tridiagonalization', module=this_module, & 
                        & procedure='test_lanczos_tridiag_factorization_${type[0]}$${kind}$')

        ! Check correctness.
        call get_data(Xdata, X)

        ! Infinity-norm check.
        alpha = maxval(abs(matmul(A%data, Xdata(:, 1:kdim)) - matmul(Xdata, T)))
        call check(error, alpha < rtol_${kind}$)
        call check_test(error, 'test_lanczos_tridiag_factorization_${type[0]}$${kind}$', &
                                 & 'Tridiagonal Lanczos factorization not correct: A @ X[:,1:k] /= X[:,1:k+1] @ T[1:k+1,1:k]')

        ! Compute Gram matrix associated to the right Krylov basis.
        G = zero_${type[0]}$${kind}$
        call innerprod(G, X(1:kdim), X(1:kdim))

        ! Check orthonormality of the Krylov basis.
        Id = eye(kdim)
        call check(error, maxval(abs(G - Id)) < rtol_${kind}$)
        call check_test(error, 'test_lanczos_tridiag_factorization_${type[0]}$${kind}$', &
                                 &'Basis X not orthonormal: X[:,1:k].H @ X[:,1:k] /= I')

        return
    end subroutine test_lanczos_tridiag_factorization_${type[0]}$${kind}$

    #:endfor
end module TestKrylov
