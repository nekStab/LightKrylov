#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module TestVectors
    ! Fortran Standard Library
    use stdlib_math, only: is_close, all_close
    use stdlib_linalg, only: norm
    use stdlib_stats_distribution_normal, only: normal => rvs_normal
    use stdlib_optval, only: optval
    ! Testdrive
    use testdrive, only: new_unittest, unittest_type, error_type, check
    ! LightKrylov
    use LightKrylov
    use LightKrylov_Logger
    ! Test Utilities
    use LightKrylov_TestUtils
    
    implicit none
    
    private

    character(len=*), parameter, private :: this_module      = 'LK_TVectors'
    character(len=*), parameter, private :: this_module_long = 'LightKrylov_TestVectors'
    integer, parameter :: n = 128

    #:for kind, type in RC_KINDS_TYPES
    public :: collect_vector_${type[0]}$${kind}$_testsuite
    #:endfor

contains
        
    !---------------------------------------------------------
    !-----     DEFINITIONS OF THE VARIOUS UNIT TESTS     -----
    !---------------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine collect_vector_${type[0]}$${kind}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
                    new_unittest("Vector norm", test_vector_${type[0]}$${kind}$_norm)      , &
                    new_unittest("Vector scale", test_vector_${type[0]}$${kind}$_scal)     , &
                    new_unittest("Vector addition", test_vector_${type[0]}$${kind}$_add)   , &
                    new_unittest("Vector subtraction", test_vector_${type[0]}$${kind}$_sub), &
                    new_unittest("Vector dot product", test_vector_${type[0]}$${kind}$_dot)  &
                    ]
        return
    end subroutine collect_vector_${type[0]}$${kind}$_testsuite

    subroutine test_vector_${type[0]}$${kind}$_norm(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test vector.
        type(dense_vector_${type[0]}$${kind}$) :: x
        ${type}$ :: x_(n)
        real(${kind}$) :: alpha

        ! Initialize vector.
        x_ = 0.0_${kind}$ ; x = dense_vector(x_) ; call x%rand()
        
        ! Compute its norm.
        alpha = x%norm()

        ! Check result.
        call check(error, is_close(alpha, norm(x%data, 2)))
        call check_test(error, 'test_vector_${type[0]}$${kind}$_norm', eq='is_close(x%norm, norm(x, 2))')
        
        return
    end subroutine test_vector_${type[0]}$${kind}$_norm

    subroutine test_vector_${type[0]}$${kind}$_add(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error

        ! Test vectors.
        type(dense_vector_${type[0]}$${kind}$), allocatable :: x, y, z
        ${type}$ :: x_(n), y_(n), z_(n)

        ! Initialize vectors.
        x = dense_vector(x_) ; call x%rand()
        y = dense_vector(y_) ; call y%rand()
        z = x

        ! Vector addition.
        call z%add(y)

        ! Check correctness.
        call check(error, norm(z%data - x%data - y%data, 2) < rtol_${kind}$)
        call check_test(error, 'test_vector_${type[0]}$${kind}$_norm', eq='is_close(x%norm, norm(z - (x+y), 2))')

        return
    end subroutine test_vector_${type[0]}$${kind}$_add
 
    subroutine test_vector_${type[0]}$${kind}$_sub(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error

        ! Test vectors.
        type(dense_vector_${type[0]}$${kind}$), allocatable :: x, y, z
        ${type}$ :: x_(n), y_(n), z_(n)

        ! Initialize vectors.
        x = dense_vector(x_) ; call x%rand()
        y = dense_vector(y_) ; call y%rand()
        z = x

        ! Vector addition.
        call z%sub(y)

        ! Check correctness.
        call check(error, norm(z%data - (x%data - y%data), 2) < rtol_${kind}$)
        call check_test(error, 'test_vector_${type[0]}$${kind}$_norm', eq='is_close(x%norm, norm(z - (x-y), 2))')

        return
    end subroutine test_vector_${type[0]}$${kind}$_sub

    subroutine test_vector_${type[0]}$${kind}$_dot(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error

        ! Test vectors.
        type(dense_vector_${type[0]}$${kind}$), allocatable :: x, y
        ${type}$ :: x_(n), y_(n)
        ${type}$ :: alpha

        ! Initialize vectors.
        x = dense_vector(x_) ; call x%rand()
        y = dense_vector(y_) ; call y%rand()

        ! Compute inner-product.
        alpha = x%dot(y)

        ! Check correctness.
        call check(error, abs(alpha - dot_product(x%data, y%data)) < rtol_${kind}$)
        call check_test(error, 'test_vector_${type[0]}$${kind}$_dot', eq='abs(x%dot(y) - dot_product(x, y))')

        return
    end subroutine test_vector_${type[0]}$${kind}$_dot

    subroutine test_vector_${type[0]}$${kind}$_scal(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error

        ! Test vector.
        type(dense_vector_${type[0]}$${kind}$), allocatable :: x, y
        ${type}$ :: x_(n), y_(n)
        ${type}$ :: alpha

        ! Initialize vector.
        x = dense_vector(x_) ; call x%rand(ifnorm=.true.)
        y = x
        #:if type[0] == "c"
        alpha = 0.0_${kind}$ ; call random_number(alpha%re) ; call random_number(alpha%im)
        alpha = alpha / abs(alpha)
        #:else
        call random_number(alpha)
        #:endif
        
        ! Scale the vector.
        call x%scal(alpha)

        ! Check correctness.
        call check(error, norm(x%data - alpha*y%data, 2) < rtol_${kind}$)
        call check_test(error, 'test_vector_${type[0]}$${kind}$_scal', eq='norm(x - alpha*y, 2)')

        return
    end subroutine test_vector_${type[0]}$${kind}$_scal

    #:endfor

end module TestVectors
