#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module TestVectors
    use LightKrylov
    use testdrive, only: new_unittest, unittest_type, error_type, check
    use stdlib_math, only: is_close, all_close
    use stdlib_optval, only: optval
    implicit none
    private

    integer, parameter, public :: test_size = 128

    #:for k1, t1 in RC_KINDS_TYPES
    public :: collect_vector_${t1[0]}$${k1}$_testsuite
    #:endfor

    #:for k1, t1 in RC_KINDS_TYPES
    type, extends(abstract_vector_${t1[0]}$${k1}$), public :: vector_${t1[0]}$${k1}$
        ${t1}$, dimension(test_size) :: data = 0.0_${k1}$
    contains
        private
        procedure, pass(self), public :: zero => zero_${t1[0]}$${k1}$
        procedure, pass(self), public :: dot => dot_${t1[0]}$${k1}$
        procedure, pass(self), public :: scal => scal_${t1[0]}$${k1}$
        procedure, pass(self), public :: axpby => axpby_${t1[0]}$${k1}$
        procedure, pass(self), public :: rand => rand_${t1[0]}$${k1}$
    end type vector_${t1[0]}$${k1}$

    #:endfor
contains
    
    !--------------------------------------------------------------------
    !-----     DEFINITIONS OF THE VARIOUS TYPE-BOUND PROCEDURES     -----
    !--------------------------------------------------------------------

    #:for k1, t1 in RC_KINDS_TYPES
    subroutine zero_${t1[0]}$${k1}$(self)
        class(vector_${t1[0]}$${k1}$), intent(inout) :: self
        self%data = 0.0_${k1}$
        return
    end subroutine zero_${t1[0]}$${k1}$

    function dot_${t1[0]}$${k1}$(self, vec) result(alpha)
        class(vector_${t1[0]}$${k1}$), intent(in) :: self
        class(abstract_vector_${t1[0]}$${k1}$), intent(in) :: vec
        ${t1}$ :: alpha

        select type(vec)
        type is(vector_${t1[0]}$${k1}$)
            alpha = dot_product(self%data, vec%data)
        end select
    end function dot_${t1[0]}$${k1}$

    subroutine scal_${t1[0]}$${k1}$(self, alpha)
        class(vector_${t1[0]}$${k1}$), intent(inout) :: self
        ${t1}$, intent(in) :: alpha
        self%data = alpha * self%data
        return
    end subroutine scal_${t1[0]}$${k1}$

    subroutine axpby_${t1[0]}$${k1}$(self, alpha, vec, beta)
        class(vector_${t1[0]}$${k1}$), intent(inout) :: self
        class(abstract_vector_${t1[0]}$${k1}$), intent(in) :: vec
        ${t1}$, intent(in) :: alpha, beta

        select type(vec)
        type is(vector_${t1[0]}$${k1}$)
            self%data = alpha*self%data + beta*vec%data
        end select
        return
    end subroutine

    subroutine rand_${t1[0]}$${k1}$(self, ifnorm)
        class(vector_${t1[0]}$${k1}$), intent(inout) :: self
        logical, optional, intent(in) :: ifnorm
        logical :: normalized
        ${t1}$ :: alpha
        #:if t1[0] == "c"
        real(${k1}$), dimension(test_size, 2) :: data

        call random_number(data)
        self%data%re = data(:, 1)
        self%data%im = data(:, 2)
        #:else
        call random_number(self%data)
        #:endif

        normalized = optval(ifnorm, .false.)
        if (normalized) then
            alpha = self%norm()
            call self%scal(1.0_${k1}$/alpha)
        endif
    end subroutine rand_${t1[0]}$${k1}$

    #:endfor
    
    !---------------------------------------------------------
    !-----     DEFINITIONS OF THE VARIOUS UNIT TESTS     -----
    !---------------------------------------------------------

    #:for k1, t1 in RC_KINDS_TYPES
    subroutine collect_vector_${t1[0]}$${k1}$_testsuite(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
                    new_unittest("Vector norm", test_vector_${t1[0]}$${k1}$_norm)      , &
                    new_unittest("Vector scale", test_vector_${t1[0]}$${k1}$_scal)     , &
                    new_unittest("Vector addition", test_vector_${t1[0]}$${k1}$_add)   , &
                    new_unittest("Vector subtraction", test_vector_${t1[0]}$${k1}$_sub), &
                    new_unittest("Vector dot product", test_vector_${t1[0]}$${k1}$_dot)  &
                    ]
        return
    end subroutine collect_vector_${t1[0]}$${k1}$_testsuite

    subroutine test_vector_${t1[0]}$${k1}$_norm(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error
        ! Test vector.
        type(vector_${t1[0]}$${k1}$), allocatable :: x
        real(${k1}$) :: alpha

        ! Initialize vector.
        x = vector_${t1[0]}$${k1}$() ; call x%rand()
        
        ! Compute its norm.
        alpha = x%norm()

        ! Check result.
        #:if t1[0] == "c"
        call check(error, is_close(alpha, sqrt(sum(x%data%re**2 + x%data%im**2))))
        #:else
        call check(error, is_close(alpha, norm2(x%data)))
        #:endif
        
        return
    end subroutine test_vector_${t1[0]}$${k1}$_norm

    subroutine test_vector_${t1[0]}$${k1}$_add(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error

        ! Test vectors.
        type(vector_${t1[0]}$${k1}$), allocatable :: x, y, z

        ! Initialize vectors.
        x = vector_${t1[0]}$${k1}$() ; call x%rand()
        y = vector_${t1[0]}$${k1}$() ; call y%rand()
        z = x

        ! Vector addition.
        call z%add(y)

        ! Check correctness.
        #:if t1[0] == "c"
        call check(error, sqrt(sum((z%data%re - x%data%re - y%data%re)**2 + (z%data%im - x%data%im - y%data%im)**2)) < rtol_${k1}$)
        #:else
        call check(error, norm2(z%data - x%data - y%data) < rtol_${k1}$)
        #:endif

        return
    end subroutine test_vector_${t1[0]}$${k1}$_add
 
    subroutine test_vector_${t1[0]}$${k1}$_sub(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error

        ! Test vectors.
        type(vector_${t1[0]}$${k1}$), allocatable :: x, y, z

        ! Initialize vectors.
        x = vector_${t1[0]}$${k1}$() ; call x%rand()
        y = vector_${t1[0]}$${k1}$() ; call y%rand()
        z = x

        ! Vector addition.
        call z%sub(y)

        ! Check correctness.
        #:if t1[0] == "c"
        call check(error, sqrt(sum((z%data%re - x%data%re + y%data%re)**2 + (z%data%im - x%data%im + y%data%im)**2)) < rtol_${k1}$)
        #:else
        call check(error, norm2(z%data - x%data + y%data) < rtol_${k1}$)
        #:endif

        return
    end subroutine test_vector_${t1[0]}$${k1}$_sub

    subroutine test_vector_${t1[0]}$${k1}$_dot(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error

        ! Test vectors.
        type(vector_${t1[0]}$${k1}$), allocatable :: x, y
        ${t1}$ :: alpha

        ! Initialize vectors.
        x = vector_${t1[0]}$${k1}$() ; call x%rand()
        y = vector_${t1[0]}$${k1}$() ; call y%rand()

        ! Compute inner-product.
        alpha = x%dot(y)

        ! Check correctness.
        call check(error, abs(alpha - dot_product(x%data, y%data)) < rtol_${k1}$)

        return
    end subroutine test_vector_${t1[0]}$${k1}$_dot

    subroutine test_vector_${t1[0]}$${k1}$_scal(error)
        ! Error type to be returned.
        type(error_type), allocatable, intent(out) :: error

        ! Test vector.
        type(vector_${t1[0]}$${k1}$), allocatable :: x, y
        ${t1}$ :: alpha
        #:if t1[0] == "c"
        ${t1}$ :: tmp(test_size)
        #:endif

        ! Initialize vector.
        x = vector_${t1[0]}$${k1}$() ; call x%rand()
        y = x
        #:if t1[0] == "c"
        call random_number(alpha%re) ; call random_number(alpha%im)
        #:else
        call random_number(alpha)
        #:endif
        
        ! Scale the vector.
        call x%scal(alpha)

        ! Check correctness.
        #:if t1[0] == "c"
        tmp = x%data - alpha*y%data
        call check(error, sqrt(sum(tmp%re**2 + tmp%im**2)) < rtol_${k1}$)
        #:else
        call check(error, norm2(x%data - alpha*y%data) < rtol_${k1}$)
        #:endif

        return
    end subroutine test_vector_${t1[0]}$${k1}$_scal

    #:endfor

end module TestVectors
