#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module LightKrylov_NewtonKrylov
   use stdlib_optval, only: optval
   use LightKrylov_Constants
   use LightKrylov_Logger
   use LightKrylov_AbstractVectors
   use LightKrylov_AbstractLinops
   use LightKrylov_AbstractSystems
   use LightKrylov_IterativeSolvers
   use LightKrylov_Utils

   implicit none
   private

   character*128, parameter :: this_module = 'LightKrylov_NewtonKrylov'

   public :: newton
   public :: constant_atol, dynamic_tol

   interface newton
      !! Implements the simple Newton-Krylov method for finding roots (fixed points) of a nonlinear vector-valued function
      !! \( F(\mathbf{X}) \), i.e. solutions \( \mathbf{X}^* \) such that \( F(\mathbf{X}^*) - \mathbf{X}^* = \mathbf{0} \) 
      !! starting from an initial guess via successive solution increments based on local linearization \( \mathbf{J}_\mathbf{X} \) 
      !! (the Jacobian) of the nonlinear function in the vicinity of the current solution.
      !!
      !! **Algorthmic Features**
      !! - At iteration \(k\), the standard Newton step \( \mathbf{\delta x}_k\) is computed as the solution of the linear system
      !!   
      !! $$ \mathbf{J}_\mathbf{X_k} \mathbf{\delta x}_k = \mathbf{r}_k $$
      !!
      !!   where \( \mathbf{r}_k = -F(\mathbf{X}_k) \) is the residual of the nonlinear function. The new guess for the fixed
      !!   point is then given by:
      !!
      !! $$ \mathbf{X}_{k+1} = \mathbf{X}_k + \alpha \mathbf{\delta x}_k$$
      !!
      !!   where \( \alpha \in ( 0, 1 \] \) parametrizes the step length. The standard Newton algorithm sets \( \alpha = 1 \).
      !!
      !! - The Jacobian is never assembled and the linear system is solved using one of the available iterative solvers.
      !! - When the residual norm does not decrease during iteration indicating that the linearization is not a very
      !!   accurate model of the function's behaviour, which often happens during the initial iterations, a 1D step bisection
      !!   method based on the golden ratio is implemented to dampen the step and improve convergence of the method.
      !! - The implementation allows for dynamic tolerances (also known as inexact Newton), where the approximation for 
      !!   the residual and the linear system can be solved with relaxed tolerances to reduce overall time to solution.
      !! - The method is suitable to both fixed points and periodic orbits via the choice of residual and corresponding
      !!   Jacobian matrix. In the case of unforced periodic orbits, the period is itself an unknown that must be included
      !!   in the iteration.
      !!
      !! **Advantages**
      !! - The iterative solution of the linear systems has a comparatively low storage footprint.
      !! - If the Newton iteration converges, the convergence is formally asymptotically of second order. Using dynamic
      !!   tolerances and line searches slightly reduce this convergence rate in exchange for a larger convergence region.
      !! 
      !! **Limitations**
      !! - The method is not guaranteed to converge if the initial guess is too far from the fixed point. 
      !!   If the Newton iteration diverges even with step bisection, the best suggestion is to find a 
      !!   better initial guess. If this is not feasible, some alternatives to improve the convergence 
      !!   of the Newton iteration are possible (but not implemented to date), including various line search
      !!   algorithms and trust region methods (doglog, double dogleg, hookstep, ...).
      !!
      !! **References**
      !! - Sánchez, J., Net, M., Garcıa-Archilla, B., & Simó, C. (2004). "Newton–Krylov continuation of periodic orbits 
      !!   for Navier–Stokes flows". Journal of Computational Physics, 201(1), 13-33.
      !! - Viswanath, D. (2007). "Recurrent motions within plane Couette turbulence". Journal of Fluid Mechanics, 580, 339-358.
      !! - Duguet, Y., Pringle, C. C. T., Kerswell, R. R. (2008). "Relative periodic orbits in transitional pipe flow". Physics
      !!   of Fluids, 20(11), 114102.
      !! - Frantz, R. A., Loiseau, J. C., & Robinet, J. C. (2023). "Krylov methods for large-scale dynamical systems: Application 
      !!   in fluid dynamics". Applied Mechanics Reviews, 75(3), 030802.
      !!
      #:for kind, type in RC_KINDS_TYPES
      module procedure newton_${type[0]}$${kind}$
      #:endfor
   end interface

   interface constant_atol
      #:for kind, type in REAL_KINDS_TYPES
      module procedure constant_atol_${kind}$
      #:endfor
   end interface

   interface dynamic_tol
      #:for kind, type in REAL_KINDS_TYPES
      module procedure dynamic_tol_${kind}$
      #:endfor
   end interface

   abstract interface
      #:for kind, type in REAL_KINDS_TYPES
      subroutine abstract_scheduler_${kind}$(tol, rnorm, iter, info)
         import ${kind}$
         !! Abstract interface to define a tolerance scheduler for the Newton iteration
         real(${kind}$), intent(out) :: tol
         !! Tolerance to be used
         real(${kind}$), intent(in)  :: rnorm
         !! Norm of the residual of the current iterate
         integer,  intent(in)  :: iter
         !! Newton iteration count
         integer,  intent(out)  :: info
         !! Information flag
      end subroutine abstract_scheduler_${kind}$
      #:endfor
   end interface

   type, extends(abstract_opts), public :: newton_opts
      !! Options for Newton-Krylov fixed-point iteration.
      integer :: maxiter = 100
      !! Maximum number of Newton iterations (default = 100)
      logical :: ifbisect = .false.
      !! Bisection toggle to enforce residual reduction (default = .false.)
      integer :: maxstep_bisection = 5
      !! Maximum number of bisections (evaluations of F) for step selection (default = 5)
      !! Ignored if ifbisect = .false.
      logical :: verbose = .false.
      !! Verbosity control (default: `.false.`)
    end type

contains

   #:for kind, type in RC_KINDS_TYPES
   subroutine newton_${type[0]}$${kind}$(sys, X, info, options, linear_solver, linear_solver_options, preconditioner, scheduler)
      class(abstract_system_${type[0]}$${kind}$),                         intent(inout) :: sys
      !! Dynamical system for which we wish to compute a fixed point
      class(abstract_vector_${type[0]}$${kind}$),                         intent(inout) :: X
      !! Initial guess for the fixed point, will be overwritten with solution
      integer,                                            intent(out)   :: info
      !! Information flag
      type(newton_opts),                        optional, intent(in)    :: options
      type(newton_opts)                                                 :: opts
      !! Options for the Newton-Krylov iteration
      procedure(abstract_linear_solver_${type[0]}$${kind}$),    optional                :: linear_solver
      !! Linear solver to be used to find Newton step
      class(abstract_opts),                     optional, intent(in)    :: linear_solver_options
      class(abstract_opts), allocatable                                 :: solver_opts
      !! Options for the linear solver
      class(abstract_precond_${type[0]}$${kind}$),              optional, intent(in)    :: preconditioner
      class(abstract_precond_${type[0]}$${kind}$), allocatable                          :: precond
      !! Preconditioner for the linear solver
      procedure(abstract_scheduler_${kind}$),        optional                :: scheduler

      !--------------------------------------
      !-----     Internal variables     -----
      !--------------------------------------
      
      ! residual vector
      class(abstract_vector_${type[0]}$${kind}$), allocatable :: residual, increment
      real(${kind}$) :: rnorm, tol
      logical :: converged, has_precond, has_solver_opts, verb
      integer :: i, maxiter, maxstep_bisection
      procedure(abstract_linear_solver_${type[0]}$${kind}$), pointer :: solver => null()
      procedure(abstract_scheduler_${kind}$), pointer :: tolerance_scheduler => null()

      ! Newton-Krylov options
      if (present(options)) then
         opts = options
      else
         opts = newton_opts()
      end if
      ! Linear solver
      if (present(linear_solver)) then
         solver => linear_solver
      else
         solver => gmres_${type[0]}$${kind}$
      end if
      ! Linear solver options ?
      if (present(linear_solver_options)) then
         has_solver_opts = .true.
         allocate(solver_opts, source=linear_solver_options)
      else
         has_solver_opts = .false.
      end if
      ! Preconditioner ?
      if (present(preconditioner)) then
         has_precond = .true.
         allocate(precond, source=preconditioner)
      else
         has_precond = .false.
      end if
      ! Scheduler
      if (present(scheduler)) then
         tolerance_scheduler => scheduler
      else
         tolerance_scheduler => constant_atol_${kind}$
      endif

      ! Initialisation
      verb = opts%verbose
      
      maxiter = opts%maxiter
      maxstep_bisection = opts%maxstep_bisection
      converged = .false.
      allocate(residual, source=X); call residual%zero()
      allocate(increment,source=X); call increment%zero()

      if (verb) write(*,*) 'Starting Newton ...'
      ! Newton iteration
      newton: do i = 1, maxiter

         call sys%eval(X, residual)
         rnorm = residual%norm()
         if (verb) write(*,*) "Iteration", i, ": Residual norm = ", rnorm

         ! Check for convergence.
         call tolerance_scheduler(tol, rnorm, i, info)
         if (rnorm < tol) then
            if (verb) write(*,*) "Convergence achieved."
            converged = .true.
            exit newton
         end if

         ! Define the Jacobian
         sys%jacobian%X = X
        
         ! Solve the linear system using GMRES.
         call residual%chsgn()
         if (.not. has_precond .and. .not. has_solver_opts) then
            call solver(sys%jacobian, residual, increment, info,                                              transpose=.false.)
         elseif (.not. has_precond .and. has_solver_opts) then
            call solver(sys%jacobian, residual, increment, info,                         options=solver_opts, transpose=.false.)
         elseif (has_precond .and. .not. has_solver_opts) then
            call solver(sys%jacobian, residual, increment, info, preconditioner=precond,                      transpose=.false.)
         else
            call solver(sys%jacobian, residual, increment, info, preconditioner=precond, options=solver_opts, transpose=.false.)
         end if
         print *, 'DEBUG: Newton-Krylov iteration', i, ': solver info:', info
         !call check_info(info, 'linear_solver', module=this_module, procedure='newton_${type[0]}$${kind}$')

         ! Update the solution and overwrite X0
         if (opts%ifbisect) then
            call increment_bisection_${type[0]}$${kind}$(X, sys, increment, rnorm, maxstep_bisection, verb)
         else
            call X%add(increment)
         endif

      enddo newton

      if (.not.converged) then
         if (verb) write(*,*) 'Newton iteration did not converge within', maxiter, 'steps.'
         info = -1
      endif

      return
   end subroutine newton_${type[0]}$${kind}$

   #:endfor

   #:for kind, type in RC_KINDS_TYPES
   subroutine increment_bisection_${type[0]}$${kind}$(X, sys, increment, rold, maxstep, verb)
      !! Classic 1D bisection method based on the golden ratio to damped the Newton step in 
      !! order to maximally reduce the residual at each iteration.
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X
      !! Current system state to be updated
      class(abstract_system_${type[0]}$${kind}$), intent(in)    :: sys
      !! Dynamical system for which the residual is minimized
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: increment
      !! Newton step computed from the standard method
      real(${kind}$),                   intent(in)    :: rold
      !! Residual of the current system state to determine improvement
      integer,                    intent(in)    :: maxstep
      !! Maximum number of bisection steps. Each additional bisection step requires an evaluation of the nonlinear function
      logical,                    intent(in)    :: verb
      !! Verbosity toggle
      ! internals
      integer :: i, j, idx(1)
      real(${kind}$) :: invphi, invphi2
      #:if type[0] == "c"
      complex(${kind}$) :: alpha(4), step
      #:else
      real(${kind}$) :: alpha(4), step
      #:endif
      real(${kind}$) :: res(4)
      class(abstract_vector_${type[0]}$${kind}$), allocatable :: Xin, residual

      allocate(Xin, source=X)
      allocate(residual, source=X); call residual%zero()
      step    = one_${type[0]}$${kind}$
      invphi  = (sqrt(5.0) - 1.0)/2.0  ! 1 / phi
      invphi2 = (3.0 - sqrt(5.0))/2.0  ! 1 / phi**2
      alpha = (/ zero_${type[0]}$${kind}$, invphi2*one_${type[0]}$${kind}$, invphi*one_${type[0]}$${kind}$, one_${type[0]}$${kind}$ /)
      res   = (/ rold, zero_r${kind}$, zero_r${kind}$, zero_r${kind}$ /)

      call X%add(increment)
      ! evaluate residual norm
      call sys%eval(X, residual)
      res(4) = residual%norm()

      if (res(4) > rold) then
         if (verb) print *, 'Start Newton step bisection ...'
         ! compute new trial solutions
         do j = 2, 3
            call X%axpby(zero_${type[0]}$${kind}$, Xin, one_${type[0]}$${kind}$)
            call X%axpby(one_${type[0]}$${kind}$, increment, alpha(j))
            call sys%eval(X, residual)
            res(j) = residual%norm()
         end do

         do i = 1, maxstep
            step = step * invphi
            if (res(2) < res(3)) then
               ! alphas
               ! a1 is kept
               alpha(3:4) = alpha(2:3)           
               ! residuals
               ! r1 is kept
               res(3:4) = res(2:3)
               ! new point --> a2, r2
               alpha(2) = alpha(1) + step * invphi2
               call X%axpby(zero_${type[0]}$${kind}$, Xin, one_${type[0]}$${kind}$)
               call X%axpby(one_${type[0]}$${kind}$, increment, alpha(2))
               call sys%eval(X, residual)
               res(2) = residual%norm()
            else
               ! alphas
               alpha(1:2) = alpha(2:3)
               ! a4 is kept
               ! residuals
               res(1:2) = res(2:3)
               ! r4 is kept
               ! new point --> a3, r3
               alpha(3) = alpha(1) + step * invphi
               call X%axpby(zero_${type[0]}$${kind}$, Xin, one_${type[0]}$${kind}$)
               call X%axpby(one_${type[0]}$${kind}$, increment, alpha(3))
               call sys%eval(X, residual)
               res(3) = residual%norm()
            end if
         end do
         ! set new vector to optimal step
         idx = minloc(res)
         call X%axpby(zero_${type[0]}$${kind}$, Xin, one_${type[0]}$${kind}$)
         call X%axpby(one_${type[0]}$${kind}$, increment, alpha(idx(1)))
      end if

      return
   end subroutine

   #:endfor

   #:for kind, type in REAL_KINDS_TYPES
   !--------------------------------------------------------------------
   !-----     Definition of two basic tolerance schedulers (${kind}$)    -----
   !--------------------------------------------------------------------

   subroutine constant_atol_${kind}$(tol, rnorm, iter, info)
      !! Abstract interface to define tolerance scheduler for the Newton iteration
      real(${kind}$), intent(out) :: tol
      !! Tolerance to be used
      real(${kind}$), intent(in)  :: rnorm
      !! Norm of the residual of the current iterate
      integer,  intent(in)  :: iter
      !! Newton iteration count
      integer,  intent(out)  :: info
      !! Information flag
      tol = 10*atol_${kind}$
      return
   end subroutine constant_atol_${kind}$

   subroutine dynamic_tol_${kind}$(tol, rnorm, iter, info)
      !! Abstract interface to define tolerance scheduler for the Newton iteration
      real(${kind}$), intent(out) :: tol
      !! Tolerance to be used
      real(${kind}$), intent(in)  :: rnorm
      !! Norm of the residual of the current iterate
      integer,  intent(in)  :: iter
      !! Newton iteration count
      integer,  intent(out)  :: info
      !! Information flag
      tol = max(0.1*rnorm, atol_${kind}$)
      return
   end subroutine dynamic_tol_${kind}$

   #:endfor

end module LightKrylov_NewtonKrylov
