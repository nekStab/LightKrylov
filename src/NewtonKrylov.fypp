#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module LightKrylov_NewtonKrylov
   use stdlib_optval, only: optval
   use LightKrylov_Constants
   use LightKrylov_Logger
   use LightKrylov_AbstractVectors
   use LightKrylov_AbstractLinops
   use LightKrylov_AbstractSystems
   use LightKrylov_IterativeSolvers
   use LightKrylov_Utils

   implicit none
   private

   character(len=*), parameter :: this_module      = 'LK_NwtKryl'
   character(len=*), parameter :: this_module_long = 'LightKrylov_NewtonKrylov'

   public :: newton
   #:for kind in REAL_KINDS
   public :: constant_atol_${kind}$
   public :: dynamic_tol_${kind}$
   #:endfor

   interface newton
      !! Implements the simple Newton-Krylov method for finding roots (fixed points) of a nonlinear vector-valued function
      !! \( F(\mathbf{X}) \), i.e. solutions \( \mathbf{X}^* \) such that \( F(\mathbf{X}^*) - \mathbf{X}^* = \mathbf{0} \) 
      !! starting from an initial guess via successive solution increments based on local linearization \( \mathbf{J}_\mathbf{X} \) 
      !! (the Jacobian) of the nonlinear function in the vicinity of the current solution.
      !!
      !! **Algorthmic Features**
      !!
      !! - At iteration \(k\), the standard Newton step \( \mathbf{\delta x}_k\) is computed as the solution of the linear system
      !!   
      !! $$ \mathbf{J}_\mathbf{X_k} \mathbf{\delta x}_k = \mathbf{r}_k $$
      !!
      !!   where \( \mathbf{r}_k = -F(\mathbf{X}_k) \) is the residual of the nonlinear function. The new guess for the fixed
      !!   point is then given by:
      !!
      !! $$ \mathbf{X}_{k+1} = \mathbf{X}_k + \alpha \mathbf{\delta x}_k$$
      !!
      !!   where \( \alpha \in \left( 0, 1 \right] \) parametrizes the step length. The standard Newton algorithm sets \( \alpha = 1 \).
      !!
      !! - The Jacobian is never assembled and the linear system is solved using one of the available iterative solvers.
      !! - When the residual norm does not decrease during iteration indicating that the linearization is not a very
      !!   accurate model of the function's behaviour, which often happens during the initial iterations, a 1D step bisection
      !!   method based on the golden ratio is implemented to dampen the step and improve convergence of the method.
      !! - The implementation allows for dynamic tolerances (also known as inexact Newton), where the approximation for 
      !!   the residual and the linear system can be solved with relaxed tolerances to reduce overall time to solution.
      !! - The method is suitable to both fixed points and periodic orbits via the choice of residual and corresponding
      !!   Jacobian matrix. In the case of unforced periodic orbits, the period is itself an unknown that must be included
      !!   in the iteration.
      !!
      !! **Advantages**
      !!
      !! - The iterative solution of the linear systems has a comparatively low storage footprint.
      !! - If the Newton iteration converges, the convergence is formally asymptotically of second order. Using dynamic
      !!   tolerances and line searches slightly reduce this convergence rate in exchange for a larger convergence region.
      !! 
      !! **Limitations**
      !!
      !! - The method is not guaranteed to converge if the initial guess is too far from the fixed point. 
      !!   If the Newton iteration diverges even with step bisection, the best suggestion is to find a 
      !!   better initial guess. If this is not feasible, some alternatives to improve the convergence 
      !!   of the Newton iteration are possible (but not implemented to date), including various line search
      !!   algorithms and trust region methods (doglog, double dogleg, hookstep, ...).
      !!
      !! **References**
      !!
      !! - Sánchez, J., Net, M., Garcıa-Archilla, B., & Simó, C. (2004). "Newton–Krylov continuation of periodic orbits 
      !!   for Navier–Stokes flows". Journal of Computational Physics, 201(1), 13-33.
      !! - Viswanath, D. (2007). "Recurrent motions within plane Couette turbulence". Journal of Fluid Mechanics, 580, 339-358.
      !! - Duguet, Y., Pringle, C. C. T., Kerswell, R. R. (2008). "Relative periodic orbits in transitional pipe flow". Physics
      !!   of Fluids, 20(11), 114102.
      !! - Frantz, R. A., Loiseau, J. C., & Robinet, J. C. (2023). "Krylov methods for large-scale dynamical systems: Application 
      !!   in fluid dynamics". Applied Mechanics Reviews, 75(3), 030802.
      #:for kind, type in RC_KINDS_TYPES
      module procedure newton_${type[0]}$${kind}$
      #:endfor
   end interface

   abstract interface
      #:for kind, type in REAL_KINDS_TYPES
      subroutine abstract_scheduler_${kind}$(tol, target_tol, rnorm, iter, info)
         import ${kind}$
         !! Abstract interface to define a tolerance scheduler for the Newton iteration
         real(${kind}$), intent(out) :: tol
         !! Tolerance to be used
         real(${kind}$), intent(in) :: target_tol
         !! Target tolerance
         real(${kind}$), intent(in)  :: rnorm
         !! Norm of the residual of the current iterate
         integer,  intent(in)  :: iter
         !! Newton iteration count
         integer,  intent(out)  :: info
         !! Information flag
      end subroutine abstract_scheduler_${kind}$

      #:endfor
   end interface

contains

   #:for kind, type in RC_KINDS_TYPES
   subroutine newton_${type[0]}$${kind}$(sys, X, solver, info, tolerance, options, linear_solver_options, preconditioner, scheduler, meta)
      class(abstract_system_${type[0]}$${kind}$),                         intent(inout) :: sys
      !! Dynamical system for which we wish to compute a fixed point
      class(abstract_vector_${type[0]}$${kind}$),                         intent(inout) :: X
      !! Initial guess for the fixed point, will be overwritten with solution
      procedure(abstract_linear_solver_${type[0]}$${kind}$)                :: solver
      !! Linear solver to be used to find Newton step
      integer,                                            intent(out)   :: info
      !! Information flag
      real(${kind}$),                                 optional, intent(in)    :: tolerance
      real(${kind}$)                                                          :: target_tol
      !! Target absolute solver tolerance
      type(newton_${kind}$_opts),                     optional, intent(in)    :: options
      type(newton_${kind}$_opts)                                              :: opts
      !! Options for the Newton-Krylov iteration
      class(abstract_opts),                     optional, intent(in)    :: linear_solver_options
      !! Options for the linear solver
      class(abstract_precond_${type[0]}$${kind}$),              optional, intent(in)    :: preconditioner
      !! Preconditioner for the linear solver
      procedure(abstract_scheduler_${kind}$),         optional                :: scheduler
      class(abstract_metadata),                       optional,   intent(out) :: meta
      !! Metadata.

      !--------------------------------------
      !-----     Internal variables     -----
      !--------------------------------------
      
      procedure(abstract_scheduler_${kind}$),      pointer :: tolerance_scheduler => null()
      class(abstract_vector_${type[0]}$${kind}$), allocatable        :: residual, increment
      real(${kind}$)           :: rnorm, tol
      integer            :: i, maxiter, maxstep_bisection
      type(newton_${kind}$_metadata) :: newton_meta
      character(len=256) :: msg
      
      ! Newton-solver tolerance
      target_tol = optval(tolerance, atol_${kind}$)
      ! Newton-Krylov options
      if (present(options)) then
         opts = options
      else
         opts = newton_${kind}$_opts()
      end if
      ! Scheduler
      if (present(scheduler)) then
         tolerance_scheduler => scheduler
      else
         tolerance_scheduler => constant_atol_${kind}$
      endif

      ! Initialisation      
      maxiter           = opts%maxiter
      maxstep_bisection = opts%maxstep_bisection
      allocate(residual, source=X); call residual%zero()
      allocate(increment,source=X); call increment%zero()

      newton_meta = newton_${kind}$_metadata()
      allocate(newton_meta%res(2*maxiter)); newton_meta%res = 0.0_${kind}$
      allocate(newton_meta%tol(2*maxiter)); newton_meta%tol = 0.0_${kind}$

      ! Get initial residual.
      call sys%eval(X, residual, target_tol) ; newton_meta%n_eval = newton_meta%n_eval + 1
      rnorm = residual%norm()

      ! Save metadata.
      newton_meta%res(1) = rnorm
      newton_meta%tol(1) = target_tol

      ! Check for lucky convergence.
      if (rnorm < target_tol) then
         write(msg,'(A)') 'Initial guess is a fixed point to tolerance!'
         call logger%log_warning(msg, module=this_module, procedure='newton_${type[0]}$${kind}$')
         newton_meta%converged = .true.
         return
      end if

      write(msg,'(A)') 'Starting Newton iteration ...'
      call logger%log_information(msg, module=this_module, procedure='newton_${type[0]}$${kind}$')
      ! Newton iteration
      newton: do i = 1, maxiter

         ! Set dynamic tolerances for Newton iteration and linear solves.
         call tolerance_scheduler(tol, target_tol, rnorm, i, info)
         write(msg,"(A,I0,3(A,E9.2))") 'Start step ', i, ': rnorm= ', rnorm, ', tol= ', tol, ', target= ', target_tol
         call logger%log_message(msg, module=this_module, procedure='newton_${type[0]}$${kind}$')

         ! Define the Jacobian
         sys%jacobian%X = X
        
         ! Solve the linear system using GMRES.
         call residual%chsgn()
         call solver(sys%jacobian, residual, increment, info, atol=tol, &
            & preconditioner=preconditioner, options=linear_solver_options, transpose=.false.)
         call check_info(info, 'linear_solver', module=this_module, procedure='newton_${type[0]}$${kind}$')

         ! Update the solution and overwrite X0
         if (opts%ifbisect) then
            call increment_bisection_${type[0]}$${kind}$(X, sys, increment, rnorm, tol, maxstep_bisection)
         else
            call X%add(increment)
         endif

         ! Evaluate new residual
         call sys%eval(X, residual, tol) ; newton_meta%n_eval = newton_meta%n_eval + 1
         rnorm = residual%norm()

         ! Save metadata.
         newton_meta%n_iter = newton_meta%n_iter + 1
         newton_meta%res(newton_meta%n_eval+1) = rnorm
         newton_meta%tol(newton_meta%n_eval+1) = tol

         ! Check for convergence.
         if (rnorm < tol) then
            if (tol >= target_tol .and. tol < 100.0_${kind}$*target_tol) then
               ! the tolerances are not at the target, check the accurate residual                  
               call sys%eval(X, residual, target_tol) ; newton_meta%n_eval = newton_meta%n_eval + 1
               rnorm = residual%norm()
               ! Save metadata.
               newton_meta%res(newton_meta%n_eval + 1) = rnorm
               newton_meta%tol(newton_meta%n_eval + 1) = target_tol
               if (rnorm < target_tol) then
                  write(msg,'(A,I0,A)') 'Newton iteration converged after ',i,' iterations.'
                  call logger%log_message(msg, module=this_module, procedure='newton_${type[0]}$${kind}$')
                  newton_meta%converged = .true.
                  exit newton
               else
                  write(msg,'(A)') 'Dynamic tolerance but not target tolerance reached. Continue iteration.'
                  call logger%log_warning(msg, module=this_module, procedure='newton_${type[0]}$${kind}$')
               end if
            end if
         end if

      enddo newton

      if (.not.newton_meta%converged) then
         write(msg,'(A,I0,A)') 'Newton iteration did not converge within', maxiter, 'steps.'
         call logger%log_warning(msg, module=this_module, procedure='newton_${type[0]}$${kind}$')
         info = -1
      endif

      ! Copy info flag
      newton_meta%info = info

      ! Set metadata output
      select type(meta)
         type is (newton_${kind}$_metadata)
            meta = newton_meta
      end select

      return
   end subroutine newton_${type[0]}$${kind}$

   #:endfor

   #:for kind, type in RC_KINDS_TYPES
   subroutine increment_bisection_${type[0]}$${kind}$(X, sys, increment, rold, tol, maxstep)
      !! Classic 1D bisection method based on the golden ratio to damped the Newton step in 
      !! order to maximally reduce the residual at each iteration.
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X
      !! Current system state to be updated
      class(abstract_system_${type[0]}$${kind}$), intent(in)    :: sys
      !! Dynamical system for which the residual is minimized
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: increment
      !! Newton step computed from the standard method
      real(${kind}$),                   intent(in)    :: rold
      !! Residual of the current system state to determine improvement
      real(${kind}$),                   intent(in)    :: tol
      integer,                    intent(in)    :: maxstep
      !! Maximum number of bisection steps. Each additional bisection step requires an evaluation of the nonlinear function

      ! internals
      integer :: i, j, idx(1)
      real(${kind}$) :: invphi, invphi2
      #:if type[0] == "c"
      complex(${kind}$) :: alpha(4), step
      #:else
      real(${kind}$) :: alpha(4), step
      #:endif
      real(${kind}$) :: res(4)
      class(abstract_vector_${type[0]}$${kind}$), allocatable :: Xin, residual
      character(len=256) :: msg

      allocate(Xin, source=X)
      allocate(residual, source=X); call residual%zero()
      step    = one_${type[0]}$${kind}$
      invphi  = (sqrt(5.0) - 1.0)/2.0  ! 1 / phi
      invphi2 = (3.0 - sqrt(5.0))/2.0  ! 1 / phi**2
      alpha = (/ zero_${type[0]}$${kind}$, invphi2*one_${type[0]}$${kind}$, invphi*one_${type[0]}$${kind}$, one_${type[0]}$${kind}$ /)
      res   = (/ rold, zero_r${kind}$, zero_r${kind}$, zero_r${kind}$ /)

      call X%add(increment)
      ! evaluate residual norm
      call sys%eval(X, residual, tol)
      res(4) = residual%norm()

      if (res(4) > rold) then
         write(msg,'(A)') 'Start Newton step bisection ... '
         call logger%log_information(msg, module=this_module, procedure='increment_bisection_${type[0]}$${kind}$')
         ! compute new trial solutions
         do j = 2, 3
            call copy(X, Xin)
            call X%axpby(one_${type[0]}$${kind}$, increment, alpha(j))
            call sys%eval(X, residual, tol)
            res(j) = residual%norm()
         end do

         do i = 1, maxstep
            step = step * invphi
            if (res(2) < res(3)) then
               ! alphas
               ! a1 is kept
               alpha(3:4) = alpha(2:3)           
               ! residuals
               ! r1 is kept
               res(3:4) = res(2:3)
               ! new point --> a2, r2
               alpha(2) = alpha(1) + step * invphi2
               call copy(X, Xin)
               call X%axpby(one_${type[0]}$${kind}$, increment, alpha(2))
               call sys%eval(X, residual, tol)
               res(2) = residual%norm()
            else
               ! alphas
               alpha(1:2) = alpha(2:3)
               ! a4 is kept
               ! residuals
               res(1:2) = res(2:3)
               ! r4 is kept
               ! new point --> a3, r3
               alpha(3) = alpha(1) + step * invphi
               call copy(X, Xin)
               call X%axpby(one_${type[0]}$${kind}$, increment, alpha(3))
               call sys%eval(X, residual, tol)
               res(3) = residual%norm()
            end if
            write(msg,'(4X,I0,2(A,F6.4))') i, ': New interval: ', alpha(1), ' <= alpha <= ', alpha(4)
            call logger%log_information(msg, module=this_module, procedure='increment_bisection_${type[0]}$${kind}$')
         end do
         ! set new vector to optimal step
         idx = minloc(res)
         write(msg,'(A,F6.4)') 'Optimal damping: alpha= ', alpha(idx(1))
         call logger%log_information(msg, module=this_module, procedure='increment_bisection_${type[0]}$${kind}$')
         call copy(X, Xin)
         call X%axpby(one_${type[0]}$${kind}$, increment, alpha(idx(1)))
      else
         write(msg,'(A)') 'Full Newton step reduces the residual. Skip bisection.'
         call logger%log_information(msg, module=this_module, procedure='increment_bisection_${type[0]}$${kind}$')
      end if

      return
   end subroutine

   #:endfor

   #:for kind, type in REAL_KINDS_TYPES
   !--------------------------------------------------------------------
   !-----     Definition of two basic tolerance schedulers (${kind}$)    -----
   !--------------------------------------------------------------------

   subroutine constant_atol_${kind}$(tol, target_tol, rnorm, iter, info)
      !! Abstract interface to define tolerance scheduler for the Newton iteration
      real(${kind}$), intent(out) :: tol
      !! Tolerance to be used
      real(${kind}$), intent(in) :: target_tol
      !! Target tolerance
      real(${kind}$), intent(in)  :: rnorm
      !! Norm of the residual of the current iterate
      integer,  intent(in)  :: iter
      !! Newton iteration count
      integer,  intent(out)  :: info
      !! Information flag
      character(len=256) :: msg
      tol = target_tol
      write(msg,'(A,E9.2)') 'Solver tolerance set to tol= ', tol
      call logger%log_information(msg, module=this_module, procedure='constant_atol_${kind}$')
      return
   end subroutine constant_atol_${kind}$

   subroutine dynamic_tol_${kind}$(tol, target_tol, rnorm, iter, info)
      !! Abstract interface to define tolerance scheduler for the Newton iteration
      real(${kind}$), intent(out) :: tol
      !! Tolerance to be used
      real(${kind}$), intent(in) :: target_tol
      !! Target tolerance
      real(${kind}$), intent(in)  :: rnorm
      !! Norm of the residual of the current iterate
      integer,  intent(in)  :: iter
      !! Newton iteration count
      integer,  intent(out)  :: info
      !! Information flag
      ! internals
      real(${kind}$) :: tol_old
      character(len=256) :: msg
      
      tol_old = tol
      tol = max(0.1*rnorm, target_tol)

      if (tol /= tol_old) then
         if (tol == target_tol) then
            write(msg,'(A,E9.2)') 'Solver tolerance set to input target. tol= ', tol
         else
            write(msg,'(A,E9.2)') 'Solver tolerance set to tol= ', tol
         end if
         call logger%log_information(msg, module=this_module, procedure='dynamic_tol_${kind}$')
      end if
      return
   end subroutine dynamic_tol_${kind}$

   #:endfor

end module LightKrylov_NewtonKrylov
