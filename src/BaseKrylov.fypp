#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module lightkrylov_BaseKrylov
    !!  This module provides a collection of Krylov-based factorizations forming the
    !!  computational core of `LightKrylov`. It also provides a set of utility functions
    !!  to operate on arrays of `abstract_vector`. The most important ones are:
    !!
    !!  - `arnoldi(A, X, H, info)`: Arnoldi factorization for general square matrices.
    !!  - `lanczos(A, X, H, info)`: Lanczos factorization for general symmetric/hermitian matrices.
    !!  - `bidiagonalization(A, U, V, B)`: Lanczos bidiagonalization for arbitrary matrices.
    !!  - `qr(X, R, perm, info)`: QR factorization (with and without column pivoting) of an array of `abstract_vector`.


    !--------------------------------------------
    !-----     Standard Fortran Library     -----
    !--------------------------------------------
    use iso_fortran_env
    use stdlib_optval, only: optval
    use stdlib_linalg, only: eye

    !-------------------------------
    !-----     LightKrylov     -----
    !-------------------------------
    use LightKrylov_Constants
    use LightKrylov_Logger
    use LightKrylov_Utils
    use LightKrylov_AbstractVectors
    use LightKrylov_AbstractLinops

    implicit none
    private
    
    character(len=128), parameter :: this_module = 'LightKrylov_BaseKrylov'

    public :: qr
    public :: apply_permutation_matrix
    public :: apply_inverse_permutation_matrix
    public :: arnoldi
    public :: initialize_krylov_subspace
    public :: is_orthonormal
    public :: orthonormalize_basis
    public :: orthogonalize_against_basis
    public :: lanczos_bidiagonalization
    public :: lanczos_tridiagonalization
    public :: krylov_schur
    public :: double_gram_schmidt_step
    #:for kind in REAL_KINDS
    public :: eigvals_select_${kind}$
    #:endfor

    interface qr
        !!  ### Description
        !!
        !!  Given an array \( X \) of types derived from `abstract_vector`, it computes the
        !!  *in-place* QR factorization of \( X \), i.e.
        !!
        !!  \[
        !!      X = QR,
        !!  \]
        !!
        !!  where \( Q \) is an orthonormal arrays of vectors such that \( Q^H Q = I \) and
        !!  \( R \) is upper triangular. Note that it can also perform the QR factorization
        !!  with column pivoting
        !!
        !!  \[
        !!      XP = QR
        !!  \]
        !!
        !!  where \( P \) is a permutation matrix ensuring that the diagonal entries of \( R \)
        !!  have non-increasing absolute values. This amounts to using the pivoting QR as a
        !!  rank-revealing factorization.
        !!
        !!  **References**
        !!
        !!  - G. H. Golub & C. F. Van Loan. "Matrix Computations". 4th edition, The John Hopkins
        !!   University Press, 2013.
        !!   See Chapter 5.2.8: Modified Gram-Schmidt algorithm.
        !!
        !!  ### Syntax
        !!
        !!  ```fortran
        !!      call qr(Q [, R] [, perm], info [, tol])
        !!  ```
        !!
        !!  ### Arguments
        !!
        !!  `Q`: Array of types derived from one of the base types provided in the
        !!       `AbstractVectors` module. On entry, it contains the original array.
        !!       On exit, it is overwritten by the orthogonal basis for its span.
        !!       It is an `intent(inout)` argument.
        !!
        !!  `R`: `real` or `complex` rank-2 array. On exit, its contains the upper triangular
        !!        matrix resulting from the QR factorization. It is an `intent(out)` argument.
        !!
        !!  `perm` (*optional*): Rank-1 array of `integer` corresponding to the indices of
        !!                       permuted columns. If `perm` is absent, the naive QR factorization
        !!                       is being computed.
        !!
        !!  `info`: `integer` information flag.
        !!
        !!  `tol` (*optional*): Numerical tolerance to determine whether two vectors are colinear
        !!                      or not. Default `tol = atol_sp` or `tol = atol_dp`.
        #:for kind, type in RC_KINDS_TYPES
        module procedure qr_no_pivoting_${type[0]}$${kind}$
        module procedure qr_with_pivoting_${type[0]}$${kind}$
        #:endfor
    end interface

    interface swap_columns
        #: for kind, type in RC_KINDS_TYPES
        module procedure swap_columns_${type[0]}$${kind}$
        #:endfor
    end interface

    interface apply_permutation_matrix
        !!  ### Description
        !!
        !!  Given an array \( X \) and a permutation vector \( p \), this function computes
        !!  *in-place* the column-permuted matrix
        !!
        !!  \[
        !!      X = X P
        !!  \]
        !!
        !!  where \( P \) is the column-permutation matrix constructed from the permutation
        !!  vector \( p \).
        !!
        !!  ### Syntax
        !!
        !!  ```fortran
        !!      call apply_permutation_matrix(X, perm)
        !!  ```
        !!
        !!  ### Arguments
        !!
        !!  `Q` : Array of vectors derived from the base types defined in the `AbstractVectors`
        !!       module. On entry, it is the original array. On exit, it contains the
        !!       column-permuted version computed in-place. It is an `intent(inout)` argument.
        !!
        !!  `perm` : Rank-1 array of `integer` corresponding to the desired permutation vector.
        !!          It is an `intent(in)` argument.
        #: for kind, type in RC_KINDS_TYPES
        module procedure apply_permutation_matrix_${type[0]}$${kind}$
        module procedure apply_permutation_matrix_array_${type[0]}$${kind}$
        #:endfor
    end interface

    interface apply_inverse_permutation_matrix
        !!  ### Description
        !!
        !!  Given an array \( X \) and a permutation vector \( p \), this function computes
        !!  *in-place* the column-permuted matrix
        !!
        !!  \[
        !!      X = X P^{-1}
        !!  \]
        !!
        !!  where \( P \) is the column-permutation matrix constructed from the permutation
        !!  vector \( p \) and \( P^{-1} \) its inverse.
        !!
        !!  ### Syntax
        !!
        !!  ```fortran
        !!      call apply_inverse_permutation_matrix(X, perm)
        !!  ```
        !!
        !!  ### Arguments
        !!
        !!  `Q` : Array of vectors derived from the base types defined in the `AbstractVectors`
        !!       module. On entry, it is the original array. On exit, it contains the
        !!       column-permuted version computed in-place. It is an `intent(inout)` argument.
        !!
        !!  `perm` : Rank-1 array of `integer` corresponding to the desired permutation vector.
        !!          It is an `intent(in)` argument.
        #:for kind, type in RC_KINDS_TYPES
        module procedure apply_inverse_permutation_matrix_${type[0]}$${kind}$
        module procedure apply_inverse_permutation_matrix_array_${type[0]}$${kind}$
        #:endfor
    end interface

    interface arnoldi
        !!  ### Description
        !!
        !!  Given a square linear operator \( A \), find matrices \( X \) and \( H \) such that
        !!
        !!  \[
        !!      AX_k = X_k H_k + h_{k+1, k} x_{k+1} e_k^T,
        !!  \]
        !!
        !!  where \( X \) is an orthogonal basis and \( H \) is upper Hessenberg.
        !!
        !!  **Algorithmic Features**
        !!
        !!  - The operator \( A \) only needs to be accessed through matrix-vector products.
        !!  - Constructs an orthonormal Krylov basis \( X \) via the Gram-Schmidt process.
        !!  - Constructs an upper Hessenberg matrix \( H \) whose eigenvalues approximates those of \( A \).
        !!  - Checks for convergence and invariant subspaces.
        !   - Block version available (experimental).
        !!
        !!  **References**
        !!
        !!  - Y. Saad. "Iterative methods for sparse linear systems", SIAM 2nd edition, 2003.
        !!    see Chapter 6.3: Arnoldi's method.
        !!
        !!  ### Syntax
        !!
        !!  ```fortran
        !!      call arnoldi(A, X, H, info [, kstart] [, kend] [, tol] [, transpose] [, blksize])
        !!  ```
        !!
        !!  ### Arguments
        !!
        !!  `A` : Linear operator derived from one the base types provided by the `AbstractLinops`
        !!        module. The operator needs to be square, i.e. the dimension of its domain and
        !!        co-domain is the same. It is an `intent(in)` argument.
        !!
        !!  `X` : Array of types derived from one the base types provided by the `AbstractVectors`
        !!        module. It needs to be consistent with the type of `A`. On exit, it contains the
        !!        the computed Krylov vectors. The first entry `X(1)` is the starting vector for
        !!        the Arnoldi factorization. Additionally, the maximum number of Arnoldi steps
        !!        is equal to `size(X) - 1`. It is an `intent(inout)` argument.
        !!
        !!  `H` : `real` or `complex` rank-2 array. On exit, it contains the \( (k+1) \times k\)
        !!         upper Hessenberg matrix computed from the Arnoldi factorization. It is an
        !!         `intent(inout)` argument.
        !!
        !!  `info` : `integer` variable. It is the `LightKrylov` information flag. On exit, if
        !!           `info` > 0, the Arnoldi factorization experienced a lucky breakdown. 
        !!            The array of Krylov vectors `X` spans an \(A\)-invariant subpsace of
        !!            dimension `info`.
        !!
        !!  `kstart` (*optional*): `integer` value determining the index of the first Arnoldi
        !!                          step to be computed. By default, `kstart = 1`.
        !!
        !!  `kend` (*optional*): `integer` value determining the index of the last Arnoldi step
        !!                        to be computed. By default, `kend = size(X) - 1`.
        !!
        !!  `tol` (*optional*): Numerical tolerance below which a subspace is considered
        !!                      to be \( A \)-invariant. By default `tol = atol_sp` or
        !!                      `tol = atol_rp` depending on the kind of `A`.
        !!
        !!  `transpose` (*optional*): `logical` flag determining whether the Arnoldi factorization
        !!                             is applied to \( A \) or \( A^H \). Default `transpose = .false.`
        !!
        !!  `blksize` (*optional*): `integer` value determining the dimension of a block for the
        !!                          block Arnoldi factorization. Default is `blksize=1`.
        #:for kind, type in RC_KINDS_TYPES
        module procedure arnoldi_${type[0]}$${kind}$
        #:endfor
    end interface

    interface initialize_krylov_subspace
        !!  ### Description
        !!
        !!  Utility function to initialize a basis for a Krylov subspace.
        !!
        !!  ### Syntax
        !!
        !!  ```fortran
        !!      call initialize_krylov_subspace(X [, X0])
        !!  ```
        !!
        !!  ### Arguments
        !!
        !!  `X` : Array of vectors that needs to be initialized. It is an `intent(inout)`
        !!        argument. Note that the first action in the subroutine is
        !!        `call zero_basis(X)`, effectively zeroing-out any data stored.
        !!
        !!  `X0` (*optional*) : Collection of vectors which will form the first few
        !!                      Krylov vectors. Note that `X0` need not be an orthonormal
        !!                      basis as this subroutine includes a `call qr(X0)`.
        #:for kind, type in RC_KINDS_TYPES
        module procedure initialize_krylov_subspace_${type[0]}$${kind}$
        #:endfor
    end interface

    interface is_orthonormal
        !!  ### Description
        !!
        !!  Utility function returning a logical `.true.` if the set of vectors stored in \( X \) form
        !!  an orthonormal set of vectors and `.false.` otherwise.
        !!
        !!  ### Syntax
        !!
        !!  ```fortran
        !!      out = is_orthonormal(X)
        !!  ```
        !!
        !!  ### Arguments
        !!
        !!  `X` : Array of derived types extended from the base types provided in the
        !!        `AbstractVectors` module.
        #:for kind, type in RC_KINDS_TYPES
        module procedure is_orthonormal_${type[0]}$${kind}$
        #:endfor
    end interface

    interface orthonormalize_basis
        #:for kind, type in RC_KINDS_TYPES
        module procedure orthonormalize_basis_${type[0]}$${kind}$
        #:endfor
    end interface

    interface orthogonalize_against_basis
        #:for kind, type in RC_KINDS_TYPES
        module procedure orthogonalize_vector_against_basis_${type[0]}$${kind}$
        module procedure orthogonalize_basis_against_basis_${type[0]}$${kind}$
        #:endfor
    end interface

    interface double_gram_schmidt_step
        !!  ### Description
        !!
        !!  Given an array \( X \) of `abstract_vector` and an `abstract_vector` 
        !!  (or array of `abstract_vectors`) \( y \), this subroutine returns a modified 
        !!  vector \( y \) orthogonal to all columns of \( X \), i.e.
        !!
        !!  \[
        !!      y \leftarrow \left( I - XX^H \right) y,
        !!  \]
        !!
        !!  using a double Gram-Schmidt process. On exit, \( y \) is orthogonal to \( X \) albeit
        !!  does not have unit-norm. Note moreover that \( X \) is assumed to be an orthonormal 
        !!  set of vectors. The function can also return the projection coefficients 
        !!  \( \beta = X^H y \).
        !!
        !!  ### Syntax
        !!
        !!  ```fortran
        !!      call double_gram_schmidt_step(y, X, info [, if_chk_orthonormal] [, beta])
        !!  ```
        !!
        !!  ### Arguments
        !!
        !!  `y` : `abstract_vector` (or array of `abstract_vector`) that needs to be
        !!        orthogonalize **in-place** against \( X \).
        !!
        !!  `X` : Array of `abstract_vector` against which \( y \) needs to be orthogonalized.
        !!        Note the function assumes that \( X \) is an orthonormal set of vectors, i.e.
        !!        \( X^H X = I \). If it this is not the case, the result are meaningless.
        !!
        !!  `info` : `integer` Information flag.
        !!
        !!  `if_chk_orthonormal` (*optional*) : `logical` flag (default `.true.`) to check
        !!      whether \( X \) is an orthonormal set of vectors or not. If the orthonormality
        !!      returns `.false.`, the function throws an error. Note that this check is however
        !!      computationally expensive and can be disable for the sake of performances.
        !!
        !!  `beta` (*optional*) : `real` or `complex` array containing the coefficients
        !!      \( \beta = X^H y \).
        #:for kind, type in RC_KINDS_TYPES
        module procedure DGS_vector_against_basis_${type[0]}$${kind}$
        module procedure DGS_basis_against_basis_${type[0]}$${kind}$
        #:endfor
    end interface

    interface lanczos_tridiagonalization
        !!  ### Description
        !!
        !!  Given a symmetric or Hermitian linear operator \( A \), find matrices \( X \) and
        !!  \( T \) such that
        !!
        !!  \[
        !!      AX_k = X_k T_k + t_{k+1, k} x_{k+1} e_k^T,
        !!  \]
        !!
        !!  where \( X \) is an orthogonal basis and \( T \) is symmetric tridiagonal.
        !!
        !!  **Algorithmic Features**
        !!
        !!  - The operator \( A \) only needs to be accessed through matrix-vector products.
        !!  - Constructs an orthonormal Krylov basis \( X \) via the Lanczos process with full
        !!    reorthogonalization.
        !!  - Constructs a symmetric tridiagonal matrix \( T \) whose eigenvalues approximates those of \( A \).
        !!  - Checks for convergence and invariant subspaces.
        !!
        !!  **References**
        !!
        !!  - Y. Saad. "Iterative methods for sparse linear systems", SIAM 2nd edition, 2003.
        !!    see Chapter 6.6: The symmetric Lanczos algorithm.
        !!
        !!  ### Syntax
        !!
        !!  ```fortran
        !!      call lanczos(A, X, T, info [, kstart] [, kend] [, tol])
        !!  ```
        !!
        !!  ### Arguments
        !!
        !!  `A` : Symmetric or Hermitian linear operator derived from one the base types 
        !!        provided by the `AbstractLinops` module. It is an `intent(in)` argument.
        !!
        !!  `X` : Array of types derived from one the base types provided by the `AbstractVectors`
        !!        module. It needs to be consistent with the type of `A`. On exit, it contains the
        !!        the computed Krylov vectors. The first entry `X(1)` is the starting vector for
        !!        the Lanczos factorization. Additionally, the maximum number of Lanczos steps
        !!        is equal to `size(X) - 1`. It is an `intent(inout)` argument.
        !!
        !!  `T` : `real` or `complex` rank-2 array. On exit, it contains the \( (k+1) \times k\)
        !!         symmetric tridiagonal matrix computed from the Arnoldi factorization. It is an
        !!         `intent(inout)` argument.
        !!
        !!  `info` : `integer` variable. It is the `LightKrylov` information flag. On exit, if
        !!           `info` > 0, the Lanczos factorization experienced a lucky breakdown. 
        !!            The array of Krylov vectors `X` spans an \(A\)-invariant subpsace of
        !!            dimension `info`.
        !!
        !!  `kstart` (*optional*): `integer` value determining the index of the first Lanczos
        !!                          step to be computed. By default, `kstart = 1`.
        !!
        !!  `kend` (*optional*): `integer` value determining the index of the last Lanczos step
        !!                        to be computed. By default, `kend = size(X) - 1`.
        !!
        !!  `tol` (*optional*): Numerical tolerance below which a subspace is considered
        !!                      to be \( A \)-invariant. By default `tol = atol_sp` or
        !!                      `tol = atol_rp` depending on the kind of `A`.
        #:for kind, type in RC_KINDS_TYPES
        module procedure lanczos_tridiagonalization_${type[0]}$${kind}$
        #:endfor
    end interface

    interface lanczos_bidiagonalization
        !!  ### Description
        !!
        !!  Given a general linear operator \( A \), find matrices \( U \), \( V \) and
        !!  \( B \) such that
        !!
        !!  \[
        !!      \begin{aligned}
        !!          AV_k & = U_k T_k + t_{k+1, k} v_{k+1} e_k^T, \\
        !!          A^H U_k & = V_k T_k^T + t_{k+1, k} u_{k+1} e_k^T
        !!      \end{aligned}
        !!  \]
        !!
        !!  where \( U \) and \( V \) are orthogonal bases for the column span and row span
        !!  of \( A \), respectively, and \( B \) is a bidiagonal matrix.
        !!
        !!  **Algorithmic Features**
        !!
        !!  - The operator \( A \) only needs to be accessed through matrix-vector products.
        !!  - Constructs an orthonormal Krylov basis \( U \) for the column span of \( A \).
        !!  - Constructs an orthonormal Krylov basis \( V \) for the row span of \( A \).
        !!  - Constructs a bidiagonal matrix \( B \) whose singular values approximates 
        !!    those of \( A \).
        !!  - Checks for convergence and invariant subspaces.
        !!
        !!  **References**
        !!
        !!  - R. M. Larsen. "Lanczos bidiagonalization with partial reorthogonalization." 
        !!    Technical Report, 1998. [(PDF)](http://sun.stanford.edu/~rmunk/PROPACK/paper.pdf)
        !!
        !!  ### Syntax
        !!
        !!  ```fortran
        !!      call bidiagonalization(A, U, V, B, info [, kstart] [, kend] [, tol])
        !!  ```
        !!
        !!  ### Arguments
        !!
        !!  `A` : Linear operator derived from one the base types provided by the 
        !!        `AbstractLinops` module. It is an `intent(in)` argument.
        !!
        !!  `U` : Array of types derived from one the base types provided by the `AbstractVectors`
        !!        module. It needs to be consistent with the type of `A`. On exit, it contains the
        !!        the computed Krylov vectors for the column span of `A`. The first entry `U(1)` 
        !!        is the starting vector for the Lanczos factorization. Additionally, the 
        !!        maximum number of Lanczos steps is equal to `size(X) - 1`. 
        !!        It is an `intent(inout)` argument.
        !!
        !!  `V` : Array of types derived from one the base types provided by the `AbstractVectors`
        !!        module. It needs to be consistent with the type of `A`. On exit, it contains the
        !!        the computed Krylov vectors for the row span of `A`. It is an `intent(inout)` 
        !!        argument.
        !!
        !!  `B` : `real` or `complex` rank-2 array. On exit, it contains the \( (k+1) \times k\)
        !!         bidiagonal matrix computed from the Lanczos factorization. It is an
        !!         `intent(inout)` argument.
        !!
        !!  `info` : `integer` variable. It is the `LightKrylov` information flag. On exit, if
        !!           `info` > 0, the Lanczos factorization experienced a lucky breakdown. 
        !!
        !!  `kstart` (*optional*): `integer` value determining the index of the first Lanczos
        !!                          step to be computed. By default, `kstart = 1`.
        !!
        !!  `kend` (*optional*): `integer` value determining the index of the last Lanczos step
        !!                        to be computed. By default, `kend = size(X) - 1`.
        !!
        !!  `tol` (*optional*): Numerical tolerance below which a subspace is considered
        !!                      to be \( A \)-invariant. By default `tol = atol_sp` or
        !!                      `tol = atol_rp` depending on the kind of `A`.
        #:for kind, type in RC_KINDS_TYPES
        module procedure lanczos_bidiagonalization_${type[0]}$${kind}$
        #:endfor
    end interface

    interface krylov_schur
        #:for kind, type in RC_KINDS_TYPES
        module procedure krylov_schur_${type[0]}$${kind}$
        #:endfor
    end interface

    !----------------------------------------------------------
    !-----     ABSTRACT EIGENVALUE SELECTOR INTERFACE     -----
    !----------------------------------------------------------

    abstract interface
        #:for kind in REAL_KINDS
        function eigvals_select_${kind}$(lambda) result(selected)
            import ${kind}$
            complex(${kind}$), intent(in) :: lambda(:)
            logical                       :: selected(size(lambda))
        end function eigvals_select_${kind}$
        #:endfor
    end interface

contains

    !-------------------------------------
    !-----     UTILITY FUNCTIONS     -----
    !-------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    logical function is_orthonormal_${type[0]}$${kind}$(X) result(ortho)
        class(abstract_vector_${type[0]}$${kind}$), intent(in) :: X(:)
        ${type}$, dimension(size(X), size(X)) :: G
        ortho = .true.
        call innerprod(G, X, X)
        if (norm2(abs(G - eye(size(X)))) > rtol_sp) then
            ! The basis is not orthonormal. Cannot orthonormalize.
            ortho = .false.
        end if
    end function
    #:endfor

    #:for kind, type in RC_KINDS_TYPES
    subroutine initialize_krylov_subspace_${type[0]}$${kind}$(X, X0)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        class(abstract_vector_${type[0]}$${kind}$), optional, intent(in) :: X0(:)

        ! Internal variables.
        integer :: p

        ! Zero-out X.
        call zero_basis(X)

        ! Deals with optional args.
        if(present(X0)) then
            p = size(X0)
            ! Initialize.
            call copy_basis(X(:p), X0)
            ! Orthonormalize.
            call orthonormalize_basis(X(:p))
        endif

        return
    end subroutine initialize_krylov_subspace_${type[0]}$${kind}$

    subroutine orthonormalize_basis_${type[0]}$${kind}$(X)
      !! Orthonormalizes the `abstract_vector` basis `X`
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
      !! Input `abstract_vector` basis to orthogonalize against
      
      ! internals
      ${type}$ :: R(size(X),size(X))
      integer :: info

      ! internals
      call qr(X, R, info)
      call check_info(info, 'qr', module=this_module, procedure='orthonormalize_basis_${type[0]}$${kind}$')
      
      return
    end subroutine orthonormalize_basis_${type[0]}$${kind}$

    subroutine orthogonalize_vector_against_basis_${type[0]}$${kind}$(y, X, info, if_chk_orthonormal, beta)
      !! Orthogonalizes the `abstract_vector` `y` against a basis `X` of `abstract_vector`.
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: y
      !! Input `abstract_vector` to orthogonalize
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: X(:)
      !! Input `abstract_vector` basis to orthogonalize against
      integer, intent(out) :: info
      !! Information flag.
      logical,                          optional, intent(in)    :: if_chk_orthonormal
      logical                                                   :: chk_X_orthonormality
      !! Check that input Krylov vectors `X` form an orthonormal basis (expensive!)
      ${type}$,                         optional, intent(out)   :: beta(:)
      !! Projection coefficients if requested

      ! internals
      ${type}$ :: proj_coefficients(size(X))

      info = 0

      ! optional input argument
      chk_X_orthonormality = optval(if_chk_orthonormal, .true.) ! default to true!

      ! check for zero vector
      if (y%norm() < atol_${kind}$) info = 1

      if (chk_X_orthonormality) then
         block 
            ${type}$, dimension(size(X), size(X)) :: G
            call innerprod(G, X, X)
            if (abs(G(size(X),size(X))) < rtol_${kind}$) then
               ! The last vector in X is zero, it does not impact orthogonalisation
               info = -2
            else if (norm2(abs(G - eye(size(X)))) > rtol_${kind}$) then
               ! The basis is not orthonormal. Cannot orthonormalize.
               info = -1
               return
            end if
         end block
      end if

      ! orthogonalize
      call innerprod(proj_coefficients, X, y)
      block
         class(abstract_vector_${type[0]}$${kind}$), allocatable :: proj
         call linear_combination(proj, X, proj_coefficients)
         call y%sub(proj)
      end block

      if (present(beta)) then
         ! check size
         call assert_shape(beta, shape(proj_coefficients), 'beta', this_module, 'orthogonalize_vector_against_basis_${type[0]}$${kind}$')
         beta = proj_coefficients
      end if
      
      return
    end subroutine orthogonalize_vector_against_basis_${type[0]}$${kind}$

    subroutine orthogonalize_basis_against_basis_${type[0]}$${kind}$(Y, X, info, if_chk_orthonormal, beta)
      !! Orthogonalizes the `abstract_vector` basis `Y` against a basis `X` of `abstract_vector`.
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Y(:)
      !! Input `abstract_vector` basis to orthogonalize
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: X(:)
      !! Input `abstract_vector` basis to orthogonalize against
      integer, intent(out) :: info
      !! Information flag.
      logical,                          optional, intent(in)    :: if_chk_orthonormal
      logical                                                   :: chk_X_orthonormality
      !! Check that input Krylov vectors `X` form an orthonormal basis (expensive!)
      ${type}$,                         optional, intent(out)   :: beta(:,:)
      !! Projection coefficients if requested

      ! internals
      ${type}$ :: proj_coefficients(size(X), size(Y))
      integer :: i

      info = 0

      ! optional input argument
      chk_X_orthonormality = optval(if_chk_orthonormal, .true.) ! default to true!

      ! check for zero vector
      do i = 1, size(Y)
         if ( Y(i)%norm() < atol_${kind}$) info = i
      end do

      if (chk_X_orthonormality) then
         block 
            ${type}$, dimension(size(X), size(X)) :: G
            call innerprod(G, X, X)
            if (abs(G(size(X),size(X))) < rtol_${kind}$) then
               ! The last vector in X is zero, it does not impact orthogonalisation
               info = -2
            else if (norm2(abs(G - eye(size(X)))) > rtol_${kind}$) then
               ! The basis is not orthonormal. Cannot orthonormalize.
               info = -1
               return
            end if
         end block
      end if
      
      ! orthogonalize
      call innerprod(proj_coefficients, X, Y)
      block
         class(abstract_vector_${type[0]}$${kind}$), allocatable :: proj(:)
         call linear_combination(proj, X, proj_coefficients)
         call axpby_basis(Y, one_${type[0]}$${kind}$, proj, -one_${type[0]}$${kind}$)
      end block

      if (present(beta)) then
         ! check size
         call assert_shape(beta, shape(proj_coefficients), 'beta', this_module, 'orthogonalize_basis_against_basis_${type[0]}$${kind}$')
         beta = proj_coefficients
      end if
      
      return
    end subroutine orthogonalize_basis_against_basis_${type[0]}$${kind}$

    subroutine DGS_vector_against_basis_${type[0]}$${kind}$(y, X, info, if_chk_orthonormal, beta)
      !! Computes one step of the double Gram-Schmidt orthogonalization process of the
      !! `abstract_vector` `y` against the `abstract_vector` basis `X`
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: y
      !! Input `abstract_vector` to orthogonalize
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: X(:)
      !! Input `abstract_vector` basis to orthogonalize against
      integer, intent(out) :: info
      !! Information flag.
      logical,                          optional, intent(in)    :: if_chk_orthonormal
      logical                                                   :: chk_X_orthonormality
      !! Check that input Krylov vectors `X` form an orthonormal basis (expensive!)
      ${type}$,                         optional, intent(out)   :: beta(:)
      !! Projection coefficients if requested

      ! internals
      ${type}$, dimension(size(X)) :: proj_coefficients, wrk

      ! optional input argument
      chk_X_orthonormality = optval(if_chk_orthonormal, .true.) ! default to true!

      info = 0

      proj_coefficients = zero_${type[0]}$${kind}$; wrk = zero_${type[0]}$${kind}$

      ! Orthogonalize vector y w.r.t. to Krylov basis X in two passes of GS.
      ! first pass
      call orthogonalize_against_basis(y, X, info, if_chk_orthonormal=.false., beta=proj_coefficients)
      call check_info(info, 'orthogonalize_against_basis_p1', module=this_module, &
                        & procedure='DGS_vector_against_basis_${type[0]}$${kind}$, pass 1')
      ! second pass
      call orthogonalize_against_basis(y, X, info, if_chk_orthonormal=.false., beta=wrk)
      call check_info(info, 'orthogonalize_against_basis_p2', module=this_module, &
                        & procedure='DGS_vector_against_basis_${type[0]}$${kind}$, pass 2')
      ! combine passes
      proj_coefficients = proj_coefficients + wrk

      if (present(beta)) then
         ! check size
         call assert_shape(beta, shape(proj_coefficients), 'beta', this_module, 'DGS_vector_against_basis_${type[0]}$${kind}$')
         beta = proj_coefficients
      end if

    end subroutine DGS_vector_against_basis_${type[0]}$${kind}$

    subroutine DGS_basis_against_basis_${type[0]}$${kind}$(y, X, info, if_chk_orthonormal, beta)
      !! Computes one step of the double Gram-Schmidt orthogonalization process of the
      !! `abstract_vector` `y` against the `abstract_vector` basis `X`
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Y(:)
      !! Input `abstract_vector` basis to orthogonalize
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: X(:)
      !! Input `abstract_vector` basis to orthogonalize against
      integer, intent(out) :: info
      !! Information flag.
      logical,                          optional, intent(in)    :: if_chk_orthonormal
      logical                                                   :: chk_X_orthonormality
      !! Check that input Krylov vectors `X` form an orthonormal basis (expensive!)
      ${type}$,                         optional, intent(out)   :: beta(:,:)
      !! Projection coefficients if requested

      ! internals
      ${type}$, dimension(size(X),size(Y)) :: proj_coefficients, wrk

      ! optional input argument
      chk_X_orthonormality = optval(if_chk_orthonormal, .true.) ! default to true!

      info = 0

      proj_coefficients = zero_${type[0]}$${kind}$; wrk = zero_${type[0]}$${kind}$

      ! Orthogonalize Krylov basis Y w.r.t. to Krylov basis X in two passes of GS.
      ! first pass
      call orthogonalize_against_basis(Y, X, info, if_chk_orthonormal=.false., beta=proj_coefficients)
      call check_info(info, 'orthogonalize_against_basis_p1', module=this_module, procedure='DGS_basis_against_basis_${type[0]}$${kind}$, first pass')
      ! second pass
      call orthogonalize_against_basis(Y, X, info, if_chk_orthonormal=.false., beta=wrk)
      call check_info(info, 'orthogonalize_against_basis_p2', module=this_module, procedure='DGS_basis_against_basis_${type[0]}$${kind}$, second pass')
      ! combine passes
      proj_coefficients = proj_coefficients + wrk

      if (present(beta)) then
         ! check size
         call assert_shape(beta, shape(proj_coefficients), 'beta', this_module, 'DGS_basis_against_basis_${type[0]}$${kind}$')
         beta = proj_coefficients
      end if

    end subroutine DGS_basis_against_basis_${type[0]}$${kind}$  

    #:endfor

    !------------------------------------
    !-----     QR FACTORIZATION     -----
    !------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine qr_no_pivoting_${type[0]}$${kind}$(Q, R, info, tol)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Array of `abstract_vector` to be orthogonalized.
        ${type}$, intent(out) :: R(:, :)
        !! Upper triangular matrix \(\mathbf{R}\) resulting from the QR factorization.
        integer, intent(out) :: info
        !! Information flag.
        real(${kind}$), optional, intent(in) :: tol
        !! Tolerance to determine colinearity.
        real(${kind}$)                       :: tolerance

        ! Internal variables.
        ${type}$ :: beta
        integer :: j
        logical :: flag
        character(len=128) :: msg

        ! Deals with the optional args.
        tolerance = optval(tol, atol_${kind}$)

        info = 0 ; flag = .false.; R = zero_r${kind}$ ; beta = zero_r${kind}$
        do j = 1, size(Q)
            if (j > 1) then
                ! Double Gram-Schmidt orthogonalization
                call double_gram_schmidt_step(Q(j), Q(:j-1), info, if_chk_orthonormal=.false., beta = R(:j-1,j))
                call check_info(info, 'double_gram_schmidt_step', module=this_module, procedure='qr_no_pivoting_${type[0]}$${kind}$')
            end if        

            ! Check for breakdown.
            beta = Q(j)%norm()
            if (abs(beta) < tolerance) then
                if (.not.flag) then
                    flag = .true.
                    info = j
                    write(msg,'(A,I0,A,E15.8)') 'Colinear column detected after ', j, ' steps. beta= ', abs(beta)
                    call logger%log_information(msg, module=this_module, procedure='qr_no_pivoting_${type[0]}$${kind}$')
                end if
                R(j, j) = zero_r${kind}$
                call Q(j)%rand()
                if (j > 1) then
                    call double_gram_schmidt_step(Q(j), Q(:j-1), info, if_chk_orthonormal=.false.)
                    call check_info(info, 'double_gram_schmidt_step', module=this_module, procedure='qr_with_pivoting_${type[0]}$${kind}$')
                end if
                beta = Q(j)%norm()
            else
                R(j, j) = beta
            endif
            ! Normalize column.
            call Q(j)%scal(one_r${kind}$ / beta)
        enddo

        return
    end subroutine qr_no_pivoting_${type[0]}$${kind}$

    subroutine qr_with_pivoting_${type[0]}$${kind}$(Q, R, perm, info, tol)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Array of `abstract_vector` to be orthogonalized.
        ${type}$, intent(out) :: R(:, :)
        !! Upper triangular matrix resulting from the QR factorization.
        integer, intent(out) :: perm(size(Q))
        !! Permutation matrix.
        integer, intent(out) :: info
        !! Information flag.
        real(${kind}$), optional, intent(in) :: tol

        !! Tolerance to detect colinearity.
        real(${kind}$) :: tolerance

        ! Internal variables
        ${type}$ :: beta
        integer :: idx, i, j, kdim
        integer :: idxv(1)
        ${type}$  :: Rii(size(Q))
        character(len=128) :: msg

        info = 0 ; kdim = size(Q)
        R = zero_r${kind}$ ; Rii = zero_r${kind}$
        
        ! Deals with the optional arguments.
        tolerance = optval(tol, atol_${kind}$)

        ! Initialize diagonal entries.
        do i = 1, kdim
            perm(i) = i
            Rii(i) = Q(i)%dot(Q(i))
        enddo

        qr_step: do j = 1, kdim
            idxv = maxloc(abs(Rii)) ; idx = idxv(1)
            if (abs(Rii(idx)) < tolerance) then
                do i = j, kdim
                    call Q(i)%rand()
                    call double_gram_schmidt_step(Q(i), Q(:i-1), info, if_chk_orthonormal=.false.)
                    call check_info(info, 'double_gram_schmidt_step', module=this_module, procedure='qr_with_pivoting_${type[0]}$${kind}$')
                    beta = Q(i)%norm(); call Q(i)%scal(one_${type[0]}$${kind}$ / beta)
                enddo
                info = j
                write(msg,'(A,I0,A,E15.8)') 'Breakdown after ', j, ' steps. R_ii= ', abs(Rii(idx))
                call logger%log_information(msg, module=this_module, procedure='qr_with_pivoting_${type[0]}$${kind}$')
                exit qr_step
            endif

            call swap_columns(Q, R, Rii, perm, j, idx)

            ! Normalize column.
            beta = Q(j)%norm()
            if (abs(beta) < tolerance) then
                info = j
                R(j, j) = zero_r${kind}$
                call Q(j)%rand()
                call double_gram_schmidt_step(Q(j), Q(:j-1), info, if_chk_orthonormal=.false.)
                call check_info(info, 'double_gram_schmidt_step', module=this_module, procedure='qr_with_pivoting_${type[0]}$${kind}$')
                beta = Q(j)%norm()
            else
                R(j, j) = beta
            endif
            ! Normalize column.
            call Q(j)%scal(one_r${kind}$ / beta)

            ! Orthogonalize all columns against new vector.
            do i = j+1, kdim
                beta = Q(j)%dot(Q(i))
                call Q(i)%axpby(one_${type[0]}$${kind}$, Q(j), -beta)
                R(j, i) = beta
            enddo

            ! Update Rii.
            Rii(j) = zero_r${kind}$
            do i = j+1, kdim
                Rii(i) = Rii(i) - R(j, i)**2
            enddo

        enddo qr_step

        return
    end subroutine qr_with_pivoting_${type[0]}$${kind}$

    subroutine swap_columns_${type[0]}$${kind}$(Q, R, Rii, perm, i, j)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Vector basis whose i-th and j-th columns need swapping.
        ${type}$, intent(inout) :: R(:, :)
        !! Upper triangular matrix resulting from QR.
        ${type}$, intent(inout) :: Rii(:)
        !! Diagonal entries of R.
        integer, intent(inout) :: perm(:)
        !! Column ordering.
        integer, intent(in) :: i, j
        !! Index of the columns to be swapped.

        ! Internal variables.
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Qwrk
        ${type}$, allocatable :: Rwrk(:)
        integer :: iwrk, m, n

        ! Sanity checks.
        m = size(Q) ; n = min(i, j) - 1

        ! Allocations.
        allocate(Qwrk, source=Q(1)) ; call Qwrk%zero()
        allocate(Rwrk(max(1, n))) ; Rwrk = zero_r${kind}$

        ! Swap columns.
        call Qwrk%axpby(zero_${type[0]}$${kind}$, Q(j), one_${type[0]}$${kind}$)
        call Q(j)%axpby(zero_${type[0]}$${kind}$, Q(i), one_${type[0]}$${kind}$)
        call Q(i)%axpby(zero_${type[0]}$${kind}$, Qwrk, one_${type[0]}$${kind}$)
        
        Rwrk(1) = Rii(j); Rii(j) = Rii(i); Rii(i) = Rwrk(1)
        iwrk = perm(j); perm(j) = perm(i) ; perm(i) = iwrk

        if (n > 0) then
            Rwrk = R(:n, j) ; R(:n, j) = R(:n, i) ; R(:n, i) = Rwrk
        endif

        return
    end subroutine swap_columns_${type[0]}$${kind}$

    subroutine apply_permutation_matrix_${type[0]}$${kind}$(Q, perm)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Basis vectors to be permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Qwrk(:)

        allocate(Qwrk, source=Q)
        do i = 1, size(perm)
            call Q(i)%axpby(zero_${type[0]}$${kind}$, Qwrk(perm(i)), one_${type[0]}$${kind}$)
        enddo

        return
    end subroutine apply_permutation_matrix_${type[0]}$${kind}$

    subroutine apply_inverse_permutation_matrix_${type[0]}$${kind}$(Q, perm)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Basis vectors to be (un-) permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        integer :: inv_perm(size(perm))
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Qwrk(:)

        allocate(Qwrk, source=Q) ; inv_perm = 0

        ! Inverse permutation vector.
        do i = 1, size(perm)
            inv_perm(perm(i)) = i
        enddo

        ! Undo permutation.
        do i = 1, size(perm)
            call Q(i)%axpby(zero_${type[0]}$${kind}$, Qwrk(inv_perm(i)), one_${type[0]}$${kind}$)
        enddo

        return
    end subroutine apply_inverse_permutation_matrix_${type[0]}$${kind}$

    subroutine apply_permutation_matrix_array_${type[0]}$${kind}$(Q, perm)
        ${type}$, intent(inout) :: Q(:, :)
        !! Basis vectors to be permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        ${type}$, allocatable :: Qwrk(:, :)

        allocate(Qwrk, source=Q)
        do i = 1, size(perm)
            Q(:, i) = Qwrk(:, perm(i))
        enddo

        return
    end subroutine apply_permutation_matrix_array_${type[0]}$${kind}$

    subroutine apply_inverse_permutation_matrix_array_${type[0]}$${kind}$(Q, perm)
        ${type}$, intent(inout) :: Q(:, :)
        !! Basis vectors to be (un-) permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        integer :: inv_perm(size(perm))
        ${type}$, allocatable :: Qwrk(:, :)

        allocate(Qwrk, source=Q) ; inv_perm = 0

        ! Inverse permutation vector.
        do i = 1, size(perm)
            inv_perm(perm(i)) = i
        enddo

        ! Undo permutation.
        do i = 1, size(perm)
            Q(:, i) = Qwrk(:, inv_perm(i))
        enddo

        return
    end subroutine apply_inverse_permutation_matrix_array_${type[0]}$${kind}$


    #:endfor

    !-----------------------------------------
    !-----     ARNOLDI FACTORIZATION     -----
    !-----------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine arnoldi_${type[0]}$${kind}$(A, X, H, info, kstart, kend, tol, transpose, blksize)
        class(abstract_linop_${type[0]}$${kind}$), intent(in) :: A
        !! Linear operator to be factorized.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        !! Orthogonal basis for the generated Krylov subspace.
        ${type}$, intent(inout) :: H(:, :)
        !! Upper Hessenberg matrix.
        integer, intent(out) :: info
        !! Information flag.
        integer, optional, intent(in) :: kstart
        !! Starting index for the Arnoldi factorization (default 1).
        integer, optional, intent(in) :: kend
        !! Final index for the Arnoldi factorization (default `size(X)-1`)
        logical, optional, intent(in) :: transpose
        !! Whether \( \mathbf{A} \) is being transposed or not (default `.false.`)
        real(${kind}$), optional, intent(in) :: tol
        !! Tolerance to determine whether an invariant subspace has been computed or not.
        integer, optional, intent(in) :: blksize
        !! Block size for block Arnoldi (default 1).

        ! Internal variables.
        integer :: k_start, k_end, p
        logical :: trans
        real(${kind}$) :: tolerance
        real(${kind}$) :: beta
        ${type}$, allocatable :: res(:)
        integer, allocatable :: perm(:)
        integer :: k, i, kdim, kpm, kp, kpp

        ! Deals with optional non-unity blksize and allocations.
        p = optval(blksize, 1) ; allocate(res(p)) ; res = zero_r${kind}$
        allocate(perm(size(H, 2))) ; perm = 0 ; info = 0

        ! Check dimensions.
        kdim = (size(X) - p) / p

        ! Deal with the other optional args.
        k_start = optval(kstart, 1) ; k_end = optval(kend, kdim)
        tolerance = optval (tol, atol_${kind}$)
        trans     = optval(transpose, .false.)

        ! Arnoldi factorization.
        blk_arnoldi: do k = k_start, k_end
            ! Counters
            kpm = (k - 1) * p ; kp = kpm + p ; kpp = kp + p

            ! Matrix-vector product.
            if (trans) then
                do i = 1, p
                    call A%rmatvec(X(kpm+i), X(kp+i))
                enddo
            else
                do i = 1, p
                    call A%matvec(X(kpm+i), X(kp+i))
                enddo
            endif

            ! Update Hessenberg matrix via batch double Gram-Schmidt step.
            call double_gram_schmidt_step(X(kp+1:kpp), X(:kp), info, if_chk_orthonormal=.false., beta=H(:kp, kpm+1:kp))
            call check_info(info, 'double_gram_schmidt_step', module=this_module, procedure='arnoldi_${type[0]}$${kind}$')

            ! Orthogonalize current blk vectors.
            call qr(X(kp+1:kpp), H(kp+1:kpp, kpm+1:kp), info)
            call check_info(info, 'qr', module=this_module, procedure='arnoldi_${type[0]}$${kind}$')

            ! Extract residual norm (smallest diagonal element of H matrix).
            res = zero_r${kind}$
            do i = 1, p
                res(i) = H(kp+i, kpm+i)
            enddo
            beta = minval(abs(res))

            ! Exit Arnoldi loop if needed.
            if (beta < tolerance) then
                ! Dimension of the computed invariant subspace.
                info = kp
                ! Exit the Arnoldi iteration.
                exit blk_arnoldi
            endif

        enddo blk_arnoldi

        return
    end subroutine arnoldi_${type[0]}$${kind}$

    #:endfor


    !---------------------------------------------
    !-----     LANCZOS BIDIAGONALIZATION     -----
    !---------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine lanczos_bidiagonalization_${type[0]}$${kind}$(A, U, V, B, info, kstart, kend, tol)
        class(abstract_linop_${type[0]}$${kind}$), intent(in) :: A
        !! Linear operator to be factorized.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: U(:)
        !! Orthonormal basis for the column span of \(\mathbf{A}\). On entry, `U(1)` needs to be set to
        !! the starting Krylov vector.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: V(:)
        !! Orthonormal basis for the row span of \(\mathbf{A}\).
        ${type}$, intent(inout) :: B(:, :)
        !! Bidiagonal matrix.
        integer, intent(out) :: info
        !! Information flag.
        integer, optional, intent(in) :: kstart
        !! Starting index for the Lanczos factorization (default 1).
        integer, optional, intent(in) :: kend
        !! Final index for the Lanczos factorization (default 1).
        real(${kind}$), optional, intent(in) :: tol
        !! Tolerance to determine whether invariant subspaces have been computed or not.

        ! Internal variables.
        integer :: k_start, k_end
        real(${kind}$) :: tolerance
        ${type}$ :: alpha, beta
        integer :: k, kdim

        info = 0

        ! Krylov subspace dimension.
        kdim = size(U) - 1

        ! Deals with the optional args.
        k_start = optval(kstart, 1)
        k_end   = optval(kend, kdim)
        tolerance = optval(tol, atol_${kind}$)

        ! Lanczos bidiagonalization.
        lanczos : do k = k_start, k_end
            ! Transpose matrix-vector product.
            call A%rmatvec(U(k), V(k))

            ! Full re-orthogonalization of the right Krylov subspace.
            if (k > 1) then
                call double_gram_schmidt_step(V(k), V(:k-1), info, if_chk_orthonormal=.false.)
                call check_info(info, 'double_gram_schmidt_step', module=this_module, &
                                    & procedure='lanczos_bidiagonalization_${type[0]}$${kind}$, right basis')
            end if

            ! Normalization step.
            alpha = V(k)%norm() ; B(k, k) = alpha
            if (abs(alpha) > tolerance) then
                call V(k)%scal(one_${type[0]}$${kind}$/alpha)
            else
                info = k
                exit lanczos
            endif

            ! Matrix-vector product.
            call A%matvec(V(k), U(k+1))

            ! Full re-orthogonalization of the left Krylov subspace.
            call double_gram_schmidt_step(U(k+1), U(:k), info, if_chk_orthonormal=.false.)
            call check_info(info, 'double_gram_schmidt_step', module=this_module, &
                                    & procedure='lanczos_bidiagonalization_${type[0]}$${kind}$, left basis')

            ! Normalization step
            beta = U(k+1)%norm() ; B(k+1, k) = beta
            if (abs(beta) > tolerance) then
                call U(k+1)%scal(one_${type[0]}$${kind}$ / beta)
            else
                info = k
                exit lanczos
            endif

        enddo lanczos

        return
    end subroutine lanczos_bidiagonalization_${type[0]}$${kind}$

    #:endfor


    !----------------------------------------------
    !-----     LANCZOS TRIDIAGONALIZATION     -----
    !----------------------------------------------
    
    #:for kind, type in RC_KINDS_TYPES
    subroutine lanczos_tridiagonalization_${type[0]}$${kind}$(A, X, T, info, kstart, kend, tol)
        #:if type[0] == "r"
        class(abstract_sym_linop_${type[0]}$${kind}$), intent(in) :: A
        #:else
        class(abstract_hermitian_linop_${type[0]}$${kind}$), intent(in) :: A
        #:endif
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        ${type}$, intent(inout) :: T(:, :)
        integer, intent(out) :: info
        integer, optional, intent(in) :: kstart
        integer, optional, intent(in) :: kend
        real(${kind}$), optional, intent(in) :: tol

        ! Internal variables.
        integer :: k_start, k_end
        real(${kind}$) :: tolerance
        real(${kind}$) :: beta
        integer :: k, kdim

        ! Deal with optional args.
        kdim = size(X) - 1
        k_start = optval(kstart, 1)
        k_end = optval(kend, kdim)
        tolerance = optval(tol, atol_${kind}$)
        info = 0

        ! Lanczos tridiagonalization.
        lanczos: do k = k_start, k_end
            ! Matrix-vector product.
            call A%matvec(X(k), X(k+1))
            ! Update tridiagonal matrix.
            call update_tridiag_matrix_${type[0]}$${kind}$(T, X, k)
            beta = X(k+1)%norm() ; T(k+1, k) = beta

            ! Exit Lanczos loop if needed.
            if (beta < tolerance) then
                ! Dimension of the computed invariant subspace.
                info = k
                ! Exit the Lanczos iteration.
                exit lanczos
            else
                ! Normalize the new Krylov vector.
                call X(k+1)%scal(one_${type[0]}$${kind}$ / beta)
            endif
        enddo lanczos

        return
    end subroutine lanczos_tridiagonalization_${type[0]}$${kind}$

    subroutine update_tridiag_matrix_${type[0]}$${kind}$(T, X, k)
        integer, intent(in) :: k
        ${type}$, intent(inout) :: T(:, :)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)

        ! Internal variables.
        integer :: i, info

        info = 0

        ! Orthogonalize residual w.r.t. previously computed Krylov vectors to obtain coefficients in tridiag. matrix
        do i = max(1, k-1), k
            T(i, k) = X(i)%dot(X(k+1)) ; call X(k+1)%axpby(one_${type[0]}$${kind}$, X(i), -T(i, k))
        enddo

        ! Full re-orthogonalization against existing basis
        call double_gram_schmidt_step(X(k+1), X(:k), info, if_chk_orthonormal=.false.)
        call check_info(info, 'orthogonalize_against_basis_p1', module=this_module, procedure='update_tridiag_matrix_${type[0]}$${kind}$')

        return
    end subroutine update_tridiag_matrix_${type[0]}$${kind}$

    #:endfor

    !----------------------------------------
    !-----     KRYLOV-SCHUR RESTART     -----
    !----------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine krylov_schur_${type[0]}$${kind}$(n, X, H, select_eigs)
        integer, intent(out) :: n
        !! Number eigenvalues that have been moved to the upper
        !! left block of the Schur factorization of `H`.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        !! Krylov basis.
        ${type}$, intent(inout) :: H(:, :)
        !! Upper Hessenberg matrix.
        procedure(eigvals_select_${kind}$) :: select_eigs
        !! Procedure to select the eigenvalues to move in the upper left-block.

        !--------------------------------------
        !-----     Internal variables     -----
        !--------------------------------------

        integer :: i, kdim
        
        ! Schur-related.
        ${type}$ :: Z(size(H, 2), size(H, 2))
        complex(${kind}$) :: eigvals(size(H, 2))
        logical :: selected(size(H, 2))
       
        ! Krylov subspace dimension.
        kdim = size(X)-1

        ! Allocate and initializes variables.
        eigvals = zero_${type[0]}$${kind}$ ; Z = zero_${type[0]}$${kind}$
        selected = .false.

        ! Schur decomposition of the Hessenberg matrix.
        call schur(H(:size(H, 2), :), Z, eigvals)

        ! Eigenvalue selection of the upper left block.
        selected = select_eigs(eigvals) ; n = count(selected)

        ! Re-order the Schur decomposition and Schur basis.
        call ordschur(H(:kdim, :), Z, selected)

        ! Update the Hessenberg matrix and Krylov basis.
        block
        ${type}$ :: b(size(H, 2))
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Xwrk(:)
        
        ! Update the Krylov basis.
        call linear_combination(Xwrk, X(:size(H, 2)), Z(:, :n))
        call copy_basis(X(:n), Xwrk(:n))
        call X(n+1)%axpby(zero_${type[0]}$${kind}$, X(kdim+1), one_${type[0]}$${kind}$)
        call zero_basis(X(n+2:))

        ! Update the Hessenberg matrix.
        b = matmul(H(kdim+1, :), Z)
        H(n+1, :) = b
        H(n+2:, :) = zero_${type[0]}$${kind}$
        H(:, n+1:) = zero_${type[0]}$${kind}$
        end block

        return
    end subroutine krylov_schur_${type[0]}$${kind}$

    #:endfor

end module lightkrylov_BaseKrylov
