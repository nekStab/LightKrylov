#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module lightkrylov_BaseKrylov
    use iso_fortran_env
    use stdlib_optval, only: optval
    use stdlib_linalg, only: eye
    use LightKrylov_constants
    use LightKrylov_Logger
    use LightKrylov_Utils
    use LightKrylov_AbstractVectors
    use LightKrylov_AbstractLinops
    implicit none
    private
    
    character*128, parameter :: this_module = 'LightKrylov_BaseKrylov'

    public :: qr
    public :: apply_permutation_matrix
    public :: apply_inverse_permutation_matrix
    public :: arnoldi
    public :: initialize_krylov_subspace
    public :: orthogonalize_against_basis
    public :: lanczos_bidiagonalization
    public :: lanczos_tridiagonalization
    public :: krylov_schur
    public :: double_gram_schmidt_step
    #:for kind in REAL_KINDS
    public :: eigvals_select_${kind}$
    #:endfor

    interface qr
        #:for kind, type in RC_KINDS_TYPES
        module procedure qr_no_pivoting_${type[0]}$${kind}$
        module procedure qr_with_pivoting_${type[0]}$${kind}$
        #:endfor
    end interface

    interface swap_columns
        #: for kind, type in RC_KINDS_TYPES
        module procedure swap_columns_${type[0]}$${kind}$
        #:endfor
    end interface

    interface apply_permutation_matrix
        #: for kind, type in RC_KINDS_TYPES
        module procedure apply_permutation_matrix_${type[0]}$${kind}$
        module procedure apply_permutation_matrix_array_${type[0]}$${kind}$
        #:endfor
    end interface

    interface apply_inverse_permutation_matrix
        #:for kind, type in RC_KINDS_TYPES
        module procedure apply_inverse_permutation_matrix_${type[0]}$${kind}$
        module procedure apply_inverse_permutation_matrix_array_${type[0]}$${kind}$
        #:endfor
    end interface

    interface arnoldi
        #:for kind, type in RC_KINDS_TYPES
        module procedure arnoldi_${type[0]}$${kind}$
        #:endfor
    end interface

    interface initialize_krylov_subspace
        #:for kind, type in RC_KINDS_TYPES
        module procedure initialize_krylov_subspace_${type[0]}$${kind}$
        #:endfor
    end interface

    interface orthogonalize_against_basis
        #:for kind, type in RC_KINDS_TYPES
        module procedure orthogonalize_vector_against_basis_${type[0]}$${kind}$
        module procedure orthogonalize_basis_against_basis_${type[0]}$${kind}$
        #:endfor
    end interface

    interface double_gram_schmidt_step
        #:for kind, type in RC_KINDS_TYPES
        module procedure DGS_vector_against_basis_${type[0]}$${kind}$
        module procedure DGS_basis_against_basis_${type[0]}$${kind}$
        #:endfor
    end interface

    interface lanczos_tridiagonalization
        #:for kind, type in RC_KINDS_TYPES
        module procedure lanczos_tridiagonalization_${type[0]}$${kind}$
        #:endfor
    end interface

    interface lanczos_bidiagonalization
        #:for kind, type in RC_KINDS_TYPES
        module procedure lanczos_bidiagonalization_${type[0]}$${kind}$
        #:endfor
    end interface

    interface krylov_schur
        #:for kind, type in RC_KINDS_TYPES
        module procedure krylov_schur_${type[0]}$${kind}$
        #:endfor
    end interface

    !----------------------------------------------------------
    !-----     ABSTRACT EIGENVALUE SELECTOR INTERFACE     -----
    !----------------------------------------------------------

    abstract interface
        #:for kind in REAL_KINDS
        function eigvals_select_${kind}$(lambda) result(selected)
            import ${kind}$
            complex(${kind}$), intent(in) :: lambda(:)
            logical                       :: selected(size(lambda))
        end function eigvals_select_${kind}$
        #:endfor
    end interface

contains

    !-------------------------------------
    !-----     UTILITY FUNCTIONS     -----
    !-------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine initialize_krylov_subspace_${type[0]}$${kind}$(X, X0)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        class(abstract_vector_${type[0]}$${kind}$), optional, intent(in) :: X0(:)

        ! Internal variables.
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Q(:)
        ${type}$, allocatable :: R(:, :)
        integer, allocatable :: perm(:)
        integer :: i, p, info

        ! Zero-out X.
        call zero_basis(X)

        ! Deals with optional args.
        if(present(X0)) then
            p = size(X0)

            ! Initialize.
            allocate(Q(1:p), source=X0(1:p))
            do i = 1, p
                call Q(i)%zero()
                call Q(i)%add(X0(i))
            enddo
    
            ! Orthonormalize.
            allocate(R(p, p)) ; R = zero_r${kind}$
            allocate(perm(p)) ; perm = 0
            call qr(Q, R, perm, info)
            do i = 1, p
                call X(i)%add(Q(i))
            enddo
        endif

        return
    end subroutine initialize_krylov_subspace_${type[0]}$${kind}$

    subroutine orthogonalize_vector_against_basis_${type[0]}$${kind}$(y, X, info, if_chk_orthonormal, beta)
      !! Orthonormalizes the `abstract_vector` `y` against a basis `X` of `abstract_vector`.
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: y
      !! Input `abstract_vector` to orthogonalize
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: X(:)
      !! Input `abstract_vector` basis to orthogonalize against
      integer, intent(out) :: info
      !! Information flag.
      logical,                          optional, intent(in)    :: if_chk_orthonormal
      logical                                                   :: chk_X_orthonormality
      !! Check that input Krylov vectors `X` form an orthonormal basis (expensive!)
      ${type}$,                         optional, intent(out)   :: beta(:)
      !! Projection coefficients if requested

      ! internals
      ${type}$ :: proj_coefficients(size(X))

      info = 0

      ! optional input argument
      chk_X_orthonormality = optval(if_chk_orthonormal, .true.) ! default to true!

      ! check for zero vector
      if (y%norm() < atol_${kind}$) info = 1

      if (chk_X_orthonormality) then
         block 
            ${type}$, dimension(size(X), size(X)) :: G
            call innerprod(G, X, X)
            if (abs(G(size(X),size(X))) < rtol_${kind}$) then
               ! The last vector in X is zero, it does not impact orthogonalisation
               info = -2
            else if (norm2(abs(G - eye(size(X)))) > rtol_${kind}$) then
               ! The basis is not orthonormal. Cannot orthonormalize.
               info = -1
               return
            end if
         end block
      end if

      ! orthogonalize
      call innerprod(proj_coefficients, X, y)
      block
         class(abstract_vector_${type[0]}$${kind}$), allocatable :: proj
         call linear_combination(proj, X, proj_coefficients)
         call y%sub(proj)
      end block

      if (present(beta)) then
         ! check size
         call assert_shape(beta, shape(proj_coefficients), 'orthogonalize_vector_against_basis_${type[0]}$${kind}$', 'beta')
         beta = proj_coefficients
      end if
      
      return
    end subroutine orthogonalize_vector_against_basis_${type[0]}$${kind}$

    subroutine orthogonalize_basis_against_basis_${type[0]}$${kind}$(Y, X, info, if_chk_orthonormal, beta)
      !! Orthonormalizes the `abstract_vector` basis `Y` against a basis `X` of `abstract_vector`.
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Y(:)
      !! Input `abstract_vector` basis to orthogonalize
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: X(:)
      !! Input `abstract_vector` basis to orthogonalize against
      integer, intent(out) :: info
      !! Information flag.
      logical,                          optional, intent(in)    :: if_chk_orthonormal
      logical                                                   :: chk_X_orthonormality
      !! Check that input Krylov vectors `X` form an orthonormal basis (expensive!)
      ${type}$,                         optional, intent(out)   :: beta(:,:)
      !! Projection coefficients if requested

      ! internals
      ${type}$ :: proj_coefficients(size(X), size(Y))
      integer :: i

      info = 0

      ! optional input argument
      chk_X_orthonormality = optval(if_chk_orthonormal, .true.) ! default to true!

      ! check for zero vector
      do i = 1, size(Y)
         if ( Y(i)%norm() < atol_${kind}$) info = i
      end do

      if (chk_X_orthonormality) then
         block 
            ${type}$, dimension(size(X), size(X)) :: G
            call innerprod(G, X, X)
            if (abs(G(size(X),size(X))) < rtol_${kind}$) then
               ! The last vector in X is zero, it does not impact orthogonalisation
               info = -2
            else if (norm2(abs(G - eye(size(X)))) > rtol_${kind}$) then
               ! The basis is not orthonormal. Cannot orthonormalize.
               info = -1
               return
            end if
         end block
      end if
      
      ! orthogonalize
      call innerprod(proj_coefficients, X, Y)
      block
         class(abstract_vector_${type[0]}$${kind}$), allocatable :: proj(:)
         call linear_combination(proj, X, proj_coefficients)
         call axpby_basis(Y, one_${type[0]}$${kind}$, proj, -one_${type[0]}$${kind}$)
      end block

      if (present(beta)) then
         ! check size
         call assert_shape(beta, shape(proj_coefficients), 'orthogonalize_basis_against_basis_${type[0]}$${kind}$', 'beta')
         beta = proj_coefficients
      end if
      
      return
    end subroutine orthogonalize_basis_against_basis_${type[0]}$${kind}$

    subroutine DGS_vector_against_basis_${type[0]}$${kind}$(y, X, info, if_chk_orthonormal, beta)
      !! Computes one step of the double Gram-Schmidt orthogonalization process of the
      !! `abstract_vector` `y` against the `abstract_vector` basis `X`
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: y
      !! Input `abstract_vector` to orthogonalize
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: X(:)
      !! Input `abstract_vector` basis to orthogonalize against
      integer, intent(out) :: info
      !! Information flag.
      logical,                          optional, intent(in)    :: if_chk_orthonormal
      logical                                                   :: chk_X_orthonormality
      !! Check that input Krylov vectors `X` form an orthonormal basis (expensive!)
      ${type}$,                         optional, intent(out)   :: beta(:)
      !! Projection coefficients if requested

      ! internals
      ${type}$, dimension(size(X)) :: proj_coefficients, wrk

      ! optional input argument
      chk_X_orthonormality = optval(if_chk_orthonormal, .true.) ! default to true!

      info = 0

      proj_coefficients = zero_${type[0]}$${kind}$; wrk = zero_${type[0]}$${kind}$

      ! Orthogonalize vector y w.r.t. to Krylov basis X in two passes of GS.
      ! first pass
      call orthogonalize_against_basis(y, X, info, if_chk_orthonormal=.false., beta=proj_coefficients)
      call check_info(info, 'orthogonalize_against_basis', module=this_module, procedure='DGS_vector_against_basis, first pass')
      ! second pass
      call orthogonalize_against_basis(y, X, info, if_chk_orthonormal=.false., beta=wrk)
      call check_info(info, 'orthogonalize_against_basis', module=this_module, procedure='DGS_vector_against_basis_${type[0]}$${kind}$, second pass')
      ! combine passes
      proj_coefficients = proj_coefficients + wrk

      if (present(beta)) then
         ! check size
         call assert_shape(beta, shape(proj_coefficients), 'DGS_vector_against_basis_${type[0]}$${kind}$', 'beta')
         beta = proj_coefficients
      end if

    end subroutine DGS_vector_against_basis_${type[0]}$${kind}$

    subroutine DGS_basis_against_basis_${type[0]}$${kind}$(y, X, info, if_chk_orthonormal, beta)
      !! Computes one step of the double Gram-Schmidt orthogonalization process of the
      !! `abstract_vector` `y` against the `abstract_vector` basis `X`
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Y(:)
      !! Input `abstract_vector` basis to orthogonalize
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: X(:)
      !! Input `abstract_vector` basis to orthogonalize against
      integer, intent(out) :: info
      !! Information flag.
      logical,                          optional, intent(in)    :: if_chk_orthonormal
      logical                                                   :: chk_X_orthonormality
      !! Check that input Krylov vectors `X` form an orthonormal basis (expensive!)
      ${type}$,                         optional, intent(out)   :: beta(:,:)
      !! Projection coefficients if requested

      ! internals
      ${type}$, dimension(size(X),size(Y)) :: proj_coefficients, wrk

      ! optional input argument
      chk_X_orthonormality = optval(if_chk_orthonormal, .true.) ! default to true!

      info = 0

      proj_coefficients = zero_${type[0]}$${kind}$; wrk = zero_${type[0]}$${kind}$

      ! Orthogonalize Krylov basis Y w.r.t. to Krylov basis X in two passes of GS.
      ! first pass
      call orthogonalize_against_basis(Y, X, info, if_chk_orthonormal=.false., beta=proj_coefficients)
      call check_info(info, 'orthogonalize_against_basis', module=this_module, procedure='DGS_basis_against_basis_${type[0]}$${kind}$, first pass')
      ! second pass
      call orthogonalize_against_basis(Y, X, info, if_chk_orthonormal=.false., beta=wrk)
      call check_info(info, 'orthogonalize_against_basis', module=this_module, procedure='DGS_basis_against_basis_${type[0]}$${kind}$, second pass')
      ! combine passes
      proj_coefficients = proj_coefficients + wrk

      if (present(beta)) then
         ! check size
         call assert_shape(beta, shape(proj_coefficients), 'DGS_basis_against_basis_${type[0]}$${kind}$', 'beta')
         beta = proj_coefficients
      end if

    end subroutine DGS_basis_against_basis_${type[0]}$${kind}$  

    #:endfor

    !------------------------------------
    !-----     QR FACTORIZATION     -----
    !------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine qr_no_pivoting_${type[0]}$${kind}$(Q, R, info, verbosity, tol)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Array of `abstract_vector` to be orthogonalized.
        ${type}$, intent(out) :: R(:, :)
        !! Upper triangular matrix \(\mathbf{R}\) resulting from the QR factorization.
        integer, intent(out) :: info
        !! Information flag.
        logical, optional, intent(in) :: verbosity
        !! Verbosity control.
        logical                       :: verbose
        real(${kind}$), optional, intent(in) :: tol
        !! Tolerance to determine colinearity.
        real(${kind}$)                       :: tolerance

        ! Internal variables.
        ${type}$ :: alpha
        ${type}$ :: beta(size(Q))
        integer :: idx, j, kdim, iwrk

        ! Deals with the optional args.
        verbose   = optval(verbosity, .false.)
        tolerance = optval(tol, rtol_${kind}$)

        info = 0 ; R = zero_r${kind}$ ; beta = zero_r${kind}$
        do j = 1, size(Q)
            if (j > 1) then
                ! Double Gram-Schmidt orthogonalization
                call double_gram_schmidt_step(Q(j), Q(1:j-1), info, if_chk_orthonormal=.false., beta=R(1:j-1,j))
                call check_info(info, 'double_gram_schmidt_step', module=this_module, procedure='qr_no_pivoting_${type[0]}$${kind}$')
            end if

            ! Normalize column.
            alpha = Q(j)%norm()

            ! Check for breakdown.
            if (abs(alpha) < tolerance) then
                if(verbose) then
                    write(output_unit, *) "INFO: Colinear columns detected."
                    write(output_unit, *) "      (j, alpha) = (", j, ", ", alpha, ")"
                endif
                info = j
                R(j, j) = zero_r${kind}$ ; call Q(j)%zero()
            else
                call Q(j)%scal(one_r${kind}$ / alpha) ; R(j, j) = alpha
            endif
        enddo

        return
    end subroutine qr_no_pivoting_${type[0]}$${kind}$

    subroutine qr_with_pivoting_${type[0]}$${kind}$(Q, R, perm, info, verbosity, tol)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Array of `abstract_vector` to be orthogonalized.
        ${type}$, intent(out) :: R(:, :)
        !! Upper triangular matrix resulting from the QR factorization.
        integer, intent(out) :: perm(size(Q))
        !! Permutation matrix.
        integer, intent(out) :: info
        !! Information flag.
        logical, optional, intent(in) :: verbosity
        !! Verbosity control.
        logical :: verbose
        real(${kind}$), optional, intent(in) :: tol

        !! Tolerance to detect colinearity.
        real(${kind}$) :: tolerance

        ! Internal variables
        ${type}$ :: beta
        integer :: idx, i, j, kdim, iwrk
        integer :: idxv(1)
        ${type}$  :: Rii(size(Q))

        info = 0 ; kdim = size(Q)
        R = zero_r${kind}$ ; Rii = zero_r${kind}$
        
        ! Deals with the optional arguments.
        verbose = optval(verbosity, .false.)
        tolerance = optval(tol, rtol_${kind}$)

        ! Initialize diagonal entries.
        do i = 1, kdim
            perm(i) = i
            Rii(i) = Q(i)%dot(Q(i))
        enddo

        qr_step: do j = 1, kdim
            idxv = maxloc(abs(Rii)) ; idx = idxv(1)
            if (abs(Rii(idx)) < tolerance) then
                do i = j, kdim
                    call Q(i)%rand(.false.)
                    call orthogonalize_against_basis(Q(i), Q(1:i-1), info, if_chk_orthonormal=.false.)
                    call check_info(info, 'orthogonalize_against_basis', module=this_module, procedure='qr_with_pivoting_${type[0]}$${kind}$')
                    beta = Q(i)%norm(); call Q(i)%scal(one_${type[0]}$${kind}$ / beta)
                enddo
                info = j
                exit qr_step
            endif

            call swap_columns(Q, R, Rii, perm, j, idx)

            ! Normalize column.
            beta = Q(j)%norm()

            if (abs(beta) < tolerance) then
               info = j
                R(j, j) = zero_r${kind}$ ; call Q(j)%zero()
            else
                R(j, j) = beta ; call Q(j)%scal(one_r${kind}$ / beta)
            endif

            ! Orthonormalize all columns against new vector (MGS)
            do i = j+1, kdim
                beta = Q(j)%dot(Q(i))
                call Q(i)%axpby(one_${type[0]}$${kind}$, Q(j), -beta)
                R(j, i) = beta
            enddo

            ! Update Rii
            Rii(j) = zero_r${kind}$
            do i = j+1, kdim
                Rii(i) = Rii(i) - R(j, i)**2
            enddo

        enddo qr_step

        return
    end subroutine qr_with_pivoting_${type[0]}$${kind}$

    subroutine swap_columns_${type[0]}$${kind}$(Q, R, Rii, perm, i, j)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Vector basis whose i-th and j-th columns need swapping.
        ${type}$, intent(inout) :: R(:, :)
        !! Upper triangular matrix resulting from QR.
        ${type}$, intent(inout) :: Rii(:)
        !! Diagonal entries of R.
        integer, intent(inout) :: perm(:)
        !! Column ordering.
        integer, intent(in) :: i, j
        !! Index of the columns to be swapped.

        ! Internal variables.
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Qwrk
        ${type}$, allocatable :: Rwrk(:)
        integer :: iwrk, m, n

        ! Sanity checks.
        m = size(Q) ; n = min(i, j) - 1

        ! Allocations.
        allocate(Qwrk, source=Q(1)) ; call Qwrk%zero()
        allocate(Rwrk(1:max(1, n))) ; Rwrk = zero_r${kind}$

        ! Swap columns.
        call Qwrk%axpby(zero_${type[0]}$${kind}$, Q(j), one_${type[0]}$${kind}$)
        call Q(j)%axpby(zero_${type[0]}$${kind}$, Q(i), one_${type[0]}$${kind}$)
        call Q(i)%axpby(zero_${type[0]}$${kind}$, Qwrk, one_${type[0]}$${kind}$)
        
        Rwrk(1) = Rii(j); Rii(j) = Rii(i); Rii(i) = Rwrk(1)
        iwrk = perm(j); perm(j) = perm(i) ; perm(i) = iwrk

        if (n > 0) then
            Rwrk = R(1:n, j) ; R(1:n, j) = R(1:n, i) ; R(1:n, i) = Rwrk
        endif

        return
    end subroutine swap_columns_${type[0]}$${kind}$

    subroutine apply_permutation_matrix_${type[0]}$${kind}$(Q, perm)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Basis vectors to be permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Qwrk(:)

        allocate(Qwrk, source=Q)
        do i = 1, size(perm)
            call Q(i)%axpby(zero_${type[0]}$${kind}$, Qwrk(perm(i)), one_${type[0]}$${kind}$)
        enddo

        return
    end subroutine apply_permutation_matrix_${type[0]}$${kind}$

    subroutine apply_inverse_permutation_matrix_${type[0]}$${kind}$(Q, perm)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Basis vectors to be (un-) permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        integer :: inv_perm(size(perm))
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Qwrk(:)

        allocate(Qwrk, source=Q) ; inv_perm = 0

        ! Inverse permutation vector.
        do i = 1, size(perm)
            inv_perm(perm(i)) = i
        enddo

        ! Undo permutation.
        do i = 1, size(perm)
            call Q(i)%axpby(zero_${type[0]}$${kind}$, Qwrk(inv_perm(i)), one_${type[0]}$${kind}$)
        enddo

        return
    end subroutine apply_inverse_permutation_matrix_${type[0]}$${kind}$

    subroutine apply_permutation_matrix_array_${type[0]}$${kind}$(Q, perm)
        ${type}$, intent(inout) :: Q(:, :)
        !! Basis vectors to be permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        ${type}$, allocatable :: Qwrk(:, :)

        Qwrk = Q
        do i = 1, size(perm)
            Q(:, i) = Qwrk(:, perm(i))
        enddo

        return
    end subroutine apply_permutation_matrix_array_${type[0]}$${kind}$

    subroutine apply_inverse_permutation_matrix_array_${type[0]}$${kind}$(Q, perm)
        ${type}$, intent(inout) :: Q(:, :)
        !! Basis vectors to be (un-) permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        integer :: inv_perm(size(perm))
        ${type}$, allocatable :: Qwrk(:, :)

        Qwrk = Q ; inv_perm = 0

        ! Inverse permutation vector.
        do i = 1, size(perm)
            inv_perm(perm(i)) = i
        enddo

        ! Undo permutation.
        do i = 1, size(perm)
            Q(:, i) = Qwrk(:, inv_perm(i))
        enddo

        return
    end subroutine apply_inverse_permutation_matrix_array_${type[0]}$${kind}$


    #:endfor

    !-----------------------------------------
    !-----     ARNOLDI FACTORIZATION     -----
    !-----------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine arnoldi_${type[0]}$${kind}$(A, X, H, info, kstart, kend, verbosity, tol, transpose, blksize)
        class(abstract_linop_${type[0]}$${kind}$), intent(in) :: A
        !! Linear operator to be factorized.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        !! Orthogonal basis for the generated Krylov subspace.
        ${type}$, intent(inout) :: H(:, :)
        !! Upper Hessenberg matrix.
        integer, intent(out) :: info
        !! Information flag.
        integer, optional, intent(in) :: kstart
        !! Starting index for the Arnoldi factorization (default 1).
        integer, optional, intent(in) :: kend
        !! Final index for the Arnoldi factorization (default `size(X)-1`)
        logical, optional, intent(in) :: verbosity
        !! Verbosity control.
        logical, optional, intent(in) :: transpose
        !! Whether \( \mathbf{A} \) is being transposed or not (default `.false.`)
        real(${kind}$), optional, intent(in) :: tol
        !! Tolerance to determine whether an invariant subspace has been computed or not.
        integer, optional, intent(in) :: blksize
        !! Block size for block Arnoldi (default 1).

        ! Internal variables.
        integer :: k_start, k_end, p
        logical :: verbose, trans
        real(${kind}$) :: tolerance
        real(${kind}$) :: beta
        ${type}$, allocatable :: res(:)
        integer, allocatable :: perm(:)
        integer :: k, i, kdim, kpm, kp, kpp

        ! Deals with optional non-unity blksize and allocations.
        p = optval(blksize, 1) ; allocate(res(1:p)) ; res = zero_r${kind}$
        allocate(perm(1:size(H, 2))) ; perm = 0 ; info = 0

        ! Check dimensions.
        kdim = (size(X) - p) / p

        ! Deal with the other optional args.
        k_start = optval(kstart, 1) ; k_end = optval(kend, kdim)
        verbose   = optval(verbosity, .false.)
        tolerance = optval (tol, atol_${kind}$)
        trans     = optval(transpose, .false.)

        ! Arnoldi factorization.
        blk_arnoldi: do k = k_start, k_end
            ! Counters
            kpm = (k - 1) * p ; kp = kpm + p ; kpp = kp + p

            ! Matrix-vector product.
            if (trans) then
                do i = 1, p
                    call A%rmatvec(X(kpm+i), X(kp+i))
                enddo
            else
                do i = 1, p
                    call A%matvec(X(kpm+i), X(kp+i))
                enddo
            endif

            ! Update Hessenberg matrix via batch double Gram-Schmidt step.
            call double_gram_schmidt_step(X(kp+1:kpp), X(1:kp), info, if_chk_orthonormal=.false., beta=H(1:kp, kpm+1:kp))
            call check_info(info, 'double_gram_schmidt_step', module=this_module, procedure='arnoldi_${type[0]}$${kind}$')

            ! Orthogonalize current blk vectors.
            call qr(X(kp+1:kpp), H(kp+1:kpp, kpm+1:kp), info)
            call check_info(info, 'qr', module=this_module, procedure='arnoldi_${type[0]}$${kind}$')

            ! Extract residual norm (smallest diagonal element of H matrix).
            res = zero_r${kind}$
            do i = 1, p
                res(i) = H(kp+i, kpm+i)
            enddo
            beta = minval(abs(res))

            ! Exit Arnoldi loop if needed.
            if (beta < tolerance) then
                ! Dimension of the computed invariant subspace.
                info = kp
                ! Exit the Arnoldi iteration.
                exit blk_arnoldi
            endif

        enddo blk_arnoldi

        return
    end subroutine arnoldi_${type[0]}$${kind}$

    #:endfor


    !---------------------------------------------
    !-----     LANCZOS BIDIAGONALIZATION     -----
    !---------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine lanczos_bidiagonalization_${type[0]}$${kind}$(A, U, V, B, info, kstart, kend, verbosity, tol)
        class(abstract_linop_${type[0]}$${kind}$), intent(in) :: A
        !! Linear operator to be factorized.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: U(:)
        !! Orthonormal basis for the column span of \(\mathbf{A}\). On entry, `U(1)` needs to be set to
        !! the starting Krylov vector.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: V(:)
        !! Orthonormal basis for the row span of \(\mathbf{A}\).
        ${type}$, intent(inout) :: B(:, :)
        !! Bidiagonal matrix.
        integer, intent(out) :: info
        !! Information flag.
        integer, optional, intent(in) :: kstart
        !! Starting index for the Lanczos factorization (default 1).
        integer, optional, intent(in) :: kend
        !! Final index for the Lanczos factorization (default 1).
        logical, optional, intent(in) :: verbosity
        !! Verbosity control (default `.false.`)
        real(${kind}$), optional, intent(in) :: tol
        !! Tolerance to determine whether invariant subspaces have been computed or not.

        ! Internal variables.
        integer :: k_start, k_end
        logical :: verbose
        real(${kind}$) :: tolerance
        ${type}$ :: alpha, beta
        integer :: i, j, k, kdim


        info = 0

        ! Krylov subspace dimension.
        kdim = size(U) - 1

        ! Deals with the optional args.
        k_start = optval(kstart, 1)
        k_end   = optval(kend, kdim)
        verbose = optval(verbosity, .false.)
        tolerance = optval(tol, atol_${kind}$)

        ! Lanczos bidiagonalization.
        lanczos : do k = k_start, k_end
            ! Transpose matrix-vector product.
            call A%rmatvec(U(k), V(k))

            ! Full re-orthogonalization of the right Krylov subspace.
            if (k > 1 ) then
            
                call double_gram_schmidt_step(V(k), V(:k-1), info, if_chk_orthonormal=.true.)
                call check_info(info, 'double_gram_schmidt_step', module=this_module, &
                                    & procedure='lanczos_bidiagonalization_${type[0]}$${kind}$, first pass')
            end if

            ! Normalization step.
            alpha = V(k)%norm() ; B(k, k) = alpha
            if (abs(alpha) > tolerance) then
                call V(k)%scal(one_${type[0]}$${kind}$/alpha)
            else
                info = k
                exit lanczos
            endif

            ! Matrix-vector product.
            call A%matvec(V(k), U(k+1))

            ! Full re-orthogonalization of the left Krylov subspace.
            call double_gram_schmidt_step(U(k+1), U(:k), info, if_chk_orthonormal=.true.)
            call check_info(info, 'orthogonalize_against_basis', module=this_module, &
                                    & procedure='lanczos_bidiagonalization_${type[0]}$${kind}$, second pass')

            ! Normalization step
            beta = U(k+1)%norm() ; B(k+1, k) = beta
            if (abs(beta) > tolerance) then
                call U(k+1)%scal(one_${type[0]}$${kind}$ / beta)
            else
                info = k
                exit lanczos
            endif

        enddo lanczos

        return
    end subroutine lanczos_bidiagonalization_${type[0]}$${kind}$

    #:endfor


    !----------------------------------------------
    !-----     LANCZOS TRIDIAGONALIZATION     -----
    !----------------------------------------------
    
    #:for kind, type in RC_KINDS_TYPES
    subroutine lanczos_tridiagonalization_${type[0]}$${kind}$(A, X, T, info, kstart, kend, verbosity, tol)
        #:if type[0] == "r"
        class(abstract_sym_linop_${type[0]}$${kind}$), intent(in) :: A
        #:else
        class(abstract_hermitian_linop_${type[0]}$${kind}$), intent(in) :: A
        #:endif
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        ${type}$, intent(inout) :: T(:, :)
        integer, intent(out) :: info
        integer, optional, intent(in) :: kstart
        integer, optional, intent(in) :: kend
        logical, optional, intent(in) :: verbosity
        real(${kind}$), optional, intent(in) :: tol

        ! Internal variables.
        integer :: k_start, k_end
        logical :: verbose
        real(${kind}$) :: tolerance
        real(${kind}$) :: beta
        integer :: i, j, k, kdim

        ! Deal with optional args.
        kdim = size(X) - 1
        k_start = optval(kstart, 1)
        k_end = optval(kend, kdim)
        verbose = optval(verbosity, .false.)
        tolerance = optval(tol, atol_${kind}$)

        ! Lanczos tridiagonalization.
        lanczos: do k = k_start, k_end
            ! Matrix-vector product.
            call A%matvec(X(k), X(k+1))
            ! Update tridiagonal matrix.
            call update_tridiag_matrix_${type[0]}$${kind}$(T, X, k)
            beta = X(k+1)%norm() ; T(k+1, k) = beta

            ! Exit Lanczos loop if needed.
            if (beta < tolerance) then
                ! Dimension of the computed invariant subspace.
                info = k
                ! Exit the Lanczos iteration.
                exit lanczos
            else
                ! Normalize the new Krylov vector.
                call X(k+1)%scal(one_${type[0]}$${kind}$ / beta)
            endif
        enddo lanczos

        return
    end subroutine lanczos_tridiagonalization_${type[0]}$${kind}$

    subroutine update_tridiag_matrix_${type[0]}$${kind}$(T, X, k)
        integer, intent(in) :: k
        ${type}$, intent(inout) :: T(:, :)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)

        ! Internal variables.
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: wrk
        integer :: i, info

        info = 0

        ! Orthogonalize residual w.r.t. previously computed Krylov vectors to obtain coefficients in tridiag. matrix
        do i = max(1, k-1), k
            T(i, k) = X(i)%dot(X(k+1)) ; call X(k+1)%axpby(one_${type[0]}$${kind}$, X(i), -T(i, k))
        enddo

        ! Full re-orthogonalization against existing basis
        call double_gram_schmidt_step(X(k+1), X(:k), info, if_chk_orthonormal=.true.)
        call check_info(info, 'orthogonalize_against_basis', module=this_module, procedure='update_tridiag_matrix_${type[0]}$${kind}$')

        return
    end subroutine update_tridiag_matrix_${type[0]}$${kind}$

    #:endfor

    !----------------------------------------
    !-----     KRYLOV-SCHUR RESTART     -----
    !----------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine krylov_schur_${type[0]}$${kind}$(n, X, H, select_eigs)
        integer, intent(out) :: n
        !! Number eigenvalues that have been moved to the upper
        !! left block of the Schur factorization of `H`.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        !! Krylov basis.
        ${type}$, intent(inout) :: H(:, :)
        !! Upper Hessenberg matrix.
        procedure(eigvals_select_${kind}$) :: select_eigs
        !! Procedure to select the eigenvalues to move in the upper left-block.

        !--------------------------------------
        !-----     Internal variables     -----
        !--------------------------------------

        integer :: i, j, kdim
        
        ! Schur-related.
        ${type}$ :: Z(size(H, 2), size(H, 2))
        complex(${kind}$) :: eigvals(size(H, 2))
        logical :: selected(size(H, 2))
       
        ! Krylov subspace dimension.
        kdim = size(X)-1

        ! Allocate and initializes variables.
        eigvals = zero_${type[0]}$${kind}$ ; Z = zero_${type[0]}$${kind}$
        selected = .false.

        ! Schur decomposition of the Hessenberg matrix.
        call schur(H(:size(H, 2), :), Z, eigvals)

        ! Eigenvalue selection of the upper left block.
        selected = select_eigs(eigvals) ; n = count(selected)

        ! Re-order the Schur decomposition and Schur basis.
        call ordschur(H(:kdim, :), Z, selected)

        ! Update the Hessenberg matrix and Krylov basis.
        block
        ${type}$ :: b(size(H, 2))
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Xwrk(:)
        
        ! Update the Krylov basis.
        call linear_combination(Xwrk, X(:size(H, 2)), Z(:, :n))

        do i = 1, n
            call X(i)%axpby(zero_${type[0]}$${kind}$, Xwrk(i), one_${type[0]}$${kind}$)
        enddo

        call X(n+1)%axpby(zero_${type[0]}$${kind}$, X(kdim+1), one_${type[0]}$${kind}$)

        call zero_basis(X(n+2:))

        ! Update the Hessenberg matrix.
        b = matmul(H(kdim+1, :), Z)
        H(n+1, :) = b
        H(n+2:, :) = zero_${type[0]}$${kind}$
        H(:, n+1:) = zero_${type[0]}$${kind}$
        end block

        return
    end subroutine krylov_schur_${type[0]}$${kind}$

    #:endfor

end module lightkrylov_BaseKrylov
