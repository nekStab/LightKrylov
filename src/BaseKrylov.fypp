#:include "../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
module lightkrylov_BaseKrylov
    use iso_fortran_env
    use stdlib_optval, only: optval
    use lightkrylov_constants
    use LightKrylov_Logger
    use lightkrylov_utils
    use lightkrylov_AbstractVectors
    use lightkrylov_AbstractLinops
    implicit none
    
    character*128, parameter, private :: this_module = 'LightKrylov_BaseKrylov'

    public :: qr
    public :: apply_permutation_matrix
    public :: apply_inverse_permutation_matrix
    public :: arnoldi
    public :: initialize_krylov_subspace
    public :: orthonormalize_against_basis
    public :: lanczos_bidiagonalization
    public :: lanczos_tridiagonalization
    public :: krylov_schur

    interface qr
        #:for kind, type in RC_KINDS_TYPES
        module procedure qr_no_pivoting_${type[0]}$${kind}$
        module procedure qr_with_pivoting_${type[0]}$${kind}$
        #:endfor
    end interface

    interface swap_columns
        #: for kind, type in RC_KINDS_TYPES
        module procedure swap_columns_${type[0]}$${kind}$
        #:endfor
    end interface

    interface apply_permutation_matrix
        #: for kind, type in RC_KINDS_TYPES
        module procedure apply_permutation_matrix_${type[0]}$${kind}$
        module procedure apply_permutation_matrix_array_${type[0]}$${kind}$
        #:endfor
    end interface

    interface apply_inverse_permutation_matrix
        #:for kind, type in RC_KINDS_TYPES
        module procedure apply_inverse_permutation_matrix_${type[0]}$${kind}$
        module procedure apply_inverse_permutation_matrix_array_${type[0]}$${kind}$
        #:endfor
    end interface

    interface arnoldi
        #:for kind, type in RC_KINDS_TYPES
        module procedure arnoldi_${type[0]}$${kind}$
        #:endfor
    end interface

    interface initialize_krylov_subspace
        #:for kind, type in RC_KINDS_TYPES
        module procedure initialize_krylov_subspace_${type[0]}$${kind}$
        #:endfor
    end interface

    interface orthonormalize_against_basis
        #:for kind, type in RC_KINDS_TYPES
        module procedure orthonormalize_vector_against_basis_${type[0]}$${kind}$
        module procedure orthonormalize_basis_against_basis_${type[0]}$${kind}$
        #:endfor
    end interface

    interface lanczos_tridiagonalization
        #:for kind, type in RC_KINDS_TYPES
        module procedure lanczos_tridiagonalization_${type[0]}$${kind}$
        #:endfor
    end interface

    interface lanczos_bidiagonalization
        #:for kind, type in RC_KINDS_TYPES
        module procedure lanczos_bidiagonalization_${type[0]}$${kind}$
        #:endfor
    end interface

    interface krylov_schur
        #:for kind, type in RC_KINDS_TYPES
        module procedure krylov_schur_${type[0]}$${kind}$
        #:endfor
    end interface

contains

    !-------------------------------------
    !-----     UTILITY FUNCTIONS     -----
    !-------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine initialize_krylov_subspace_${type[0]}$${kind}$(X, X0)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        class(abstract_vector_${type[0]}$${kind}$), optional, intent(in) :: X0(:)

        ! Internal variables.
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Q(:)
        ${type}$, allocatable :: R(:, :)
        integer, allocatable :: perm(:)
        integer :: i, p, info

        ! Zero-out X.
        do i = 1, size(X)
            call X(i)%zero()
        enddo

        ! Deals with optional args.
        if(present(X0)) then
            p = size(X0)

            ! Initialize.
            allocate(Q(1:p), source=X0(1:p))
            do i = 1, p
                call Q(i)%zero()
                call Q(i)%add(X0(i))
            enddo
    
            ! Orthonormalize.
            allocate(R(1:p, 1:p)) ; R = zero_r${kind}$
            allocate(perm(1:p)) ; perm = 0
            call qr(Q, R, perm, info)
            do i = 1, p
                call X(i)%add(Q(i))
            enddo
        endif

        return
    end subroutine initialize_krylov_subspace_${type[0]}$${kind}$

    subroutine orthonormalize_vector_against_basis_${type[0]}$${kind}$(y, X)
      !! Orthonormalizes the `abstract_vector` `y` against a basis `X` of `abstract_vector`.
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: y
      !! Input `abstract_vector` to orthonormalize
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: X(:)
      !! Input `abstract_vector` basis to orthonormalize against

      ! internals
      character*128 :: msg
      ${type}$ :: coef(size(X))
      real(${kind}$) :: alpha

      ! check for zero vector
      if (y%norm() < atol_${kind}$) then
         write(msg, *) 'The input vector is (numerically) zero.'
         call logger%log_error(trim(msg), module=this_module, procedure='orthonormalize_vector_against_basis_${type[0]}$${kind}$', &
                              & stat=-1, errmsg='Cannot orthonormalize.')
      end if

      ! orthogonalize
      call innerprod(coef, X, y)
      block
         class(abstract_vector_${type[0]}$${kind}$), allocatable :: proj
         call linear_combination(proj, X, coef)
         call y%sub(proj)
      end block

      ! normalize
      alpha = y%norm()
      call y%scal(one_${type[0]}$${kind}$ / alpha)

      if (alpha < atol_${kind}$) then ! error
         write(msg, *) 'The input vector is in the span of X.'
         call logger%log_error(trim(msg), module=this_module, procedure='orthonormalize_vector_against_basis_${type[0]}$${kind}$', & 
                              & stat=-2, errmsg='Cannot orthonormalize.')
      else if (alpha < rtol_${kind}$) then ! repeat to avoid floating point errors
         write(msg, *) 'The norm of the component of y in the orthogonal complement of X is ||y||_orth = ', &
                        & alpha, ': The input vector is nearly in the span of X. ', &
                        & 'Repeating orthonormalisation.'
         call logger%log_warning(trim(msg), module=this_module, procedure='orthonormalize_vector_against_basis_${type[0]}$${kind}$')
         call innerprod(coef, X, y)
         block
            class(abstract_vector_${type[0]}$${kind}$), allocatable :: proj
            call linear_combination(proj, X, coef)
            call y%sub(proj)
         end block
         alpha = y%norm()
         call y%scal(one_${type[0]}$${kind}$ / alpha)
      end if
      
      return
    end subroutine orthonormalize_vector_against_basis_${type[0]}$${kind}$

    subroutine orthonormalize_basis_against_basis_${type[0]}$${kind}$(Y, X)
      !! Orthonormalizes the `abstract_vector` basis `Y` against a basis `X` of `abstract_vector`.
      class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Y(:)
      !! Input `abstract_vector` basis to orthonormalize
      class(abstract_vector_${type[0]}$${kind}$), intent(in)    :: X(:)
      !! Input `abstract_vector` basis to orthonormalize against

      ! internals
      character*128 :: msg
      ${type}$ :: coef(size(X), size(Y))
      ${type}$ :: R(size(Y), size(Y))
      integer :: i, info
      logical :: repeat

      ! check for zero vector
      do i = 1, size(Y)
         if ( Y(i)%norm() < atol_${kind}$) then
            write(msg, *) 'Zero vector detected in input basis, column ', i
            call logger%log_error(trim(msg), module=this_module, &
                                 & procedure='orthonormalize_basis_against_basis_${type[0]}$${kind}$', & 
                                 & stat=-1, errmsg='Cannot orthonormalize.')
         end if
      end do

      ! check for colinear vectors
      R = zero_r${kind}$
      call qr(Y, R, info)
      call check_info(info, 'qr', module=this_module, procedure='orthonormalize_basis_against_basis_${type[0]}$${kind}$')
      if (info > 0) then ! colinear vectors!
         write(msg, *) 'Colinear vectors detected in input basis'
         call logger%log_error(trim(msg), module=this_module, &
                                 & procedure='orthonormalize_basis_against_basis_${type[0]}$${kind}$', & 
                                 & stat=-2, errmsg='Cannot orthonormalize.')
      end if

      ! orthogonalize
      call innerprod(coef, X, Y)
      block
         class(abstract_vector_${type[0]}$${kind}$), allocatable :: proj(:)
         call linear_combination(proj, X, coef)
         call axpby_basis(Y, one_${type[0]}$${kind}$, proj, -one_${type[0]}$${kind}$)
      end block

      do i = 1, size(Y)
         if ( Y(i)%norm() < atol_${kind}$) then ! error
            write(msg, *) 'The input vector', i, 'is in the span of X.'
            call logger%log_error(trim(msg), module=this_module, &
                                 & procedure='orthonormalize_basis_against_basis_${type[0]}$${kind}$', & 
                                 & stat=-3, errmsg='Cannot orthonormalize.')
         end if
      end do
         
      ! normalize
      R = zero_r${kind}$
      call qr(Y, R, info)
      call check_info(info, 'qr', module=this_module, procedure='orthonormalize_basis_against_basis_${type[0]}$${kind}$')
      
      return
    end subroutine orthonormalize_basis_against_basis_${type[0]}$${kind}$

    #:endfor

    !------------------------------------
    !-----     QR FACTORIZATION     -----
    !------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine qr_no_pivoting_${type[0]}$${kind}$(Q, R, info, verbosity, tol)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Array of `abstract_vector` to be orthonormalized.
        ${type}$, intent(out) :: R(:, :)
        !! Upper triangular matrix \(\mathbf{R}\) resulting from the QR factorization.
        integer, intent(out) :: info
        !! Information flag.
        logical, optional, intent(in) :: verbosity
        !! Verbosity control.
        logical                       :: verbose
        real(${kind}$), optional, intent(in) :: tol
        !! Tolerance to determine colinearity.
        real(${kind}$)                       :: tolerance

        ! Internal variables.
        ${type}$ :: beta
        integer :: idx, i, j, k, kdim, iwrk

        ! Deals with the optional args.
        verbose   = optval(verbosity, .false.)
        tolerance = optval(tol, rtol_${kind}$)

        info = 0 ; R = zero_r${kind}$
        do j = 1, size(Q)
            ! First pass
            do i = 1, j-1
                beta = Q(i)%dot(Q(j))
                call Q(j)%axpby(one_${type[0]}$${kind}$, Q(i), -beta)
                R(i, j) = beta
            enddo

            ! Second pass
            do i = 1, j-1
                beta = Q(i)%dot(Q(j))
                call Q(j)%axpby(one_${type[0]}$${kind}$, Q(i), -beta)
                R(i, j) = R(i, j) + beta
            enddo

            ! Normalize column.
            beta = Q(j)%norm()

            ! Check for breakdown.
            if (abs(beta) < tolerance) then
                if(verbose) then
                    write(output_unit, *) "INFO: Colinear columns detected."
                    write(output_unit, *) "      (j, beta) = (", j, ", ", beta, ")"
                endif
                info = j
                R(i, j) = zero_r${kind}$ ; call Q(j)%zero()
            else
                call Q(j)%scal(one_r${kind}$ / beta) ; R(j, j) = beta
            endif
        enddo

        return
    end subroutine qr_no_pivoting_${type[0]}$${kind}$

    subroutine qr_with_pivoting_${type[0]}$${kind}$(Q, R, perm, info, verbosity, tol)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Array of `abstract_vector` to be orthonormalized.
        ${type}$, intent(out) :: R(:, :)
        !! Upper triangular matrix resulting from the QR factorization.
        integer, intent(out) :: perm(size(Q))
        !! Permutation matrix.
        integer, intent(out) :: info
        !! Information flag.
        logical, optional, intent(in) :: verbosity
        !! Verbosity control.
        logical :: verbose
        real(${kind}$), optional, intent(in) :: tol

        !! Tolerance to detect colinearity.
        real(${kind}$) :: tolerance

        ! Internal variables
        ${type}$ :: beta
        integer :: idx, i, j, k, kdim, iwrk
        integer :: idxv(1)
        ${type}$  :: Rii(size(Q))

        info = 0 ; kdim = size(Q)
        R = zero_r${kind}$ ; Rii = zero_r${kind}$
        
        ! Deals with the optional arguments.
        verbose = optval(verbosity, .false.)
        tolerance = optval(tol, rtol_${kind}$)

        ! Initialize diagonal entries.
        do i = 1, kdim
            perm(i) = i
            Rii(i) = Q(i)%dot(Q(i))
        enddo

        qr_step: do j = 1, kdim
            idxv = maxloc(abs(Rii)) ; idx = idxv(1)
            if (abs(Rii(idx)) < tolerance) then
                do i = j, kdim
                    call Q(i)%rand(.false.)
                    ! Orthogonalize against existing columns
                    do k = 1, i-1
                        beta = Q(i)%dot(Q(k))
                        call Q(i)%axpby(one_${type[0]}$${kind}$, Q(k), -beta)
                    enddo
                    beta = Q(i)%norm() ; call Q(i)%scal(one_r${kind}$ / beta)
                enddo
                info = j
                exit qr_step
            endif

            call swap_columns(Q, R, Rii, perm, j, idx)

            ! Normalize column.
            beta = Q(j)%norm()

            if (abs(beta) < tolerance) then
               info = j
                R(j, j) = zero_r${kind}$ ; call Q(j)%zero()
            else
                R(j, j) = beta ; call Q(j)%scal(one_r${kind}$ / beta)
            endif

            ! Orthonormalize all columns against new vector (MGS)
            do i = j+1, kdim
                beta = Q(j)%dot(Q(i))
                call Q(i)%axpby(one_${type[0]}$${kind}$, Q(j), -beta)
                R(j, i) = beta
            enddo

            ! Update Rii
            Rii(j) = zero_r${kind}$
            do i = j+1, kdim
                Rii(i) = Rii(i) - R(j, i)**2
            enddo

        enddo qr_step

        return
    end subroutine qr_with_pivoting_${type[0]}$${kind}$

    subroutine swap_columns_${type[0]}$${kind}$(Q, R, Rii, perm, i, j)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Vector basis whose i-th and j-th columns need swapping.
        ${type}$, intent(inout) :: R(:, :)
        !! Upper triangular matrix resulting from QR.
        ${type}$, intent(inout) :: Rii(:)
        !! Diagonal entries of R.
        integer, intent(inout) :: perm(:)
        !! Column ordering.
        integer, intent(in) :: i, j
        !! Index of the columns to be swapped.

        ! Internal variables.
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Qwrk
        ${type}$, allocatable :: Rwrk(:)
        integer :: iwrk, m, n

        ! Sanity checks.
        m = size(Q) ; n = min(i, j) - 1

        ! Allocations.
        allocate(Qwrk, source=Q(1)) ; call Qwrk%zero()
        allocate(Rwrk(1:max(1, n))) ; Rwrk = zero_r${kind}$

        ! Swap columns.
        call Qwrk%axpby(zero_${type[0]}$${kind}$, Q(j), one_${type[0]}$${kind}$)
        call Q(j)%axpby(zero_${type[0]}$${kind}$, Q(i), one_${type[0]}$${kind}$)
        call Q(i)%axpby(zero_${type[0]}$${kind}$, Qwrk, one_${type[0]}$${kind}$)
        
        Rwrk(1) = Rii(j); Rii(j) = Rii(i); Rii(i) = Rwrk(1)
        iwrk = perm(j); perm(j) = perm(i) ; perm(i) = iwrk

        if (n > 0) then
            Rwrk = R(1:n, j) ; R(1:n, j) = R(1:n, i) ; R(1:n, i) = Rwrk
        endif

        return
    end subroutine swap_columns_${type[0]}$${kind}$

    subroutine apply_permutation_matrix_${type[0]}$${kind}$(Q, perm)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Basis vectors to be permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Qwrk(:)

        allocate(Qwrk, source=Q)
        do i = 1, size(perm)
            call Q(i)%axpby(zero_${type[0]}$${kind}$, Qwrk(perm(i)), one_${type[0]}$${kind}$)
        enddo

        return
    end subroutine apply_permutation_matrix_${type[0]}$${kind}$

    subroutine apply_inverse_permutation_matrix_${type[0]}$${kind}$(Q, perm)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: Q(:)
        !! Basis vectors to be (un-) permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        integer :: inv_perm(size(perm))
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Qwrk(:)

        allocate(Qwrk, source=Q) ; inv_perm = 0

        ! Inverse permutation vector.
        do i = 1, size(perm)
            inv_perm(perm(i)) = i
        enddo

        ! Undo permutation.
        do i = 1, size(perm)
            call Q(i)%axpby(zero_${type[0]}$${kind}$, Qwrk(inv_perm(i)), one_${type[0]}$${kind}$)
        enddo

        return
    end subroutine apply_inverse_permutation_matrix_${type[0]}$${kind}$

    subroutine apply_permutation_matrix_array_${type[0]}$${kind}$(Q, perm)
        ${type}$, intent(inout) :: Q(:, :)
        !! Basis vectors to be permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        ${type}$, allocatable :: Qwrk(:, :)

        Qwrk = Q
        do i = 1, size(perm)
            Q(:, i) = Qwrk(:, perm(i))
        enddo

        return
    end subroutine apply_permutation_matrix_array_${type[0]}$${kind}$

    subroutine apply_inverse_permutation_matrix_array_${type[0]}$${kind}$(Q, perm)
        ${type}$, intent(inout) :: Q(:, :)
        !! Basis vectors to be (un-) permuted.
        integer, intent(in) :: perm(:)
        !! Permutation matrix (vector representation).

        ! Internal variables.
        integer :: i
        integer :: inv_perm(size(perm))
        ${type}$, allocatable :: Qwrk(:, :)

        Qwrk = Q ; inv_perm = 0

        ! Inverse permutation vector.
        do i = 1, size(perm)
            inv_perm(perm(i)) = i
        enddo

        ! Undo permutation.
        do i = 1, size(perm)
            Q(:, i) = Qwrk(:, inv_perm(i))
        enddo

        return
    end subroutine apply_inverse_permutation_matrix_array_${type[0]}$${kind}$


    #:endfor

    !-----------------------------------------
    !-----     ARNOLDI FACTORIZATION     -----
    !-----------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine arnoldi_${type[0]}$${kind}$(A, X, H, info, kstart, kend, verbosity, tol, transpose, blksize)
        class(abstract_linop_${type[0]}$${kind}$), intent(in) :: A
        !! Linear operator to be factorized.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        !! Orthogonal basis for the generated Krylov subspace.
        ${type}$, intent(inout) :: H(:, :)
        !! Upper Hessenberg matrix.
        integer, intent(out) :: info
        !! Information flag.
        integer, optional, intent(in) :: kstart
        !! Starting index for the Arnoldi factorization (default 1).
        integer, optional, intent(in) :: kend
        !! Final index for the Arnoldi factorization (default `size(X)-1`)
        logical, optional, intent(in) :: verbosity
        !! Verbosity control.
        logical, optional, intent(in) :: transpose
        !! Whether \( \mathbf{A} \) is being transposed or not (default `.false.`)
        real(${kind}$), optional, intent(in) :: tol
        !! Tolerance to determine whether an invariant subspace has been computed or not.
        integer, optional, intent(in) :: blksize
        !! Block size for block Arnoldi (default 1).

        ! Internal variables.
        integer :: k_start, k_end, p
        logical :: verbose, trans
        real(${kind}$) :: tolerance
        real(${kind}$) :: beta
        ${type}$, allocatable :: res(:)
        integer, allocatable :: perm(:)
        integer :: k, i, kdim, kpm, kp, kpp

        ! Deals with optional non-unity blksize and allocations.
        p = optval(blksize, 1) ; allocate(res(1:p)) ; res = zero_r${kind}$
        allocate(perm(1:size(H, 2))) ; perm = 0 ; info = 0

        ! Check dimensions.
        kdim = (size(X) - p) / p

        ! Deal with the other optional args.
        k_start = optval(kstart, 1) ; k_end = optval(kend, kdim)
        verbose   = optval(verbosity, .false.)
        tolerance = optval (tol, atol_${kind}$)
        trans     = optval(transpose, .false.)

        ! Arnoldi factorization.
        blk_arnoldi: do k = k_start, k_end
            ! Counters
            kpm = (k - 1) * p ; kp = kpm + p ; kpp = kp + p

            ! Matrix-vector product.
            if (trans) then
                do i = 1, p
                    call A%rmatvec(X(kpm+i), X(kp+i))
                enddo
            else
                do i = 1, p
                    call A%matvec(X(kpm+i), X(kp+i))
                enddo
            endif

            ! Update Hessenberg matrix and orthogonalize w.r.t. previous vectors.
            call update_hessenberg_matrix_${type[0]}$${kind}$(H, X, k, p)

            ! Orthogonalize current blk vectors.
            call qr(X(kp+1:kpp), H(kp+1:kpp, kpm+1:kp), info)
            call check_info(info, 'qr', module=this_module, procedure='arnoldi_${type[0]}$${kind}$')

            ! Extract residual norm (smallest diagonal element of H matrix).
            res = zero_r${kind}$
            do i = 1, p
                res(i) = H(kp+i, kpm+i)
            enddo
            beta = minval(abs(res))

            ! Exit Arnoldi loop if needed.
            if (beta < tolerance) then
                ! Dimension of the computed invariant subspace.
                info = kp
                ! Exit the Arnoldi iteration.
                exit blk_arnoldi
            endif

        enddo blk_arnoldi

        return
    end subroutine arnoldi_${type[0]}$${kind}$

    subroutine update_hessenberg_matrix_${type[0]}$${kind}$(H, X, k, blksize)
        integer, intent(in) :: k
        ${type}$, intent(inout) :: H(:, :)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        integer, optional, intent(in) :: blksize

        ! Internal variables.
        ${type}$, allocatable :: wrk(:, :)
        integer :: p, kpm, kp, kpp, i, j

        ! Deals with optional non-unity block size.
        p = optval(blksize, 1)

        ! Counters and allocations.
        kpm = (k - 1) * p ; kp = kpm + p ; kpp = kp + p
        allocate(wrk(1:kp, 1:p)) ; wrk = zero_r${kind}$

        ! Orthogonalize residual vector w.r.t. to previous computed Krylov vectors.
        call innerprod(H(1:kp, kpm+1:kp), X(1:kp), X(kp+1:kpp))
        do i = 1, p
            do j = 1, kp
                call X(kp+i)%axpby(one_${type[0]}$${kind}$, X(j), -H(j, kpm+i))
            enddo
        enddo

        call innerprod(wrk, X(1:kp), X(kp+1:kpp))
        do i = 1, p
            do j = 1, kp
                call X(kp+i)%axpby(one_${type[0]}$${kind}$, X(j), -wrk(j, i))
            enddo
        enddo

        ! Update Hessenberg matrix with the second pass correction.
        H(1:kp, kpm+1:kp) = H(1:kp, kpm+1:kp) + wrk

        return
    end subroutine update_hessenberg_matrix_${type[0]}$${kind}$

    #:endfor


    !---------------------------------------------
    !-----     LANCZOS BIDIAGONALIZATION     -----
    !---------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine lanczos_bidiagonalization_${type[0]}$${kind}$(A, U, V, B, info, kstart, kend, verbosity, tol)
        class(abstract_linop_${type[0]}$${kind}$), intent(in) :: A
        !! Linear operator to be factorized.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: U(:)
        !! Orthonormal basis for the column span of \(\mathbf{A}\). On entry, `U(1)` needs to be set to
        !! the starting Krylov vector.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: V(:)
        !! Orthonormal basis for the row span of \(\mathbf{A}\).
        ${type}$, intent(inout) :: B(:, :)
        !! Bidiagonal matrix.
        integer, intent(out) :: info
        !! Information flag.
        integer, optional, intent(in) :: kstart
        !! Starting index for the Lanczos factorization (default 1).
        integer, optional, intent(in) :: kend
        !! Final index for the Lanczos factorization (default 1).
        logical, optional, intent(in) :: verbosity
        !! Verbosity control (default `.false.`)
        real(${kind}$), optional, intent(in) :: tol
        !! Tolerance to determine whether invariant subspaces have been computed or not.

        ! Internal variables.
        integer :: k_start, k_end
        logical :: verbose
        real(${kind}$) :: tolerance
        ${type}$ :: alpha, beta, gamma
        integer :: i, j, k, kdim


        info = 0

        ! Krylov subspace dimension.
        kdim = size(U) - 1

        ! Deals with the optional args.
        k_start = optval(kstart, 1)
        k_end   = optval(kend, kdim)
        verbose = optval(verbosity, .false.)
        tolerance = optval(tol, atol_${kind}$)

        ! Lanczos bidiagonalization.
        lanczos : do k = k_start, k_end
            ! Transpose matrix-vector product.
            call A%rmatvec(U(k), V(k))

            ! Full reorthogonalization of the right Krylov subspace.
            do j = 1, k-1
                gamma = V(j)%dot(V(k))
                call V(k)%axpby(one_${type[0]}$${kind}$, V(j), -gamma)
            enddo

            ! Normalization step.
            alpha = V(k)%norm() ; B(k, k) = alpha
            if (abs(alpha) > tolerance) then
                call V(k)%scal(one_${type[0]}$${kind}$/alpha)
            else
                info = k
                exit lanczos
            endif

            ! Matrix-vector product.
            call A%matvec(V(k), U(k+1))

            ! Full re-orthogonalization of the left Krylov subspace.
            do j = 1, k
                gamma = U(j)%dot(U(k+1))
                call U(k+1)%axpby(one_${type[0]}$${kind}$, U(j), -gamma)
           enddo

            ! Normalization step
            beta = U(k+1)%norm() ; B(k+1, k) = beta
            if (abs(beta) > tolerance) then
                call U(k+1)%scal(one_${type[0]}$${kind}$ / beta)
            else
                info = k
                exit lanczos
            endif

        enddo lanczos

        return
    end subroutine lanczos_bidiagonalization_${type[0]}$${kind}$

    #:endfor


    !----------------------------------------------
    !-----     LANCZOS TRIDIAGONALIZATION     -----
    !----------------------------------------------
    
    #:for kind, type in RC_KINDS_TYPES
    subroutine lanczos_tridiagonalization_${type[0]}$${kind}$(A, X, T, info, kstart, kend, verbosity, tol)
        #:if type[0] == "r"
        class(abstract_sym_linop_${type[0]}$${kind}$), intent(in) :: A
        #:else
        class(abstract_hermitian_linop_${type[0]}$${kind}$), intent(in) :: A
        #:endif
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        ${type}$, intent(inout) :: T(:, :)
        integer, intent(out) :: info
        integer, optional, intent(in) :: kstart
        integer, optional, intent(in) :: kend
        logical, optional, intent(in) :: verbosity
        real(${kind}$), optional, intent(in) :: tol

        ! Internal variables.
        integer :: k_start, k_end
        logical :: verbose
        real(${kind}$) :: tolerance
        real(${kind}$) :: beta
        integer :: i, j, k, kdim

        ! Deal with optional args.
        kdim = size(X) - 1
        k_start = optval(kstart, 1)
        k_end = optval(kend, kdim)
        verbose = optval(verbosity, .false.)
        tolerance = optval(tol, atol_${kind}$)

        ! Lanczos tridiagonalization.
        lanczos: do k = k_start, k_end
            ! Matrix-vector product.
            call A%matvec(X(k), X(k+1))
            ! Update tridiagonal matrix.
            call update_tridiag_matrix_${type[0]}$${kind}$(T, X, k)
            beta = X(k+1)%norm() ; T(k+1, k) = beta

            ! Exit Lanczos loop if needed.
            if (beta < tolerance) then
                ! Dimension of the computed invariant subspace.
                info = k
                ! Exit the Lanczos iteration.
                exit lanczos
            else
                ! Normalize the new Krylov vector.
                call X(k+1)%scal(one_${type[0]}$${kind}$ / beta)
            endif
        enddo lanczos

        return
    end subroutine lanczos_tridiagonalization_${type[0]}$${kind}$

    subroutine update_tridiag_matrix_${type[0]}$${kind}$(T, X, k)
        integer, intent(in) :: k
        ${type}$, intent(inout) :: T(:, :)
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)

        ! Internal variables.
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: wrk
        integer :: i
        ${type}$ :: alpha

        ! Orthogonalize residual w.r.t. previously computed Krylov vectors.
        do i = max(1, k-1), k
            alpha = X(i)%dot(X(k+1)) ; call X(k+1)%axpby(one_${type[0]}$${kind}$, X(i), -alpha)
            ! Update tridiag. matrix.
            T(i, k) = alpha
        enddo

        ! Full re-orthogonalization.
        do i = 1, k
            alpha = X(i)%dot(X(k+1)) ; call X(k+1)%axpby(one_${type[0]}$${kind}$, X(i), -alpha)
        enddo

        return
    end subroutine update_tridiag_matrix_${type[0]}$${kind}$

    #:endfor

    !----------------------------------------
    !-----     KRYLOV-SCHUR RESTART     -----
    !----------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    subroutine krylov_schur_${type[0]}$${kind}$(n, X, H, select_eigs)
        integer, intent(out) :: n
        !! Number eigenvalues that have been moved to the upper
        !! left block of the Schur factorization of `H`.
        class(abstract_vector_${type[0]}$${kind}$), intent(inout) :: X(:)
        !! Krylov basis.
        ${type}$, intent(inout) :: H(:, :)
        !! Upper Hessenberg matrix.
        interface
            function selector(lambda) result(out)
                import ${kind}$
                complex(${kind}$), intent(in) :: lambda(:)
                logical                       :: out(size(lambda))
            end function selector
        end interface
        procedure(selector) :: select_eigs
        !! Procedure to select the eigenvalues to move in the upper left-block.

        !--------------------------------------
        !-----     Internal variables     -----
        !--------------------------------------

        integer :: i, j, kdim
        
        ! Schur-related.
        ${type}$ :: Z(size(H, 2), size(H, 2))
        complex(${kind}$) :: eigvals(size(H, 2))
        logical :: selected(size(H, 2))
       
        ! Krylov subspace dimension.
        kdim = size(X)-1

        ! Allocate and initializes variables.
        eigvals = zero_${type[0]}$${kind}$ ; Z = zero_${type[0]}$${kind}$
        selected = .false.

        ! Schur decomposition of the Hessenberg matrix.
        call schur(H(:size(H, 2), :), Z, eigvals)

        ! Eigenvalue selection of the upper left block.
        selected = select_eigs(eigvals) ; n = count(selected)

        ! Re-order the Schur decomposition and Schur basis.
        call ordschur(H(:kdim, :), Z, selected)

        ! Update the Hessenberg matrix and Krylov basis.
        block
        ${type}$ :: b(size(H, 2))
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Xwrk(:)
        
        ! Update the Krylov basis.
        call linear_combination(Xwrk, X(:size(H, 2)), Z(:, :n))

        do i = 1, n
            call X(i)%axpby(zero_${type[0]}$${kind}$, Xwrk(i), one_${type[0]}$${kind}$)
        enddo

        call X(n+1)%axpby(zero_${type[0]}$${kind}$, X(kdim+1), one_${type[0]}$${kind}$)

        do i = n+2, size(X)
            call X(i)%zero()
        enddo

        ! Update the Hessenberg matrix.
        b = matmul(H(kdim+1, :), Z)
        H(n+1, :) = b
        H(n+2:, :) = zero_${type[0]}$${kind}$
        H(:, n+1:) = zero_${type[0]}$${kind}$
        end block

        return
    end subroutine krylov_schur_${type[0]}$${kind}$

    #:endfor

end module lightkrylov_BaseKrylov
