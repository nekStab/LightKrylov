#:include "../../../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
submodule (lightkrylov_iterativesolvers) svds_solver
    use stdlib_linalg, only: hermitian, svd
    implicit none
    character(len=*), parameter :: svds_output = 'svds_output.txt'
contains

    !----- Utility functions -----
    #:for kind, type in RC_KINDS_TYPES
    elemental pure function svd_residual_${type[0]}$${kind}$(beta, x) result(residual)
        !! Computes the residual associated with a Ritz eigenpair.
        ${type}$, intent(in) :: beta
        !! Norm of the residual Krylov vector.
        ${type}$, intent(in) :: x
        !! Last entry of the low-dimensional Ritz eigenvector.
        real(${kind}$) :: residual
        !! Residual associated to the corresponding Ritz eigenpair.
        residual = abs(beta*x)
        return
    end function svd_residual_${type[0]}$${kind}$
    #:endfor

    !----------------------------------------
    !-----     ITERATIVE SVD SOLVER     -----
    !----------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    module procedure svds_${type[0]}$${kind}$
        ! Left and right Krylov subspaces.
        integer :: kdim_
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: Uwrk(:), Vwrk(:)
        ! Bidiagonal matrix.
        ${type}$, allocatable :: B(:, :)
        ! Working arrays for the singular vectors and singular values.
        real(${kind}$), allocatable :: svdvals_wrk(:)
        ${type}$, allocatable :: umat(:, :), vmat(:, :)
        real(${kind}$), allocatable :: residuals_wrk(:)
        ! Miscellaneous.
        character(len=*), parameter :: this_procedure = 'svds_${type[0]}$${kind}$'
        integer :: nsv, conv
        integer :: i, j, k
        real(${kind}$) :: tol, u0_norm
        logical :: outpost
        character(len=256) :: msg

        if (time_lightkrylov()) call timer%start(this_procedure)
        ! Deals with the optional arguments.
        nsv = size(U)
        kdim_   = optval(kdim, 4*nsv)
        tol     = optval(tolerance, rtol_${kind}$)
        outpost = optval(write_intermediate, .false.)

        ! Allocate working variables.
        allocate(Uwrk(kdim_+1), mold=U(1)) ; call zero_basis(Uwrk)
        if (present(u0)) then
            call copy(Uwrk(1), u0)
            u0_norm = u0%norm(); call Uwrk(1)%scal(one_${type[0]}$${kind}$/u0_norm)
        else
            call Uwrk(1)%rand(.true.)
        endif
        allocate(Vwrk(kdim_+1), mold=V(1)) ; call zero_basis(Vwrk)
        allocate(svdvals_wrk(kdim_)) ; svdvals_wrk = 0.0_${kind}$
        allocate(umat(kdim_, kdim_)) ; umat = 0.0_${kind}$
        allocate(vmat(kdim_, kdim_)) ; vmat = 0.0_${kind}$
        allocate(residuals_wrk(kdim_)) ; residuals_wrk = 0.0_${kind}$
        allocate(B(kdim_+1, kdim_)) ; B = 0.0_${kind}$

        info = 0

        ! Ritz singular triplets computation.
        lanczos_iter : do k = 1, kdim_
            ! Lanczos bidiag. step.
            call bidiagonalization(A, Uwrk, Vwrk, B, info, kstart=k, kend=k, tol=tol)
            call check_info(info, 'bidiagonalization', this_module, this_procedure)

            ! SVD of the k x k bidiagonal matrix and residual computation.
            svdvals_wrk = 0.0_${kind}$ ; umat = 0.0_${kind}$ ; vmat = 0.0_${kind}$

            call svd(B(:k, :k), svdvals_wrk(:k), umat(:k, :k), vmat(:k, :k))
            vmat(:k, :k) = hermitian(vmat(:k, :k))

            residuals_wrk(:k) = svd_residual_${type[0]}$${kind}$(B(k+1, k), vmat(k, :k))

            ! Check for convergence.
            conv = count(residuals_wrk(:k) < tol)
            write(msg,'(I0,A,I0,A,I0,A)') conv, '/', nsv, ' singular values converged after ', k, &
                            & ' iterations of the Lanczos process.'
            call log_information(msg, this_module, this_procedure)
            if (outpost) call write_results_r${kind}$(svds_output, svdvals_wrk(:k), residuals_wrk(:k), tol)
            if (conv >= nsv) exit lanczos_iter
        enddo lanczos_iter

        !--------------------------------
        !-----     POST-PROCESS     -----
        !--------------------------------

        ! Singular values.
        S = svdvals_wrk(:nsv) ; residuals = residuals_wrk(:nsv)

        ! Singular vectors.
        k = min(k, kdim_) ; info = k
        call zero_basis(U) ; call zero_basis(V)
        do i = 1, nsv
            do j = 1, k
                call U(i)%axpby(umat(j, i), Uwrk(j), one_${type[0]}$${kind}$)
                call V(i)%axpby(vmat(j, i), Vwrk(j), one_${type[0]}$${kind}$)
            enddo
        enddo
        if (time_lightkrylov()) call timer%stop(this_procedure)
        
        return
    end procedure

    #:endfor
end submodule

