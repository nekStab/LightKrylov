#:include "../../../include/common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
submodule (lightkrylov_iterativesolvers) gmres_solver
    use stdlib_strings, only: padr
    use stdlib_linalg, only: lstsq, norm
    implicit none
contains

    !----------------------------------------
    !-----     Options and Metadata     -----
    !----------------------------------------

    #:for kind in REAL_KINDS
    module procedure print_gmres_${kind}$
        ! internals
        integer :: i
        logical :: ifreset, ifverbose
        character(len=128) :: msg

        ifreset   = optval(reset_counters, .false.)
        ifverbose = optval(verbose, .false.)
  
        write(msg,'(A30,I6,"  (",I6,"/",I3,")")') padr('Iterations   (inner/outer): ', 30), &
                  & self%n_iter, self%n_inner, self%n_outer
        call log_message(msg, module=this_module, procedure='gmres_metadata')
        if (ifverbose) then
            write(msg,'(14X,A15)') 'Residual'
            call log_message(msg, module=this_module, procedure='gmres_metadata')
            write(msg,'(A14,E15.8)') '   INIT:', self%res(1)
            call log_message(msg, module=this_module, procedure='gmres_metadata')
            do i = 1, self%n_iter
               write(msg,'(A,I3,A,E20.8)') '   Step ', i, ': ', self%res(i)
               call log_message(msg, module=this_module, procedure='gmres_metadata')
            end do
        else
            write(msg,'(A30,I20)') padr('Number of records: ', 30), size(self%res)
            call log_message(msg, module=this_module, procedure='gmres_metadata')
            write(msg,'(A30,E20.8)') padr('Residual: ', 30), self%res(size(self%res))
            call log_message(msg, module=this_module, procedure='gmres_metadata')
        end if
        if (self%converged) then
            call log_message('Status: CONVERGED', module=this_module, procedure='gmres_metadata')
        else
            call log_message('Status: NOT CONVERGED', module=this_module, procedure='gmres_metadata')
        end if
        if (ifreset) call self%reset()
        return
    end procedure

    module procedure reset_gmres_${kind}$
        self%n_iter = 0
        self%n_inner = 0
        self%n_outer = 0
        self%converged = .false.
        self%info = 0
        if (allocated(self%res)) deallocate(self%res)
        return
    end procedure
    #:endfor

    !----------------------------------------------------
    !-----     GMRES SOLVERS FOR ABSTRACT TYPES     -----
    !----------------------------------------------------

    #:for kind, type in RC_KINDS_TYPES
    module procedure gmres_${type[0]}$${kind}$
       ! Options.
        integer :: kdim, maxiter
        real(${kind}$) :: tol, rtol_, atol_
        logical :: trans
        type(gmres_${kind}$_opts)     :: opts
        type(gmres_${kind}$_metadata) :: gmres_meta

        ! Krylov subspace
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: V(:)
        ! Hessenberg matrix.
        ${type}$, allocatable :: H(:, :)
        ! Least-squares variables.
        ${type}$, allocatable :: y(:), e(:)
        real(${kind}$) :: beta

        ! Preconditioner
        logical :: has_precond
        class(abstract_precond_${type[0]}$${kind}$), allocatable :: precond

        ! Miscellaneous.
        integer :: i, k
        class(abstract_vector_${type[0]}$${kind}$), allocatable :: dx, wrk
        character(len=256) :: msg

        if (time_lightkrylov()) call timer%start('gmres_${type[0]}$${kind}$')
        ! Deals with the optional args.
        rtol_ = optval(rtol, rtol_${kind}$)
        atol_ = optval(atol, atol_${kind}$)
        if (present(options)) then
            select type (options)
            type is (gmres_${kind}$_opts)
                opts = options
            end select
        else
            opts = gmres_${kind}$_opts()
        endif

        kdim = opts%kdim ; maxiter = opts%maxiter
        tol = atol_ + rtol_ * b%norm()
        trans = optval(transpose, .false.)

        ! Deals with the preconditioner.
        has_precond = optval(present(preconditioner), .false.)
        if (has_precond) allocate(precond, source=preconditioner)

        ! Initialize working variables.
        allocate(wrk, mold=b) ; call wrk%zero()
        allocate(V(kdim+1), mold=b) ; call zero_basis(V)
        allocate(H(kdim+1, kdim)) ; H = 0.0_${kind}$
        allocate(y(kdim)) ; y = 0.0_${kind}$
        allocate(e(kdim+1)) ; e = 0.0_${kind}$

        ! Initialize metadata and & reset matvec counter
        gmres_meta = gmres_${kind}$_metadata()
        call A%reset_counter(trans, 'gmres%init')

        info = 0

        ! Initial Krylov vector.
        if (x%norm() > 0) then
            if (trans) then
                call A%apply_rmatvec(x, V(1))
            else
                call A%apply_matvec(x, V(1))
            endif
        endif

        call V(1)%sub(b) ; call V(1)%chsgn()
        beta = V(1)%norm() ; call V(1)%scal(one_${type[0]}$${kind}$/beta)
        allocate(gmres_meta%res(1)); gmres_meta%res(1) = abs(beta)

        write(msg,'(2(A,E11.4))') 'GMRES(k)   init step     : |res|= ', &
                    & abs(beta), ', tol= ', tol
        call log_information(msg, module=this_module, procedure='gmres_${type[0]}$${kind}$')

        ! Iterative solver.
        gmres_iter : do i = 1, maxiter
            ! Zero-out variables.
            H = 0.0_${kind}$ ; y = 0.0_${kind}$ ; e = 0.0_${kind}$ ; e(1) = beta

            ! Arnoldi factorization.
            arnoldi_fact: do k = 1, kdim
                ! Preconditioner.
                wrk = V(k) ; if (has_precond) call precond%apply(wrk, k, beta, tol)

                ! Matrix-vector product.
                if (trans) then
                    call A%apply_rmatvec(wrk, V(k+1))
                else
                    call A%apply_matvec(wrk, V(k+1))
                endif

                ! Double Gram-Schmid orthogonalization
                call double_gram_schmidt_step(V(k+1), V(:k), info, if_chk_orthonormal=.false., beta=H(:k, k))
                call check_info(info, 'double_gram_schmidt_step', module=this_module, procedure='gmres_${type[0]}$${kind}$')

                ! Update Hessenberg matrix and normalize residual Krylov vector.
                H(k+1, k) = V(k+1)%norm()
                if (abs(H(k+1, k)) > tol) call V(k+1)%scal(one_${type[0]}$${kind}$ / H(k+1, k))

                ! Least-squares problem.
                y(:k) = lstsq(H(:k+1, :k), e(:k+1))

                ! Compute residual.
                beta = norm(e(:k+1) - matmul(H(:k+1, :k), y(:k)), 2)

                ! Save metadata.
                gmres_meta%n_iter  = gmres_meta%n_iter + 1
                gmres_meta%n_inner = gmres_meta%n_inner + 1
                gmres_meta%res = [ gmres_meta%res, abs(beta) ]

                ! Check convergence.
                write(msg,'(A,I3,2(A,E11.4))') 'GMRES(k)   inner step ', k, ': |res|= ', &
                            & abs(beta), ', tol= ', tol
                call log_information(msg, module=this_module, procedure='gmres_${type[0]}$${kind}$')
                if (abs(beta) <= tol) then
                    gmres_meta%converged = .true.
                    exit arnoldi_fact
                endif
            enddo arnoldi_fact

            ! Update solution.
            k = min(k, kdim) ; call linear_combination(dx, V(:k), y(:k))
            if (has_precond) call precond%apply(dx) ; call x%add(dx)

            ! Recompute residual for sanity check.
            if (trans) then
                call A%apply_rmatvec(x, v(1))
            else
                call A%apply_matvec(x, v(1))
            endif
            call v(1)%sub(b) ; call v(1)%chsgn()

            ! Initialize new starting Krylov vector if needed.
            beta = v(1)%norm() ; call v(1)%scal(one_${type[0]}$${kind}$ / beta)

            ! Save metadata.
            gmres_meta%n_iter  = gmres_meta%n_iter + 1
            gmres_meta%n_outer = gmres_meta%n_outer + 1
            gmres_meta%res = [ gmres_meta%res, abs(beta) ]

            write(msg,'(A,I3,2(A,E11.4))') 'GMRES(k) outer step   ', i, ': |res|= ', &
                            & abs(beta), ', tol= ', tol
            call log_information(msg, module=this_module, procedure='gmres_${type[0]}$${kind}$')

            ! Exit gmres if desired accuracy is reached.
            if (abs(beta) <= tol) then
               gmres_meta%converged = .true.
               exit gmres_iter
            end if
        enddo gmres_iter

        ! Returns the number of iterations.
        info = gmres_meta%n_iter
        gmres_meta%info = info

        if (opts%if_print_metadata) call gmres_meta%print()

        ! Set metadata output
        if (present(meta)) then
           select type(meta)
                 type is (gmres_${kind}$_metadata)
                    meta = gmres_meta
           end select
        end if

        call A%reset_counter(trans, 'gmres%post')
        if (time_lightkrylov()) call timer%stop('gmres_${type[0]}$${kind}$')
        
        return
    end procedure 
    #:endfor

end submodule
